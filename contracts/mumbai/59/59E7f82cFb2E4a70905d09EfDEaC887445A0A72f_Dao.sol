// SPDX-License-Identifier: MIT

pragma solidity ^0.8.7;


interface IdaoContract {
        function balanceOf(address, uint256) external view returns (uint256);
    }

contract Dao {

    address public owner;
    uint256 nextProposal;
    uint256[] public validTokens;
    IdaoContract daoContract;

    constructor(){
        owner = msg.sender;
        nextProposal = 1;
        daoContract = IdaoContract(0x2953399124F0cBB46d2CbACD8A89cF0599974963);
        //-------------------------0x2953399124F0cBB46d2CbACD8A89cF0599974963
        validTokens = [51540308150861228635517323782287372650707639654396802749657747226746305904641];
    }

    // Proposals structure

    struct proposal{
        uint256 id;
        bool exists;
        string description;
        uint deadline;
        address[] canVote;
        mapping(address => bool) voteStatus;
        uint256 votesUp;
        uint256 votesDown;
        uint256 maxVotes;
        bool countConducted;
        bool passed;
    }

    // Making all the proposal public

    mapping(uint256 => proposal) public Proposals;

    // Events created so that moralis can hear it and show us results and data

    event proposalCreated(
        uint256 id,
        string description,
        uint256 maxVotes,
        address proposer
    );

    event newVote(
        uint256 votesUp,
        uint256 votesDown,
        address voter,
        uint256 proposal,
        bool votedFor
    );

    event proposalCount(
        uint256 id,
        bool passed
    );

    // Check if the address holds any NFT from the valid NFT array list

    function checkProposalEligibility(address _proposalist) private view returns (bool){
        for(uint i = 0; i < validTokens.length; i++){
            if(daoContract.balanceOf(_proposalist, validTokens[i]) >= 1){
                return true;
            }
        }
        return false;
    }

    // Check if the address is eligible for vote

    function checkVoteEligibility(uint256 _id, address _voter) private view returns (bool){
        for (uint256 i = 0; i < Proposals[_id].canVote.length; i++) {
            if (Proposals[_id].canVote[i] == _voter) {
            return true;
            }
        }
        return false;
    }

    // Creating a proposal

    function createProposal(string memory _description, address[] memory _canVote) public {
        require(checkProposalEligibility(msg.sender), "Only NFT holders can put forth Proposals");

        // Creating a new instance of proposal (newProposal) struct and storing it in storage

        proposal storage newProposal = Proposals[nextProposal];
        newProposal.id = nextProposal;
        newProposal.exists = true;
        newProposal.description = _description;
        newProposal.deadline = block.number + 100;
        newProposal.canVote = _canVote;
        newProposal.maxVotes = _canVote.length;

        // Emiting event
        emit proposalCreated(nextProposal, _description, _canVote.length, msg.sender);

        nextProposal++;
    }

    // Casting vote


    function voteOnProposal(uint256 _id, bool _vote) public {
        require(Proposals[_id].exists, "This Proposal does not exist");
        require(checkVoteEligibility(_id, msg.sender), "You can not vote on this Proposal");
        require(!Proposals[_id].voteStatus[msg.sender], "You have already voted on this Proposal");
        require(block.number <= Proposals[_id].deadline, "The deadline has passed for this Proposal");

        proposal storage p = Proposals[_id];

        if(_vote) {
            p.votesUp++;
        }else{
            p.votesDown++;
        }

        // Change vote status to true to avoid multiple votes from same address
        p.voteStatus[msg.sender] = true;

        emit newVote(p.votesUp, p.votesDown, msg.sender, _id, _vote);
        
    }

    // Counting the counts

    function countVotes(uint256 _id) public {
        require(msg.sender == owner, "Only Owner Can Count Votes");
        require(Proposals[_id].exists, "This Proposal does not exist");
        require(block.number > Proposals[_id].deadline, "Voting has not concluded");
        require(!Proposals[_id].countConducted, "Count already conducted");

        proposal storage p = Proposals[_id];
        
        if(Proposals[_id].votesDown < Proposals[_id].votesUp){
            p.passed = true;            
        }

        p.countConducted = true;

        emit proposalCount(_id, p.passed);
    }


    function addTokenId(uint256 _tokenId) public {
        require(msg.sender == owner, "Only Owner Can Add Tokens");

        validTokens.push(_tokenId);
    }
    
}