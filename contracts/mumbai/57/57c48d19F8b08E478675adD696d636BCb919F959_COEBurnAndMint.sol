// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./ConfirmedOwnerWithProposal.sol";

/**
 * @title The ConfirmedOwner contract
 * @notice A contract with helpers for basic contract ownership.
 */
contract ConfirmedOwner is ConfirmedOwnerWithProposal {
  constructor(address newOwner) ConfirmedOwnerWithProposal(newOwner, address(0)) {}
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "./interfaces/OwnableInterface.sol";

/**
 * @title The ConfirmedOwner contract
 * @notice A contract with helpers for basic contract ownership.
 */
contract ConfirmedOwnerWithProposal is OwnableInterface {
  address private s_owner;
  address private s_pendingOwner;

  event OwnershipTransferRequested(address indexed from, address indexed to);
  event OwnershipTransferred(address indexed from, address indexed to);

  constructor(address newOwner, address pendingOwner) {
    require(newOwner != address(0), "Cannot set owner to zero");

    s_owner = newOwner;
    if (pendingOwner != address(0)) {
      _transferOwnership(pendingOwner);
    }
  }

  /**
   * @notice Allows an owner to begin transferring ownership to a new address,
   * pending.
   */
  function transferOwnership(address to) public override onlyOwner {
    _transferOwnership(to);
  }

  /**
   * @notice Allows an ownership transfer to be completed by the recipient.
   */
  function acceptOwnership() external override {
    require(msg.sender == s_pendingOwner, "Must be proposed owner");

    address oldOwner = s_owner;
    s_owner = msg.sender;
    s_pendingOwner = address(0);

    emit OwnershipTransferred(oldOwner, msg.sender);
  }

  /**
   * @notice Get the current owner
   */
  function owner() public view override returns (address) {
    return s_owner;
  }

  /**
   * @notice validate, transfer ownership, and emit relevant events
   */
  function _transferOwnership(address to) private {
    require(to != msg.sender, "Cannot transfer to self");

    s_pendingOwner = to;

    emit OwnershipTransferRequested(s_owner, to);
  }

  /**
   * @notice validate access
   */
  function _validateOwnership() internal view {
    require(msg.sender == s_owner, "Only callable by owner");
  }

  /**
   * @notice Reverts if called by anyone other than the contract owner.
   */
  modifier onlyOwner() {
    _validateOwnership();
    _;
  }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface OwnableInterface {
  function owner() external returns (address);

  function transferOwnership(address recipient) external;

  function acceptOwnership() external;
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

interface VRFCoordinatorV2Interface {
  /**
   * @notice Get configuration relevant for making requests
   * @return minimumRequestConfirmations global min for request confirmations
   * @return maxGasLimit global max for request gas limit
   * @return s_provingKeyHashes list of registered key hashes
   */
  function getRequestConfig()
    external
    view
    returns (
      uint16,
      uint32,
      bytes32[] memory
    );

  /**
   * @notice Request a set of random words.
   * @param keyHash - Corresponds to a particular oracle job which uses
   * that key for generating the VRF proof. Different keyHash's have different gas price
   * ceilings, so you can select a specific one to bound your maximum per request cost.
   * @param subId  - The ID of the VRF subscription. Must be funded
   * with the minimum subscription balance required for the selected keyHash.
   * @param minimumRequestConfirmations - How many blocks you'd like the
   * oracle to wait before responding to the request. See SECURITY CONSIDERATIONS
   * for why you may want to request more. The acceptable range is
   * [minimumRequestBlockConfirmations, 200].
   * @param callbackGasLimit - How much gas you'd like to receive in your
   * fulfillRandomWords callback. Note that gasleft() inside fulfillRandomWords
   * may be slightly less than this amount because of gas used calling the function
   * (argument decoding etc.), so you may need to request slightly more than you expect
   * to have inside fulfillRandomWords. The acceptable range is
   * [0, maxGasLimit]
   * @param numWords - The number of uint256 random values you'd like to receive
   * in your fulfillRandomWords callback. Note these numbers are expanded in a
   * secure way by the VRFCoordinator from a single random value supplied by the oracle.
   * @return requestId - A unique identifier of the request. Can be used to match
   * a request to a response in fulfillRandomWords.
   */
  function requestRandomWords(
    bytes32 keyHash,
    uint64 subId,
    uint16 minimumRequestConfirmations,
    uint32 callbackGasLimit,
    uint32 numWords
  ) external returns (uint256 requestId);

  /**
   * @notice Create a VRF subscription.
   * @return subId - A unique subscription id.
   * @dev You can manage the consumer set dynamically with addConsumer/removeConsumer.
   * @dev Note to fund the subscription, use transferAndCall. For example
   * @dev  LINKTOKEN.transferAndCall(
   * @dev    address(COORDINATOR),
   * @dev    amount,
   * @dev    abi.encode(subId));
   */
  function createSubscription() external returns (uint64 subId);

  /**
   * @notice Get a VRF subscription.
   * @param subId - ID of the subscription
   * @return balance - LINK balance of the subscription in juels.
   * @return reqCount - number of requests for this subscription, determines fee tier.
   * @return owner - owner of the subscription.
   * @return consumers - list of consumer address which are able to use this subscription.
   */
  function getSubscription(uint64 subId)
    external
    view
    returns (
      uint96 balance,
      uint64 reqCount,
      address owner,
      address[] memory consumers
    );

  /**
   * @notice Request subscription owner transfer.
   * @param subId - ID of the subscription
   * @param newOwner - proposed new owner of the subscription
   */
  function requestSubscriptionOwnerTransfer(uint64 subId, address newOwner) external;

  /**
   * @notice Request subscription owner transfer.
   * @param subId - ID of the subscription
   * @dev will revert if original owner of subId has
   * not requested that msg.sender become the new owner.
   */
  function acceptSubscriptionOwnerTransfer(uint64 subId) external;

  /**
   * @notice Add a consumer to a VRF subscription.
   * @param subId - ID of the subscription
   * @param consumer - New consumer which can use the subscription
   */
  function addConsumer(uint64 subId, address consumer) external;

  /**
   * @notice Remove a consumer from a VRF subscription.
   * @param subId - ID of the subscription
   * @param consumer - Consumer to remove from the subscription
   */
  function removeConsumer(uint64 subId, address consumer) external;

  /**
   * @notice Cancel a subscription
   * @param subId - ID of the subscription
   * @param to - Where to send the remaining LINK to
   */
  function cancelSubscription(uint64 subId, address to) external;

  /*
   * @notice Check to see if there exists a request commitment consumers
   * for all consumers and keyhashes for a given sub.
   * @param subId - ID of the subscription
   * @return true if there exists at least one unfulfilled request for the subscription, false
   * otherwise.
   */
  function pendingRequestExists(uint64 subId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

/** ****************************************************************************
 * @notice Interface for contracts using VRF randomness
 * *****************************************************************************
 * @dev PURPOSE
 *
 * @dev Reggie the Random Oracle (not his real job) wants to provide randomness
 * @dev to Vera the verifier in such a way that Vera can be sure he's not
 * @dev making his output up to suit himself. Reggie provides Vera a public key
 * @dev to which he knows the secret key. Each time Vera provides a seed to
 * @dev Reggie, he gives back a value which is computed completely
 * @dev deterministically from the seed and the secret key.
 *
 * @dev Reggie provides a proof by which Vera can verify that the output was
 * @dev correctly computed once Reggie tells it to her, but without that proof,
 * @dev the output is indistinguishable to her from a uniform random sample
 * @dev from the output space.
 *
 * @dev The purpose of this contract is to make it easy for unrelated contracts
 * @dev to talk to Vera the verifier about the work Reggie is doing, to provide
 * @dev simple access to a verifiable source of randomness. It ensures 2 things:
 * @dev 1. The fulfillment came from the VRFCoordinator
 * @dev 2. The consumer contract implements fulfillRandomWords.
 * *****************************************************************************
 * @dev USAGE
 *
 * @dev Calling contracts must inherit from VRFConsumerBase, and can
 * @dev initialize VRFConsumerBase's attributes in their constructor as
 * @dev shown:
 *
 * @dev   contract VRFConsumer {
 * @dev     constructor(<other arguments>, address _vrfCoordinator, address _link)
 * @dev       VRFConsumerBase(_vrfCoordinator) public {
 * @dev         <initialization with other arguments goes here>
 * @dev       }
 * @dev   }
 *
 * @dev The oracle will have given you an ID for the VRF keypair they have
 * @dev committed to (let's call it keyHash). Create subscription, fund it
 * @dev and your consumer contract as a consumer of it (see VRFCoordinatorInterface
 * @dev subscription management functions).
 * @dev Call requestRandomWords(keyHash, subId, minimumRequestConfirmations,
 * @dev callbackGasLimit, numWords),
 * @dev see (VRFCoordinatorInterface for a description of the arguments).
 *
 * @dev Once the VRFCoordinator has received and validated the oracle's response
 * @dev to your request, it will call your contract's fulfillRandomWords method.
 *
 * @dev The randomness argument to fulfillRandomWords is a set of random words
 * @dev generated from your requestId and the blockHash of the request.
 *
 * @dev If your contract could have concurrent requests open, you can use the
 * @dev requestId returned from requestRandomWords to track which response is associated
 * @dev with which randomness request.
 * @dev See "SECURITY CONSIDERATIONS" for principles to keep in mind,
 * @dev if your contract could have multiple requests in flight simultaneously.
 *
 * @dev Colliding `requestId`s are cryptographically impossible as long as seeds
 * @dev differ.
 *
 * *****************************************************************************
 * @dev SECURITY CONSIDERATIONS
 *
 * @dev A method with the ability to call your fulfillRandomness method directly
 * @dev could spoof a VRF response with any random value, so it's critical that
 * @dev it cannot be directly called by anything other than this base contract
 * @dev (specifically, by the VRFConsumerBase.rawFulfillRandomness method).
 *
 * @dev For your users to trust that your contract's random behavior is free
 * @dev from malicious interference, it's best if you can write it so that all
 * @dev behaviors implied by a VRF response are executed *during* your
 * @dev fulfillRandomness method. If your contract must store the response (or
 * @dev anything derived from it) and use it later, you must ensure that any
 * @dev user-significant behavior which depends on that stored value cannot be
 * @dev manipulated by a subsequent VRF request.
 *
 * @dev Similarly, both miners and the VRF oracle itself have some influence
 * @dev over the order in which VRF responses appear on the blockchain, so if
 * @dev your contract could have multiple VRF requests in flight simultaneously,
 * @dev you must ensure that the order in which the VRF responses arrive cannot
 * @dev be used to manipulate your contract's user-significant behavior.
 *
 * @dev Since the block hash of the block which contains the requestRandomness
 * @dev call is mixed into the input to the VRF *last*, a sufficiently powerful
 * @dev miner could, in principle, fork the blockchain to evict the block
 * @dev containing the request, forcing the request to be included in a
 * @dev different block with a different hash, and therefore a different input
 * @dev to the VRF. However, such an attack would incur a substantial economic
 * @dev cost. This cost scales with the number of blocks the VRF oracle waits
 * @dev until it calls responds to a request. It is for this reason that
 * @dev that you can signal to an oracle you'd like them to wait longer before
 * @dev responding to the request (however this is not enforced in the contract
 * @dev and so remains effective only in the case of unmodified oracle software).
 */
abstract contract VRFConsumerBaseV2 {
  error OnlyCoordinatorCanFulfill(address have, address want);
  address private immutable vrfCoordinator;

  /**
   * @param _vrfCoordinator address of VRFCoordinator contract
   */
  constructor(address _vrfCoordinator) {
    vrfCoordinator = _vrfCoordinator;
  }

  /**
   * @notice fulfillRandomness handles the VRF response. Your contract must
   * @notice implement it. See "SECURITY CONSIDERATIONS" above for important
   * @notice principles to keep in mind when implementing your fulfillRandomness
   * @notice method.
   *
   * @dev VRFConsumerBaseV2 expects its subcontracts to have a method with this
   * @dev signature, and will call it once it has verified the proof
   * @dev associated with the randomness. (It is triggered via a call to
   * @dev rawFulfillRandomness, below.)
   *
   * @param requestId The Id initially returned by requestRandomness
   * @param randomWords the VRF output expanded to the requested number of words
   */
  function fulfillRandomWords(uint256 requestId, uint256[] memory randomWords) internal virtual;

  // rawFulfillRandomness is called by VRFCoordinator when it receives a valid VRF
  // proof. rawFulfillRandomness then calls fulfillRandomness, after validating
  // the origin of the call
  function rawFulfillRandomWords(uint256 requestId, uint256[] memory randomWords) external {
    if (msg.sender != vrfCoordinator) {
      revert OnlyCoordinatorCanFulfill(msg.sender, vrfCoordinator);
    }
    fulfillRandomWords(requestId, randomWords);
  }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.7.0) (security/Pausable.sol)

pragma solidity ^0.8.0;

import "../utils/Context.sol";

/**
 * @dev Contract module which allows children to implement an emergency stop
 * mechanism that can be triggered by an authorized account.
 *
 * This module is used through inheritance. It will make available the
 * modifiers `whenNotPaused` and `whenPaused`, which can be applied to
 * the functions of your contract. Note that they will not be pausable by
 * simply including this module, only once the modifiers are put in place.
 */
abstract contract Pausable is Context {
    /**
     * @dev Emitted when the pause is triggered by `account`.
     */
    event Paused(address account);

    /**
     * @dev Emitted when the pause is lifted by `account`.
     */
    event Unpaused(address account);

    bool private _paused;

    /**
     * @dev Initializes the contract in unpaused state.
     */
    constructor() {
        _paused = false;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is not paused.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    modifier whenNotPaused() {
        _requireNotPaused();
        _;
    }

    /**
     * @dev Modifier to make a function callable only when the contract is paused.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    modifier whenPaused() {
        _requirePaused();
        _;
    }

    /**
     * @dev Returns true if the contract is paused, and false otherwise.
     */
    function paused() public view virtual returns (bool) {
        return _paused;
    }

    /**
     * @dev Throws if the contract is paused.
     */
    function _requireNotPaused() internal view virtual {
        require(!paused(), "Pausable: paused");
    }

    /**
     * @dev Throws if the contract is not paused.
     */
    function _requirePaused() internal view virtual {
        require(paused(), "Pausable: not paused");
    }

    /**
     * @dev Triggers stopped state.
     *
     * Requirements:
     *
     * - The contract must not be paused.
     */
    function _pause() internal virtual whenNotPaused {
        _paused = true;
        emit Paused(_msgSender());
    }

    /**
     * @dev Returns to normal state.
     *
     * Requirements:
     *
     * - The contract must be paused.
     */
    function _unpause() internal virtual whenPaused {
        _paused = false;
        emit Unpaused(_msgSender());
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (security/ReentrancyGuard.sol)

pragma solidity ^0.8.0;

/**
 * @dev Contract module that helps prevent reentrant calls to a function.
 *
 * Inheriting from `ReentrancyGuard` will make the {nonReentrant} modifier
 * available, which can be applied to functions to make sure there are no nested
 * (reentrant) calls to them.
 *
 * Note that because there is a single `nonReentrant` guard, functions marked as
 * `nonReentrant` may not call one another. This can be worked around by making
 * those functions `private`, and then adding `external` `nonReentrant` entry
 * points to them.
 *
 * TIP: If you would like to learn more about reentrancy and alternative ways
 * to protect against it, check out our blog post
 * https://blog.openzeppelin.com/reentrancy-after-istanbul/[Reentrancy After Istanbul].
 */
abstract contract ReentrancyGuard {
    // Booleans are more expensive than uint256 or any type that takes up a full
    // word because each write operation emits an extra SLOAD to first read the
    // slot's contents, replace the bits taken up by the boolean, and then write
    // back. This is the compiler's defense against contract upgrades and
    // pointer aliasing, and it cannot be disabled.

    // The values being non-zero value makes deployment a bit more expensive,
    // but in exchange the refund on every call to nonReentrant will be lower in
    // amount. Since refunds are capped to a percentage of the total
    // transaction's gas, it is best to keep them low in cases like this one, to
    // increase the likelihood of the full refund coming into effect.
    uint256 private constant _NOT_ENTERED = 1;
    uint256 private constant _ENTERED = 2;

    uint256 private _status;

    constructor() {
        _status = _NOT_ENTERED;
    }

    /**
     * @dev Prevents a contract from calling itself, directly or indirectly.
     * Calling a `nonReentrant` function from another `nonReentrant`
     * function is not supported. It is possible to prevent this from happening
     * by making the `nonReentrant` function external, and making it call a
     * `private` function that does the actual work.
     */
    modifier nonReentrant() {
        // On the first call to nonReentrant, _notEntered will be true
        require(_status != _ENTERED, "ReentrancyGuard: reentrant call");

        // Any calls to nonReentrant after this point will fail
        _status = _ENTERED;

        _;

        // By storing the original value once again, a refund is triggered (see
        // https://eips.ethereum.org/EIPS/eip-2200)
        _status = _NOT_ENTERED;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// solhint-disable-next-line
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/security/Pausable.sol";
import "@chainlink/contracts/src/v0.8/interfaces/VRFCoordinatorV2Interface.sol";
import "@chainlink/contracts/src/v0.8/VRFConsumerBaseV2.sol";
import "@chainlink/contracts/src/v0.8/ConfirmedOwner.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

import "./IFunctionInterface.sol";
import "./Library.sol";

contract COEBurnAndMint is
  VRFConsumerBaseV2,
  ConfirmedOwner,
  Pausable,
  ReentrancyGuard
{
  event Mint(uint256 _tokenId, address sender);
  event RequestFulfilled(address indexed opener);

  struct RequestStatus {
    address opener;
    // AssetType assetType;
    uint256[] randomWords;
    bool exists;
    bool fulfilled;
    bool used;
  }
  mapping(uint256 => RequestStatus) public s_requests;
  mapping(address => uint256[]) public s_userRequests;

  VRFCoordinatorV2Interface COORDINATOR;
  uint64 private s_subscriptionId;
  bytes32 private keyHash;
  uint32 private callbackGasLimit;
  uint16 private requestConfirmations = 3;

  constructor(
    bytes32 _keyHash,
    address _vrfCoordinator,
    uint32 _callbackGasLimit,
    address _aegAddress,
    address _helperAddress,
    address _burnTokenAddress,
    address _adventurersAddress,
    address _ethernalsAddress,
    address _cardBacksAddress,
    address _cardsAddress,
    uint64 subscriptionId
  ) VRFConsumerBaseV2(_vrfCoordinator) ConfirmedOwner(msg.sender) {
    COORDINATOR = VRFCoordinatorV2Interface(_vrfCoordinator);
    keyHash = _keyHash;
    s_subscriptionId = subscriptionId;
    callbackGasLimit = _callbackGasLimit;

    aegAddress = _aegAddress;
    burnTokenAddress = _burnTokenAddress;
    adventurersAddress = _adventurersAddress;
    ethernalsAddress = _ethernalsAddress;
    cardBacksAddress = _cardBacksAddress;
    cardsAddress = _cardsAddress;
    helperAddress = _helperAddress;
  }

  /** BURN INTERACTION **/
  address public aegAddress;
  address public helperAddress;
  address public adventurersAddress;
  address public ethernalsAddress;
  address public cardBacksAddress;
  address public cardsAddress;
  address public burnTokenAddress;

  mapping(address => uint256) public addressOpenedFP;

  /** FOUNDER PACK OPENING **/

  function startOpenFP() public nonReentrant whenNotPaused {
    require(
      FunctionInterface(burnTokenAddress).balanceOf(msg.sender, 1) > 0,
      "No pack."
    );

    //check if user already has a unfulfilled or unused request
    uint256[] memory fulfilledNotUsed = getFulfilledByAddress(msg.sender);
    uint256[] memory unfulfilled = getUnfulfilledByAddress(msg.sender);

    require(
      fulfilledNotUsed.length == 0 && unfulfilled.length == 0,
      "Already has unfulfilled or unused request."
    );

    requestRandomWords();
  }

  function finishOpenFP() public nonReentrant {
    require(
      FunctionInterface(burnTokenAddress).balanceOf(msg.sender, 1) > 0,
      "No pack."
    );
    uint256[] memory fulfilledNotUsed = getFulfilledByAddress(msg.sender);
    require(fulfilledNotUsed.length > 0, "No fulfilled requests.");

    uint256[] memory randomWords = s_requests[fulfilledNotUsed[0]].randomWords;

    FunctionInterface(burnTokenAddress).burn(msg.sender, 1, 1);

    NftInterface(ethernalsAddress).adminMint(
      msg.sender,
      randomWords[1] % 5,
      1,
      1,
      false
    );
    NftInterface(adventurersAddress).adminMint(
      msg.sender,
      randomWords[2] % 9,
      1,
      1,
      false
    );
    NftInterface(cardBacksAddress).adminMint(msg.sender, 0, 1, 1, false); //silver
    NftInterface(cardBacksAddress).adminMint(msg.sender, 1, 1, 1, false); //gold

    uint256[] memory cardsToMint = new uint256[](39);

    //MINT 5 common rarity cards
    for (uint256 i = 0; i < 5; i++) {
      cardsToMint[i] = CardInterface(cardsAddress).getRarityToCardTypes(
        Library.Rarity.Common
      )[
          randomWords[i + 3] %
            CardInterface(cardsAddress)
              .getRarityToCardTypes(Library.Rarity.Common)
              .length
        ];
    }

    //MINT 1 uncommon rarity card
    cardsToMint[5] = CardInterface(cardsAddress).getRarityToCardTypes(
      Library.Rarity.Uncommon
    )[
        randomWords[8] %
          CardInterface(cardsAddress)
            .getRarityToCardTypes(Library.Rarity.Uncommon)
            .length
      ];

    cardsToMint[6] = 83;
    cardsToMint[7] = 85;
    cardsToMint[8] = 87;

    //add basic cards to mint
    for (
      uint256 i = 0;
      i <
      CardInterface(cardsAddress)
        .getRarityToCardTypes(Library.Rarity.Basic)
        .length;
      i++
    ) {
      cardsToMint[9 + i] = CardInterface(cardsAddress).getRarityToCardTypes(
        Library.Rarity.Basic
      )[i];
    }

    s_requests[fulfilledNotUsed[0]].used = true;

    CardInterface(cardsAddress).packMint(msg.sender, cardsToMint, false);

    addressOpenedFP[msg.sender]++;
  }

  /** GETTERS **/

  function getFulfilledByAddress(
    address _address
  ) public view returns (uint256[] memory) {
    uint256[] memory requestIds = s_userRequests[_address];
    uint256[] memory fulfilledRequestIds = new uint256[](requestIds.length);
    uint256 fulfilledRequestIdsCount = 0;
    for (uint256 i = 0; i < requestIds.length; i++) {
      if (s_requests[requestIds[i]].fulfilled) {
        fulfilledRequestIds[fulfilledRequestIdsCount] = requestIds[i];
        fulfilledRequestIdsCount++;
      }
    }

    uint256[] memory result = new uint256[](fulfilledRequestIdsCount);
    for (uint256 i = 0; i < fulfilledRequestIdsCount; i++) {
      result[i] = fulfilledRequestIds[i];
    }
    return result;
  }

  function getUnfulfilledByAddress(
    address _address
  ) public view returns (uint256[] memory) {
    uint256[] memory requestIds = s_userRequests[_address];
    uint256[] memory unfulfilledRequestIds = new uint256[](requestIds.length);
    uint256 unfulfilledRequestIdsCount = 0;
    for (uint256 i = 0; i < requestIds.length; i++) {
      if (!s_requests[requestIds[i]].fulfilled) {
        unfulfilledRequestIds[unfulfilledRequestIdsCount] = requestIds[i];
        unfulfilledRequestIdsCount++;
      }
    }

    uint256[] memory result = new uint256[](unfulfilledRequestIdsCount);
    for (uint256 i = 0; i < unfulfilledRequestIdsCount; i++) {
      result[i] = unfulfilledRequestIds[i];
    }
    return result;
  }

  /** SETTERS */

  function setAdventurersAddress(address _address) external onlyOwner {
    adventurersAddress = _address;
  }

  function setCardBacksAddress(address _address) public onlyOwner {
    cardBacksAddress = _address;
  }

  function setEthernalsAddress(address _address) public onlyOwner {
    ethernalsAddress = _address;
  }

  function setBurnTokenAddress(address _address) public onlyOwner {
    burnTokenAddress = _address;
  }

  function setCardsAddress(address _address) public onlyOwner {
    cardsAddress = _address;
  }

  function setCallbackGasLimit(uint32 _callbackGasLimit) public onlyOwner {
    callbackGasLimit = _callbackGasLimit;
  }

  /** ACTIVATION **/

  function pause() public onlyOwner {
    _pause();
  }

  function unpause() public onlyOwner {
    _unpause();
  }

  function requestRandomWords() internal returns (uint256 requestId) {
    uint32 numWords = 10;

    //TEST REQUEST ID
    // requestId = 83132445710567745731713214319446805456689061274219614556348005953705680908145; // <----------------TESTING

    requestId = COORDINATOR.requestRandomWords(
      keyHash,
      s_subscriptionId,
      requestConfirmations,
      callbackGasLimit,
      numWords //number of words to return
    );
    s_requests[requestId] = RequestStatus({
      opener: msg.sender,
      randomWords: new uint256[](0),
      exists: true,
      fulfilled: false,
      used: false
    });
    s_userRequests[msg.sender].push(requestId);
    // emit RequestSent(requestId, numWords);
    return requestId;
  }

  function fulfillRandomWords(
    uint256 _requestId,
    uint256[] memory _randomWords
  ) internal override {
    require(s_requests[_requestId].exists, "request not found");
    s_requests[_requestId].fulfilled = true;
    s_requests[_requestId].randomWords = _randomWords;

    emit RequestFulfilled(s_requests[_requestId].opener);
  }
}

// SPDX-License-Identifier: MIT
// solhint-disable-next-line
pragma solidity ^0.8.0;

import "./Library.sol";

interface AEGInterface {
  function balanceOf(address) external view returns (uint256);

  function transferFrom(address, address, uint256) external;
}

interface NftInterface {
  function adminMint(
    address _to,
    uint256 _type,
    uint256 _level,
    uint256 _amount,
    bool _sb
  ) external;

  function ownerOf(uint256) external view returns (address);

  function totalTypes() external view returns (uint256);
}

interface CardInterface {
  function packMint(address, uint256[] memory, bool) external;

  function promoTypes() external view returns (uint256[] memory);

  function getRarityToCardTypes(
    Library.Rarity
  ) external view returns (uint256[] memory);
}

interface FunctionInterface {
  function fpMint(address, uint256, uint256) external;

  function burn(uint256) external;

  function totalTokens() external view returns (uint256);

  function transfer(address, uint256) external;

  function decimals() external view returns (uint8);

  function purchaseWithToken(uint256, address, address) external;

  function trim(
    uint256,
    uint256[] memory
  ) external pure returns (uint256[] memory);

  function burn(address, uint256, uint256) external;

  function balanceOf(address, uint256) external view returns (uint256);
}

// SPDX-License-Identifier: MIT
// solhint-disable-next-line
pragma solidity ^0.8.0;

library Library {
  enum Rarity {
    Basic,
    Common,
    Uncommon,
    Rare,
    Epic,
    Legendary
  }

  // struct Card {
  //   uint256 id;
  //   uint256 mintCount;
  //   uint256 burnCount;
  //   uint256 season;
  //   string uri;
  //   Rarity rarity;
  //   bool paused;
  //   bool exists;
  //   bool isPromo;
  // }
}