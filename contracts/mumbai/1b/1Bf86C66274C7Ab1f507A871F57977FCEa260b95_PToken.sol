// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.10;

import {SafeCast} from '../../dependencies/openzeppelin/contracts/SafeCast.sol';
import {IPToken} from '../../interfaces/IPToken.sol';
import {IPool} from '../../interfaces/IPool.sol';
import {IACLManager} from '../../interfaces/IACLManager.sol';
import {VersionedInitializable} from '../libraries/aave-upgradeability/VersionedInitializable.sol';
import {Errors} from '../libraries/helpers/Errors.sol';
import {WadRayMath} from '../libraries/math/WadRayMath.sol';
import {IInitializablePToken} from '../../interfaces/IInitializablePToken.sol';
import {IPoolAddressesProvider} from '../../interfaces/IPoolAddressesProvider.sol';
import {IERC20Detailed} from '../../dependencies/openzeppelin/contracts/IERC20Detailed.sol';
import {IERC20} from '../../dependencies/openzeppelin/contracts/IERC20.sol';
import {GPv2SafeERC20} from '../../dependencies/gnosis/contracts/GPv2SafeERC20.sol';

/**
 * @title Aave ERC20 PToken
 * @author Aave
 * @notice Implementation of the interest bearing token for the Aave protocol
 */
contract PToken is VersionedInitializable, IPToken {
  using WadRayMath for uint256;
  using SafeCast for uint256;
  using GPv2SafeERC20 for IERC20;

  uint256 public constant PTOKEN_REVISION = 0x3;

  address private _supplyingAsset;
  uint8 private _supplyingAssetDecimals;
  address private _underlyingAsset;

  struct BalanceInterest {
    uint256 balance;
    uint256 interestAmount;
  }

  struct ReduxInterest {
    mapping(address => uint256) reduxAmounts;
    uint256 reduxTotalAmount;
    uint40 lastUpdateTimestamp;
  }
  
  struct UserState {
    uint256 supplyBalance;        // supply balance
    uint256 collateralBalance;    // collateral balance
    uint256 prxyBalance;          // prxy balance
    uint256 claimedPrxy;          // claimed prxy
    mapping(address => BalanceInterest) borrowBalanceInterests;   // borrow balances & interest rates
    uint256 prxyInterestAmount;   // prxy interest amount
    uint256 borrowInterestAmount; // borrow interest amount
    uint40 lastUpdateTimestamp;   // latest update timestamp
    uint16 referralCode;          // referralCode = 0 : PTOKEN, referralCode = 1 : PRXY
  }

  mapping(address => UserState) private _userState;
  ReduxInterest reduxInterest;
  uint256 private _supplyTotalBalance;
  uint256 private _prxyTotalBalance;
  uint256 private _claimedPrxyTotal;
  mapping(address => uint256) private _borrowTotalBalances;
  
  uint256 private _prxyTotalInterestAmount;
  
  uint256 public _borrowTotalInterestAmount;
  mapping(address => uint256) private _subBorrowTotalInterestAmounts;
  uint40 private _lastUpdateTimestamp;

  string private _name;
  string private _symbol;
  uint8 private _decimals;
  IPoolAddressesProvider internal immutable  _addressesProvider;
  IPool public immutable POOL;

  /**
   * @dev Constructor.
   * @param pool The address of the Pool contract
   */
  constructor(IPool pool) {
    POOL = pool;
    _addressesProvider = pool.ADDRESSES_PROVIDER();
  }
  /// @inheritdoc VersionedInitializable
  function getRevision() internal pure virtual override returns (uint256) {
    return PTOKEN_REVISION;
  }

  /// @inheritdoc IInitializablePToken
  function initialize(
    IPool initializingPool,
    address supplyingAsset,
    uint8 pTokenDecimals,
    string calldata pTokenName,
    string calldata pTokenSymbol,
    bytes calldata params
  ) external initializer {
    require(initializingPool == POOL, Errors.POOL_ADDRESSES_DO_NOT_MATCH);
    _name = pTokenName;
    _symbol = pTokenSymbol;
    _decimals = pTokenDecimals;

    _supplyingAsset = supplyingAsset;
    _supplyingAssetDecimals = IERC20Detailed(supplyingAsset).decimals();
    _underlyingAsset = supplyingAsset;

    emit Initialized(
      supplyingAsset,
      address(POOL),
      pTokenDecimals,
      pTokenName,
      pTokenSymbol,
      params
    );
  }

  /**
   * @dev Only pool can call functions marked by this modifier.
   **/
  modifier onlyPool() {
    require(msg.sender == address(POOL), Errors.CALLER_MUST_BE_POOL);
    _;
  }

  /// @inheritdoc IERC20Detailed
  function name() public view override returns (string memory) {
    return _name;
  }

  /// @inheritdoc IERC20Detailed
  function symbol() external view override returns (string memory) {
    return _symbol;
  }

  /// @inheritdoc IERC20Detailed
  function decimals() external view override returns (uint8) {
    return _decimals;
  }

  /// @inheritdoc IERC20
  function balanceOf(address user) external view returns (uint256) {
    if(_userState[user].supplyBalance == 0) return 0;

    uint256 dt = block.timestamp - uint256(_userState[user].lastUpdateTimestamp);
    uint256 curSupplyBalance = _userState[user].supplyBalance;

    if(_userState[user].borrowInterestAmount > 0) {
      uint256 borrowInterest = _userState[user].borrowInterestAmount * dt;
      if(curSupplyBalance < borrowInterest) return 0;
      curSupplyBalance -= borrowInterest;
    }

    return curSupplyBalance / WadRayMath.RAY;
  }

  /// @inheritdoc IPToken
  function collateralBalanceOf(address user) external view returns (uint256) {
    return _userState[user].collateralBalance / WadRayMath.RAY;
  }

  /// @inheritdoc IPToken
  function borrowBalanceOf(address user, address asset) external view returns (uint256) {
    uint256 curBorrowBalance = _userState[user].borrowBalanceInterests[asset].balance;
    return curBorrowBalance / WadRayMath.RAY;
  }

  /// @inheritdoc IPToken
  function prxyBalanceOf(address user) external view returns (uint256) {
    uint256 dt = block.timestamp - uint256(_userState[user].lastUpdateTimestamp);
    uint256 curPrxyBalance = _userState[user].prxyBalance;

    // if(_userState[user].referralCode == 1 && _userState[user].prxyInterestAmount > 0) {
    if(_userState[user].prxyInterestAmount > 0) {
      curPrxyBalance += _userState[user].prxyInterestAmount * dt;
    }
    if(curPrxyBalance < _userState[user].claimedPrxy) return 0;
    return (curPrxyBalance - _userState[user].claimedPrxy) / WadRayMath.RAY;
  }

  /// @inheritdoc IERC20
  function totalSupply() external view returns (uint256) {
    if(_supplyTotalBalance == 0) return 0;

    uint256 dt = block.timestamp - uint256(_lastUpdateTimestamp);
    uint256 curSupplyTotal = _supplyTotalBalance;

    if(_borrowTotalInterestAmount > 0) {
      uint256 borrowTotalInterest = _borrowTotalInterestAmount * dt;
      if(curSupplyTotal < borrowTotalInterest) return 0;
      curSupplyTotal -= borrowTotalInterest;
    } 

    return curSupplyTotal / WadRayMath.RAY;
  }

  /// @inheritdoc IPToken
  function totalBorrow(address asset) external view returns (uint256) {
    return _borrowTotalBalances[asset] / WadRayMath.RAY;
  }

  /// @inheritdoc IPToken
  function totalPrxy() external view returns (uint256) {
    uint256 dt = block.timestamp - uint256(_lastUpdateTimestamp);
    uint256 curPrxyTotal = _prxyTotalBalance + _prxyTotalInterestAmount * dt;
    if(curPrxyTotal < _claimedPrxyTotal) return 0;
    return (curPrxyTotal - _claimedPrxyTotal) / WadRayMath.RAY;
  }


  /// @inheritdoc IPToken
  function switchSupplyMode(address user) external virtual override onlyPool {
    _updateState(user);
    _userState[user].referralCode = 1 - _userState[user].referralCode;
    _updateInterestRate(user);
  }


  /// @inheritdoc IPToken
  function supply(address user, uint256 amount, uint16 referralCode) external virtual override onlyPool {
    require(referralCode == 0 || referralCode == 1, Errors.INVALID_REFERRAL_CODE);
    _updateState(user);

    if (_userState[user].supplyBalance == 0) {
      require(_userState[user].borrowInterestAmount == 0, Errors.MUST_ZERO_BORROW_BALANCE);
      _userState[user].referralCode = referralCode;
    } else {
      require(_userState[user].referralCode == referralCode, Errors.INCORRECT_REFERRAL_CODE);
    }

    uint256 supplyAmount = amount * WadRayMath.RAY;
    _userState[user].supplyBalance += supplyAmount;
    _supplyTotalBalance += supplyAmount;
    emit Transfer(address(0), user, amount);

    _updateInterestRate(user);
  }

  /// @inheritdoc IPToken
  function borrow(address user, address asset, uint256 amount) external virtual override onlyPool {
    _updateState(user);

    uint256 borrowAmount = amount * WadRayMath.RAY;
    _userState[user].borrowBalanceInterests[asset].balance += borrowAmount;
    _borrowTotalBalances[asset] += borrowAmount;

    _updateInterestRate(user);
  }

  /// @inheritdoc IPToken
  function withdraw(address user, uint256 amount) external virtual override onlyPool {
    _updateState(user);

    uint256 withdrawAmount = amount * WadRayMath.RAY;
    require (_userState[user].supplyBalance >= withdrawAmount, Errors.INSUFFICIENT_USER_SUPPLY_BALANCE);
    if(_userState[user].supplyBalance - withdrawAmount < WadRayMath.RAY) {
      reduxInterest.reduxTotalAmount += _userState[user].supplyBalance - withdrawAmount; 
      withdrawAmount = _userState[user].supplyBalance; 
    }
    require(_supplyTotalBalance >= withdrawAmount, Errors.INSUFFICIENT_TOTAL_SUPPLY_BALANCE);

    _userState[user].supplyBalance -= withdrawAmount;
    _supplyTotalBalance -= withdrawAmount;
    emit Transfer(user, address(0), amount);

    _updateInterestRate(user);
  }


  /// @inheritdoc IPToken
  function repay(address user, address asset, uint256 amount) external virtual override onlyPool {
    _updateState(user);

    uint256 repayAmount = amount * WadRayMath.RAY;
    require (_userState[user].borrowBalanceInterests[asset].balance >= repayAmount, Errors.INSUFFICIENT_USER_BORROW_BALANCE);
    require (_borrowTotalBalances[asset] >= repayAmount, Errors.INSUFFICIENT_TOTAL_BORROW_BALANCE);

    _userState[user].borrowBalanceInterests[asset].balance -= repayAmount;
    _borrowTotalBalances[asset] -= repayAmount;

    _updateInterestRate(user);
  }

  /// @inheritdoc IPToken
  function liquidate(address user) external virtual override onlyPool {
    _updateState(user);

    require (_supplyTotalBalance >= _userState[user].supplyBalance, Errors.INSUFFICIENT_TOTAL_SUPPLY_BALANCE);
    _supplyTotalBalance -= _userState[user].supplyBalance;
    uint256 transferAmount = _userState[user].supplyBalance / WadRayMath.RAY;
    reduxInterest.reduxTotalAmount += _userState[user].supplyBalance - transferAmount * WadRayMath.RAY;
    IERC20(_underlyingAsset).safeTransfer(_addressesProvider.getTradingWallet(), transferAmount);
    emit Transfer(user, address(0), transferAmount);
    _userState[user].supplyBalance = 0;

    address[] memory assetsList = POOL.getReservesList();
    for(uint256 i = 0; i < assetsList.length; i++) {
      address asset = assetsList[i];
      require (_borrowTotalBalances[asset] >= _userState[user].borrowBalanceInterests[asset].balance, Errors.INSUFFICIENT_TOTAL_BORROW_BALANCE);  
      _borrowTotalBalances[asset] -= _userState[user].borrowBalanceInterests[asset].balance;
      _userState[user].borrowBalanceInterests[asset].balance = 0;
    }

    _updateInterestRate(user);
  }
  
  /// @inheritdoc IPToken
  function claimPrxy(address user, uint256 amount) external virtual override onlyPool {
    uint256 claimPrxyAmount = amount * WadRayMath.RAY;
    _userState[user].claimedPrxy += claimPrxyAmount;
    _claimedPrxyTotal += claimPrxyAmount;
  }


  /// @inheritdoc IPToken
  function reduxInterestTransfer() external virtual override onlyPool  {
    _updateReduxInterest();
    
    uint256 amount = reduxInterest.reduxTotalAmount / WadRayMath.RAY;
    if(amount > 0) IERC20(_underlyingAsset).safeTransfer(address(POOL), amount);
    reduxInterest.reduxTotalAmount -= amount *  WadRayMath.RAY;
  }

  /// @inheritdoc IPToken
  function getReduxInterest() external view returns(uint256) {
    uint256 elapsedTime = block.timestamp - reduxInterest.lastUpdateTimestamp;
    uint256 amount = (reduxInterest.reduxTotalAmount + _borrowTotalInterestAmount * elapsedTime) / WadRayMath.RAY;
    return amount;
  }

  /// @inheritdoc IPToken
  function getReduxInterestOf(address asset) external view returns(uint256) {
    uint256 elapsedTime = block.timestamp - reduxInterest.lastUpdateTimestamp;
    uint256 amount = reduxInterest.reduxAmounts[asset];
    if(elapsedTime > 0) {
      amount += _subBorrowTotalInterestAmounts[asset] * elapsedTime;
    }
    return amount / WadRayMath.RAY;
  }

  /// @inheritdoc IPToken
  function clearReduxInterest() external {
    address[] memory assetsList = POOL.getReservesList();
    for(uint256 i = 0; i < assetsList.length; i++) {
      address asset = assetsList[i];
      reduxInterest.reduxAmounts[asset] = 0;
    }
  }

  function _updateReduxInterest() internal {
    uint256 dt = (block.timestamp - reduxInterest.lastUpdateTimestamp);
    address[] memory assetsList = POOL.getReservesList();
    for(uint256 i = 0; i < assetsList.length; i++) {
      address asset = assetsList[i];
      reduxInterest.reduxAmounts[asset] += _subBorrowTotalInterestAmounts[asset] * dt;
    }
    reduxInterest.reduxTotalAmount += _borrowTotalInterestAmount * dt;
    reduxInterest.lastUpdateTimestamp = uint40(block.timestamp);
  }


  function _updateInterestRate(address user) internal {
    address[] memory assetsList = POOL.getReservesList();

    uint256 collateralBalance = 0;
    for(uint256 i = 0; i < assetsList.length; i++) {
      address asset = assetsList[i];
      BalanceInterest storage borrowBalanceInterest = _userState[user].borrowBalanceInterests[assetsList[i]];

      collateralBalance += borrowBalanceInterest.balance.rayMul(POOL.getCollateralRate(_underlyingAsset, asset));

      _userState[user].borrowInterestAmount -= borrowBalanceInterest.interestAmount;
      _subBorrowTotalInterestAmounts[asset] -= borrowBalanceInterest.interestAmount;
      _borrowTotalInterestAmount -= borrowBalanceInterest.interestAmount;

      borrowBalanceInterest.interestAmount = borrowBalanceInterest.balance.rayMul(POOL.getBorrowInterestRate(_underlyingAsset, asset));
      _userState[user].borrowInterestAmount += borrowBalanceInterest.interestAmount;
      _subBorrowTotalInterestAmounts[asset] += borrowBalanceInterest.interestAmount;
      _borrowTotalInterestAmount += borrowBalanceInterest.interestAmount;
    }
    
    require(_userState[user].supplyBalance >= collateralBalance, Errors.INVALID_USER_BALANCE);
    _userState[user].collateralBalance = collateralBalance;

    _prxyTotalInterestAmount -= _userState[user].prxyInterestAmount;
    
    if(collateralBalance > 0) {
      uint256 rate2 = POOL.getPrxyInterestRate2(_underlyingAsset);
      _userState[user].prxyInterestAmount = collateralBalance.rayMul(rate2);
    } else {
      _userState[user].prxyInterestAmount = 0;
    }

    if(_userState[user].referralCode == 1) {
      uint256 rate = POOL.getPrxyInterestRate(_underlyingAsset);
      _userState[user].prxyInterestAmount = (_userState[user].supplyBalance - collateralBalance).rayMul(rate);

    }
    _prxyTotalInterestAmount += _userState[user].prxyInterestAmount;
  }

  function _updateState(address user) internal {
    _updateReduxInterest();

    uint256 dT = block.timestamp - uint256(_lastUpdateTimestamp);
    if(dT > 0) {
      uint256 borrowTotalInterest = _borrowTotalInterestAmount * dT;
      require(_supplyTotalBalance >= borrowTotalInterest, Errors.INVALID_TOTAL_BALANCE);
      _supplyTotalBalance -= borrowTotalInterest;
      _prxyTotalBalance += _prxyTotalInterestAmount * dT;
    }

    if(_userState[user].supplyBalance > 0) {
      uint256 dt = block.timestamp - uint256(_userState[user].lastUpdateTimestamp);
      // if(_userState[user].referralCode == 1) {
      if(_userState[user].prxyInterestAmount > 0) {
        _userState[user].prxyBalance += _userState[user].prxyInterestAmount * dt;
        // _prxyTotalInterestAmount -= _userState[user].prxyInterestAmount;
      }

      uint256 reduceAmount = _userState[user].borrowInterestAmount * dt;
      require(_userState[user].supplyBalance >= reduceAmount, Errors.INVALID_USER_BALANCE);

      uint256 diffBalance = _userState[user].supplyBalance / WadRayMath.RAY;
      _userState[user].supplyBalance -= reduceAmount;
      diffBalance -= _userState[user].supplyBalance / WadRayMath.RAY;

      if(diffBalance > 0) emit Transfer(user, address(0), diffBalance);
    }
    _userState[user].lastUpdateTimestamp = _lastUpdateTimestamp = uint40(block.timestamp);
  }

  /// @inheritdoc IPToken
  function lastUpdateTimestampOf(address user) external view returns (uint40) {
    return _userState[user].lastUpdateTimestamp;
  }

  function lastUpdateTimestamp() external view returns (uint40) {
    return _lastUpdateTimestamp;
  }

  function transfer(address, uint256) external virtual override returns (bool) {
    revert(Errors.OPERATION_NOT_SUPPORTED);
  }

  function allowance(address, address) external view returns (uint256) {
    revert(Errors.OPERATION_NOT_SUPPORTED);
  }

  function approve(address, uint256) external virtual returns (bool) {
    revert(Errors.OPERATION_NOT_SUPPORTED);
  }

  function transferFrom(
    address,
    address,
    uint256
  ) external returns (bool) {
    revert(Errors.OPERATION_NOT_SUPPORTED);
  }

  function increaseAllowance(address, uint256) external returns (bool) {
    revert(Errors.OPERATION_NOT_SUPPORTED);
  }

  function decreaseAllowance(address, uint256) external returns (bool) {
    revert(Errors.OPERATION_NOT_SUPPORTED);
  }

  function UNDERLYING_ASSET_ADDRESS() external view virtual override returns (address) {
    return _underlyingAsset;
  }

  /// @inheritdoc IPToken
  function transferUnderlyingTo(address target, uint256 amount) external virtual override onlyPool {
    IERC20(_underlyingAsset).safeTransfer(target, amount);
  }

  /// @inheritdoc IPToken
  function supplyDecimals() external view override returns (uint8) {
    return _supplyingAssetDecimals;
  }

  /// @inheritdoc IPToken
  function supplyMode(address user) external view override returns (uint16) {
    return _userState[user].referralCode;
  }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/math/SafeCast.sol)
pragma solidity 0.8.10;

/**
 * @dev Wrappers over Solidity's uintXX/intXX casting operators with added overflow
 * checks.
 *
 * Downcasting from uint256/int256 in Solidity does not revert on overflow. This can
 * easily result in undesired exploitation or bugs, since developers usually
 * assume that overflows raise errors. `SafeCast` restores this intuition by
 * reverting the transaction when such an operation overflows.
 *
 * Using this library instead of the unchecked operations eliminates an entire
 * class of bugs, so it's recommended to use it always.
 *
 * Can be combined with {SafeMath} and {SignedSafeMath} to extend it to smaller types, by performing
 * all math on `uint256` and `int256` and then downcasting.
 */
library SafeCast {
  /**
   * @dev Returns the downcasted uint224 from uint256, reverting on
   * overflow (when the input is greater than largest uint224).
   *
   * Counterpart to Solidity's `uint224` operator.
   *
   * Requirements:
   *
   * - input must fit into 224 bits
   */
  function toUint224(uint256 value) internal pure returns (uint224) {
    require(value <= type(uint224).max, "SafeCast: value doesn't fit in 224 bits");
    return uint224(value);
  }

  /**
   * @dev Returns the downcasted uint128 from uint256, reverting on
   * overflow (when the input is greater than largest uint128).
   *
   * Counterpart to Solidity's `uint128` operator.
   *
   * Requirements:
   *
   * - input must fit into 128 bits
   */
  function toUint128(uint256 value) internal pure returns (uint128) {
    require(value <= type(uint128).max, "SafeCast: value doesn't fit in 128 bits");
    return uint128(value);
  }

  /**
   * @dev Returns the downcasted uint96 from uint256, reverting on
   * overflow (when the input is greater than largest uint96).
   *
   * Counterpart to Solidity's `uint96` operator.
   *
   * Requirements:
   *
   * - input must fit into 96 bits
   */
  function toUint96(uint256 value) internal pure returns (uint96) {
    require(value <= type(uint96).max, "SafeCast: value doesn't fit in 96 bits");
    return uint96(value);
  }

  /**
   * @dev Returns the downcasted uint64 from uint256, reverting on
   * overflow (when the input is greater than largest uint64).
   *
   * Counterpart to Solidity's `uint64` operator.
   *
   * Requirements:
   *
   * - input must fit into 64 bits
   */
  function toUint64(uint256 value) internal pure returns (uint64) {
    require(value <= type(uint64).max, "SafeCast: value doesn't fit in 64 bits");
    return uint64(value);
  }

  /**
   * @dev Returns the downcasted uint32 from uint256, reverting on
   * overflow (when the input is greater than largest uint32).
   *
   * Counterpart to Solidity's `uint32` operator.
   *
   * Requirements:
   *
   * - input must fit into 32 bits
   */
  function toUint32(uint256 value) internal pure returns (uint32) {
    require(value <= type(uint32).max, "SafeCast: value doesn't fit in 32 bits");
    return uint32(value);
  }

  /**
   * @dev Returns the downcasted uint16 from uint256, reverting on
   * overflow (when the input is greater than largest uint16).
   *
   * Counterpart to Solidity's `uint16` operator.
   *
   * Requirements:
   *
   * - input must fit into 16 bits
   */
  function toUint16(uint256 value) internal pure returns (uint16) {
    require(value <= type(uint16).max, "SafeCast: value doesn't fit in 16 bits");
    return uint16(value);
  }

  /**
   * @dev Returns the downcasted uint8 from uint256, reverting on
   * overflow (when the input is greater than largest uint8).
   *
   * Counterpart to Solidity's `uint8` operator.
   *
   * Requirements:
   *
   * - input must fit into 8 bits.
   */
  function toUint8(uint256 value) internal pure returns (uint8) {
    require(value <= type(uint8).max, "SafeCast: value doesn't fit in 8 bits");
    return uint8(value);
  }

  /**
   * @dev Converts a signed int256 into an unsigned uint256.
   *
   * Requirements:
   *
   * - input must be greater than or equal to 0.
   */
  function toUint256(int256 value) internal pure returns (uint256) {
    require(value >= 0, 'SafeCast: value must be positive');
    return uint256(value);
  }

  /**
   * @dev Returns the downcasted int128 from int256, reverting on
   * overflow (when the input is less than smallest int128 or
   * greater than largest int128).
   *
   * Counterpart to Solidity's `int128` operator.
   *
   * Requirements:
   *
   * - input must fit into 128 bits
   *
   * _Available since v3.1._
   */
  function toInt128(int256 value) internal pure returns (int128) {
    require(
      value >= type(int128).min && value <= type(int128).max,
      "SafeCast: value doesn't fit in 128 bits"
    );
    return int128(value);
  }

  /**
   * @dev Returns the downcasted int64 from int256, reverting on
   * overflow (when the input is less than smallest int64 or
   * greater than largest int64).
   *
   * Counterpart to Solidity's `int64` operator.
   *
   * Requirements:
   *
   * - input must fit into 64 bits
   *
   * _Available since v3.1._
   */
  function toInt64(int256 value) internal pure returns (int64) {
    require(
      value >= type(int64).min && value <= type(int64).max,
      "SafeCast: value doesn't fit in 64 bits"
    );
    return int64(value);
  }

  /**
   * @dev Returns the downcasted int32 from int256, reverting on
   * overflow (when the input is less than smallest int32 or
   * greater than largest int32).
   *
   * Counterpart to Solidity's `int32` operator.
   *
   * Requirements:
   *
   * - input must fit into 32 bits
   *
   * _Available since v3.1._
   */
  function toInt32(int256 value) internal pure returns (int32) {
    require(
      value >= type(int32).min && value <= type(int32).max,
      "SafeCast: value doesn't fit in 32 bits"
    );
    return int32(value);
  }

  /**
   * @dev Returns the downcasted int16 from int256, reverting on
   * overflow (when the input is less than smallest int16 or
   * greater than largest int16).
   *
   * Counterpart to Solidity's `int16` operator.
   *
   * Requirements:
   *
   * - input must fit into 16 bits
   *
   * _Available since v3.1._
   */
  function toInt16(int256 value) internal pure returns (int16) {
    require(
      value >= type(int16).min && value <= type(int16).max,
      "SafeCast: value doesn't fit in 16 bits"
    );
    return int16(value);
  }

  /**
   * @dev Returns the downcasted int8 from int256, reverting on
   * overflow (when the input is less than smallest int8 or
   * greater than largest int8).
   *
   * Counterpart to Solidity's `int8` operator.
   *
   * Requirements:
   *
   * - input must fit into 8 bits.
   *
   * _Available since v3.1._
   */
  function toInt8(int256 value) internal pure returns (int8) {
    require(
      value >= type(int8).min && value <= type(int8).max,
      "SafeCast: value doesn't fit in 8 bits"
    );
    return int8(value);
  }

  /**
   * @dev Converts an unsigned uint256 into a signed int256.
   *
   * Requirements:
   *
   * - input must be less than or equal to maxInt256.
   */
  function toInt256(uint256 value) internal pure returns (int256) {
    // Note: Unsafe cast below is okay because `type(int256).max` is guaranteed to be positive
    require(value <= uint256(type(int256).max), "SafeCast: value doesn't fit in an int256");
    return int256(value);
  }
}

// SPDX-License-Identifier: AGPL-3.0
pragma solidity 0.8.10;

import {IERC20Detailed} from '../dependencies/openzeppelin/contracts/IERC20Detailed.sol';
import {IInitializablePToken} from './IInitializablePToken.sol';

/**
 * @title IPToken
 * @author Aave
 * @notice Defines the basic interface for an PToken.
 **/
interface IPToken is IERC20Detailed, IInitializablePToken {
  /**
   * @notice Returns the total amount of the borrowing asset of this pToken
   * @return The total amount of the borrowing asset 
   **/
  function totalBorrow(address asset) external view returns (uint256);

  /**
   * @notice Returns the total amount of the prxy interest of this pToken
   * @return The total amount of the prxy interest 
   **/
  function totalPrxy() external view returns (uint256);

  /**
   * @notice Returns the balance of the borrowing asset of user
   * @return The user balance of the borrowing asset
   **/
  function borrowBalanceOf(address user, address asset) external view returns (uint256);

  /**
   * @notice Returns the collateral balance of the borrowing assets of user
   * @return The user collateral balance of the borrowing assets
   **/
  function collateralBalanceOf(address user) external view returns (uint256);

  /**
   * @notice Returns the balance of the prxy interest of user
   * @return The user balance of the prxy interest
   **/
  function prxyBalanceOf(address user) external view returns (uint256);

  /**
   * @notice Returns the supply mode of the user
   * @return The supply mode
   **/
  function supplyMode(address user) external view returns (uint16);

  /**
   * @notice Returns the last update timestamp of this pToken
   * @return The last update timestamp of this pToken
   **/
  function lastUpdateTimestamp() external view returns (uint40);

  /** 
   * @notice Returns the last update timestamp of the user
   * @return The last update timestamp of user
   **/
  function lastUpdateTimestampOf(address user) external view returns (uint40);

  /** 
   * @notice Returns the decimals of the supplying asset
   * @return The decimals of the supplying asset
   **/
  function supplyDecimals() external view returns (uint8);

  /**
   * @notice Handle the switch supply mode action of the user
   * @param user The address of supplier
   **/
  function switchSupplyMode(address user) external;

  /**
   * @notice Hanlde the supply action of the user
   * @param user The address of supplier
   * @param amount The supplying amount
   **/
  function supply(address user, uint256 amount, uint16 referralCode) external;

  /**
   * @notice Handle the borrow action of the user
   * @param user The address of borrower
   * @param amount The borrowing amount
   **/
  function borrow(address user, address asset, uint256 amount) external;

  /**
   * @notice Handle the withdraw action of the user
   * @param user The address of withdrawer
   * @param amount The withdrawing amount
   **/
  function withdraw(address user, uint256 amount) external;

  /**
   * @notice Handle the repayment action of the user
   * @param user The address of repayer
   * @param amount The repaying amount
   **/
  function repay(address user, address assset, uint256 amount) external;

  /**
   * @notice Handle the liquidation action of the user
   * @param user The address of liquidator
   **/
  function liquidate(address user) external;

  /**
   * @notice Transfer the redux interest to redux central
   **/
  function reduxInterestTransfer() external;

  /**
   * @notice Get the redux interest amount
   * @return The redux interest amount
   **/
  function getReduxInterest() external view returns(uint256);

  /**
   * @notice Get the redux interest amount of asset
   * @param asset The addresss of asset
   * @return The redux interest amount and elapsed time of asset
   **/
  function getReduxInterestOf(address asset) external view returns(uint256);

  /**
   * @notice Clear the redux interest
   **/
  function clearReduxInterest() external;

  /**
   * @notice Handle the claim prxy action of the user
   * @param user The address of claimer
   * @param amount The amount of prxy token
   **/
  function claimPrxy(address user, uint256 amount) external;

  /**
   * @notice Transfers the underlying asset to `target`.
   * @dev Used by the Pool to transfer assets in borrow(), withdraw()
   * @param user The recipient of the underlying
   * @param amount The amount getting transferred
   **/
  function transferUnderlyingTo(address user, uint256 amount) external;

  /**
   * @notice Returns the address of the underlying asset of this aToken (E.g. WETH for aWETH)
   * @return The address of the underlying asset
   **/
  function UNDERLYING_ASSET_ADDRESS() external view returns (address);
}

// SPDX-License-Identifier: AGPL-3.0
pragma solidity 0.8.10;

import {IPoolAddressesProvider} from './IPoolAddressesProvider.sol';
import {DataTypes} from '../protocol/libraries/types/DataTypes.sol';

/**
 * @title IPool
 * @author Aave
 * @notice Defines the basic interface for an Aave Pool.
 **/
interface IPool {

  /**
   * @notice Initializes a reserve, activating it, assigning an pToken and debt tokens and an
   * interest rate strategy
   * @dev Only callable by the PoolConfigurator contract
   * @param supplyingAsset The address of the supplying asset of the reserve
   * @param borrowRate The borrowing rate
   * @param prxyRate The prxy interest rate
   * @param prxyRate2 The prxy interest rate
   * @param splitRate The split rate
   * @param pTokenAddress The address of the pToken that will be assigned to the reserve
   **/
  function initReserve(
    address supplyingAsset,
    uint256 borrowRate,
    uint256 prxyRate,
    uint256 prxyRate2,
    uint256 splitRate,
    address pTokenAddress
  ) external;

  /**
   * @notice Returns the PoolAddressesProvider connected to this contract
   * @return The address of the PoolAddressesProvider
   **/
  function ADDRESSES_PROVIDER() external view returns (IPoolAddressesProvider);

  /**
   * @notice Supplies an `amount` of underlying asset into the reserve, receiving in return overlying pTokens.
   * - E.g. User supplies 100 USDC and gets in return 100 pUSDC
   * @param asset The address of the underlying asset to supply
   * @param amount The amount to be supplied
   * @param referralCode Code used to register the integrator originating the operation, for potential rewards.
   *   0 if the action is executed directly by the user, without any middle-man
   **/
  function supply(
    address asset,
    uint256 amount,
    uint16 referralCode
  ) external;

  /**
   * @notice Allows users to borrow a specific `amount` of the reserve underlying asset, provided that the borrower
   * - E.g. User borrows 100 USDC passing, receiving the 100 USDC in his wallet
   * @param asset The address of the underlying asset to borrow
   * @param borrowingAsset The address of the borrowing asset to borrow
   * @param amount The amount to be borrowed
   * @param referralCode The code used to register the integrator originating the operation, for potential rewards.
   **/
  function borrow(
    address asset,
    address borrowingAsset,
    uint256 amount,
    uint16 referralCode
  ) external;

  /**
   * @notice Withdraws an `amount` of underlying asset from the reserve, burning the equivalent pTokens owned
   * E.g. User has 100 pUSDC, calls withdraw() and receives 100 USDC, burning the 100 pUSDC
   * @param asset The address of the underlying asset to withdraw
   * @param amount The underlying amount to be withdrawn
   *   - Send the value type(uint256).max in order to withdraw the whole pToken balance
   * @return The final amount withdrawn
   **/
  function withdraw(
    address asset,
    uint256 amount
  ) external returns(uint256);

  /**
   * @notice Repays a borrowed `amount` on a specific reserve, burning the equivalent debt tokens owned
   * - E.g. User repays 100 USDC
   * @param asset The address of the borrowed underlying asset previously borrowed
   * @param borrowingAsset The address of the borrowinging asset
   * @param amount The amount to repay
   * - Send the value type(uint256).max in order to repay
   * @return The final amount repaid
   **/
  function repay(
    address asset,
    address borrowingAsset,
    uint256 amount
  ) external returns(uint256);

  /**
   * @notice Function to claim the prxy interest
   * @param asset The address of the underlying asset used as collateral
   * @param amount The amount to claim
   **/
  function claimPrxy(
    address asset,
    uint256 amount
  ) external;

  /**
   * @notice Function to liquidate a non-healthy position collateral-wise, with Health Factor below 1
   * @param user The address of user for liquidation
   * @param asset The address of the underlying asset used as collateral, to receive as result of the liquidation
   **/
  function liquidationCall(
    address user,
    address asset
  ) external;

  /**
   * @notice Function to liquidate a non-healthy position collateral-wise, with Health Factor below 1 from keeper
   * @param asset The address of the underlying asset used as collateral, to receive as result of the liquidation
   **/
  function checkAndLiquidate(
    address asset
  ) external;

  /**
   * @notice Function to split the redux interest
   **/
  function splitReduxInterest() external;

  /**
   * @notice Sets the configuration bitmap of the reserve as a whole
   * @dev Only callable by the PoolConfigurator contract
   * @param asset The address of the underlying asset of the reserve
   * @param configuration The new configuration bitmap
   **/
  function setConfiguration(address asset, DataTypes.ReserveConfigurationMap calldata configuration)
    external;

  /**
   * @notice Returns the configuration of the reserve
   * @param asset The address of the underlying asset of the reserve
   * @return The configuration of the reserve
   **/
  function getConfiguration(address asset)
    external
    view
    returns (DataTypes.ReserveConfigurationMap memory);
  
  /**
   * @notice Returns the maximum number of reserves supported to be listed in this Pool
   * @return The maximum number of reserves supported
   */
  function MAX_NUMBER_RESERVES() external view returns (uint16);

  /**
   * @notice Returns the state and configuration of the reserve
   * @param asset The address of the underlying asset of the reserve
   * @return The state and configuration data of the reserve
   **/
  function getReserveData(address asset) external view returns (DataTypes.ReserveData memory);

  /**
   * @notice Sets the interest rates of the reserve
   * @dev Only callable by the PoolConfigurator contract
   * @param asset The address of the underlying asset of the reserve
   * @param borrowRate The borrow rate
   * @param prxyRate The prxy rate
   * @param prxyRate2 The collateral prxy rate
   **/
  function setReserveInterestRates(address asset, uint256 borrowRate, uint256 prxyRate, uint256 prxyRate2) external;

  /**
   * @notice Sets the split rate of the reserve
   * @dev Only callable by the PoolConfigurator contract
   * @param asset The address of the underlying asset of the reserve
   * @param splitRate The split rate
   **/
  function setReserveSplitRate(address asset, uint256 splitRate) external;


  /**
   * @notice Returns the list of the underlying assets of all the initialized reserves
   * @dev It does not include dropped reserves
   * @return The addresses of the underlying assets of the initialized reserves
   **/
  function getReservesList() external view returns (address[] memory);

  /**
   * @notice Returns the borrow interest rate
   * @param supplyingAsset The address of the supplying asset of the reserve
   * @param borrowingAsset The address of the borrowing asset of the reserve
   * @return The borrowing interest rate of the reserve
   **/
  function getBorrowInterestRate(address supplyingAsset, address borrowingAsset) external view returns(uint256);

  /**
   * @notice Returns the prxy interest rate
   * @param supplyingAsset The address of the supplying asset of the reserve
   * @return The prxy interest rate of the reserve
   **/
  function getPrxyInterestRate(address supplyingAsset) external view returns(uint256);

    /**
   * @notice Returns the collateral prxy interest rate
   * @param supplyingAsset The address of the supplying asset of the reserve
   * @return The collateral prxy interest rate of the reserve
   **/
  function getPrxyInterestRate2(address supplyingAsset) external view returns(uint256);

  /**
   * @notice Returns the collateral rate
   * @param supplyingAsset The address of the supplying asset of the reserve
   * @param borrowingAsset The address of the borrowing asset of the reserve
   * @return The collateral rate of the reserve
   **/
  function getCollateralRate(address supplyingAsset, address borrowingAsset) external view returns (uint256);

  /**
   * @notice Returns the user account data of the asset
   * @param user The address of the user
   * @param asset The address of the asset
   * @return totalCollateralBase The total collateral of the user in the base currency used by the price feed
   * @return totalDebtBase The total debt of the user in the base currency used by the price feed
   * @return availableBorrowsBase The borrowing power left of the user in the base currency used by the price feed
   * @return currentLiquidationThreshold The liquidation threshold of the user
   * @return ltv The loan to value of The user
   * @return healthFactor The current health factor of the user
   **/
  function getUserAccountData(address user, address asset)
    external
    view
    returns (
      uint256 totalCollateralBase,
      uint256 totalDebtBase,
      uint256 availableBorrowsBase,
      uint256 currentLiquidationThreshold,
      uint256 ltv,
      uint256 healthFactor
    );
  
  /**
   * @notice Sets the redux interval
   * @dev Only callable by the PoolConfigurator contract
   * @param reduxInterval The redux interval 
   */
  function setReduxInterval(uint256 reduxInterval) external;

  /**
   * @notice Function to switch the supply mode
   * @param asset The address of the underlying asset used as collateral
   **/
  function switchSupplyMode(address asset) external;

  /**
   * @notice Function to add the fund
   * @param asset The address of the underlying asset used as collateral
   * @param amount The amount to add
   **/
  function addFund(
    address asset,
    uint256 amount
  ) external;

  /**
   * @notice Function to remove the fund
   * @param asset The address of the underlying asset used as collateral
   * @param amount The amount to remove
   **/
  function removeFund(
    address asset,
    uint256 amount
  ) external;

  /**
   * @notice Function to recover the tokens from pool
   * @param token The address of the token
   * @param amount The amount to recover
   **/
  function recoverToken(
    address token, 
    uint256 amount
  ) external;

  /**
   * @notice Function to get supplier list
   * @param asset The address of the underlying asset used as collateral
   * @param start The start index
   * @param n The count
   **/
  function getSuppliers(
    address asset, 
    uint256 start, 
    uint256 n
  ) external view returns (address[] memory suppliers);
}

// SPDX-License-Identifier: AGPL-3.0
pragma solidity 0.8.10;

import {IPoolAddressesProvider} from './IPoolAddressesProvider.sol';

/**
 * @title IACLManager
 * @author Aave
 * @notice Defines the basic interface for the ACL Manager
 **/
interface IACLManager {
  /**
   * @notice Returns the contract address of the PoolAddressesProvider
   * @return The address of the PoolAddressesProvider
   */
  function ADDRESSES_PROVIDER() external view returns (IPoolAddressesProvider);

  /**
   * @notice Returns the identifier of the PoolAdmin role
   * @return The id of the PoolAdmin role
   */
  function POOL_ADMIN_ROLE() external view returns (bytes32);

  /**
   * @notice Returns the identifier of the EmergencyAdmin role
   * @return The id of the EmergencyAdmin role
   */
  function EMERGENCY_ADMIN_ROLE() external view returns (bytes32);

  /**
   * @notice Returns the identifier of the RiskAdmin role
   * @return The id of the RiskAdmin role
   */
  function RISK_ADMIN_ROLE() external view returns (bytes32);

  /**
   * @notice Returns the identifier of the FlashBorrower role
   * @return The id of the FlashBorrower role
   */
  function FLASH_BORROWER_ROLE() external view returns (bytes32);

  /**
   * @notice Returns the identifier of the Pool Re-Supply role
   * @return The id of the Pool Re-Supply role
   */
  function POOL_RESUPPLY_ROLE() external view returns (bytes32);

  /**
   * @notice Returns the identifier of the AssetListingAdmin role
   * @return The id of the AssetListingAdmin role
   */
  function ASSET_LISTING_ADMIN_ROLE() external view returns (bytes32);

  /**
   * @notice Set the role as admin of a specific role.
   * @dev By default the admin role for all roles is `DEFAULT_ADMIN_ROLE`.
   * @param role The role to be managed by the admin role
   * @param adminRole The admin role
   */
  function setRoleAdmin(bytes32 role, bytes32 adminRole) external;

  /**
   * @notice Adds a new admin as PoolAdmin
   * @param admin The address of the new admin
   */
  function addPoolAdmin(address admin) external;

  /**
   * @notice Removes an admin as PoolAdmin
   * @param admin The address of the admin to remove
   */
  function removePoolAdmin(address admin) external;

  /**
   * @notice Returns true if the address is PoolAdmin, false otherwise
   * @param admin The address to check
   * @return True if the given address is PoolAdmin, false otherwise
   */
  function isPoolAdmin(address admin) external view returns (bool);

  /**
   * @notice Adds a new admin as EmergencyAdmin
   * @param admin The address of the new admin
   */
  function addEmergencyAdmin(address admin) external;

  /**
   * @notice Removes an admin as EmergencyAdmin
   * @param admin The address of the admin to remove
   */
  function removeEmergencyAdmin(address admin) external;

  /**
   * @notice Returns true if the address is EmergencyAdmin, false otherwise
   * @param admin The address to check
   * @return True if the given address is EmergencyAdmin, false otherwise
   */
  function isEmergencyAdmin(address admin) external view returns (bool);

  /**
   * @notice Adds a new admin as RiskAdmin
   * @param admin The address of the new admin
   */
  function addRiskAdmin(address admin) external;

  /**
   * @notice Removes an admin as RiskAdmin
   * @param admin The address of the admin to remove
   */
  function removeRiskAdmin(address admin) external;

  /**
   * @notice Returns true if the address is RiskAdmin, false otherwise
   * @param admin The address to check
   * @return True if the given address is RiskAdmin, false otherwise
   */
  function isRiskAdmin(address admin) external view returns (bool);

  /**
   * @notice Adds a new address as FlashBorrower
   * @param borrower The address of the new FlashBorrower
   */
  function addFlashBorrower(address borrower) external;

  /**
   * @notice Removes an admin as FlashBorrower
   * @param borrower The address of the FlashBorrower to remove
   */
  function removeFlashBorrower(address borrower) external;

  /**
   * @notice Returns true if the address is FlashBorrower, false otherwise
   * @param borrower The address to check
   * @return True if the given address is FlashBorrower, false otherwise
   */
  function isFlashBorrower(address borrower) external view returns (bool);

  /**
   * @notice Adds a new address as Pool Supplier
   * @param supplier The address of the new pool supplier
   */
  function addPoolSupplier(address supplier) external;

  /**
   * @notice Removes an address as Pool Supplier
   * @param supplier The address of the pool supplier to remove
   */
  function removePoolSupplier(address supplier) external;

  /**
   * @notice Returns true if the address is Pool Supplier, false otherwise
   * @param supplier The address to check
   * @return True if the given address is Pool Supplier, false otherwise
   */
  function isPoolSupplier(address supplier) external view returns (bool);

  /**
   * @notice Adds a new admin as AssetListingAdmin
   * @param admin The address of the new admin
   */
  function addAssetListingAdmin(address admin) external;

  /**
   * @notice Removes an admin as AssetListingAdmin
   * @param admin The address of the admin to remove
   */
  function removeAssetListingAdmin(address admin) external;

  /**
   * @notice Returns true if the address is AssetListingAdmin, false otherwise
   * @param admin The address to check
   * @return True if the given address is AssetListingAdmin, false otherwise
   */
  function isAssetListingAdmin(address admin) external view returns (bool);
}

// SPDX-License-Identifier: AGPL-3.0
pragma solidity 0.8.10;

/**
 * @title VersionedInitializable
 * @author Aave, inspired by the OpenZeppelin Initializable contract
 * @notice Helper contract to implement initializer functions. To use it, replace
 * the constructor with a function that has the `initializer` modifier.
 * @dev WARNING: Unlike constructors, initializer functions must be manually
 * invoked. This applies both to deploying an Initializable contract, as well
 * as extending an Initializable contract via inheritance.
 * WARNING: When used with inheritance, manual care must be taken to not invoke
 * a parent initializer twice, or ensure that all initializers are idempotent,
 * because this is not dealt with automatically as with constructors.
 */
abstract contract VersionedInitializable {
  /**
   * @dev Indicates that the contract has been initialized.
   */
  uint256 private lastInitializedRevision = 0;

  /**
   * @dev Indicates that the contract is in the process of being initialized.
   */
  bool private initializing;

  /**
   * @dev Modifier to use in the initializer function of a contract.
   */
  modifier initializer() {
    uint256 revision = getRevision();
    require(
      initializing || isConstructor() || revision > lastInitializedRevision,
      'Contract instance has already been initialized'
    );

    bool isTopLevelCall = !initializing;
    if (isTopLevelCall) {
      initializing = true;
      lastInitializedRevision = revision;
    }

    _;

    if (isTopLevelCall) {
      initializing = false;
    }
  }

  /**
   * @notice Returns the revision number of the contract
   * @dev Needs to be defined in the inherited class as a constant.
   * @return The revision number
   **/
  function getRevision() internal pure virtual returns (uint256);

  /**
   * @notice Returns true if and only if the function is running in the constructor
   * @return True if the function is running in the constructor
   **/
  function isConstructor() private view returns (bool) {
    // extcodesize checks the size of the code stored in an address, and
    // address returns the current address. Since the code is still not
    // deployed when running a constructor, any checks on its code size will
    // yield zero, making it an effective way to detect if a contract is
    // under construction or not.
    uint256 cs;
    //solium-disable-next-line
    assembly {
      cs := extcodesize(address())
    }
    return cs == 0;
  }

  // Reserved storage space to allow for layout changes in the future.
  uint256[50] private ______gap;
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.10;

/**
 * @title Errors library
 * @author Aave
 * @notice Defines the error messages emitted by the different contracts of the Aave protocol
 */
library Errors {
  string public constant CALLER_NOT_POOL_ADMIN = '1'; // 'The caller of the function is not a pool admin'
  string public constant CALLER_NOT_EMERGENCY_ADMIN = '2'; // 'The caller of the function is not an emergency admin'
  string public constant CALLER_NOT_POOL_OR_EMERGENCY_ADMIN = '3'; // 'The caller of the function is not a pool or emergency admin'
  string public constant CALLER_NOT_RISK_OR_POOL_ADMIN = '4'; // 'The caller of the function is not a risk or pool admin'
  string public constant CALLER_NOT_ASSET_LISTING_OR_POOL_ADMIN = '5'; // 'The caller of the function is not an asset listing or pool admin'
  string public constant CALLER_NOT_BRIDGE = '6'; // 'The caller of the function is not a bridge'
  string public constant ADDRESSES_PROVIDER_NOT_REGISTERED = '7'; // 'Pool addresses provider is not registered'
  string public constant INVALID_ADDRESSES_PROVIDER_ID = '8'; // 'Invalid id for the pool addresses provider'
  string public constant NOT_CONTRACT = '9'; // 'Address is not a contract'
  string public constant CALLER_NOT_POOL_CONFIGURATOR = '10'; // 'The caller of the function is not the pool configurator'
  string public constant CALLER_NOT_PTOKEN = '11'; // 'The caller of the function is not an PToken'
  string public constant INVALID_ADDRESSES_PROVIDER = '12'; // 'The address of the pool addresses provider is invalid'
  string public constant INVALID_FLASHLOAN_EXECUTOR_RETURN = '13'; // 'Invalid return value of the flashloan executor function'
  string public constant RESERVE_ALREADY_ADDED = '14'; // 'Reserve has already been added to reserve list'
  string public constant NO_MORE_RESERVES_ALLOWED = '15'; // 'Maximum amount of reserves in the pool reached'
  string public constant EMODE_CATEGORY_RESERVED = '16'; // 'Zero eMode category is reserved for volatile heterogeneous assets'
  string public constant INVALID_EMODE_CATEGORY_ASSIGNMENT = '17'; // 'Invalid eMode category assignment to asset'
  string public constant RESERVE_LIQUIDITY_NOT_ZERO = '18'; // 'The liquidity of the reserve needs to be 0'
  string public constant FLASHLOAN_PREMIUM_INVALID = '19'; // 'Invalid flashloan premium'
  string public constant INVALID_RESERVE_PARAMS = '20'; // 'Invalid risk parameters for the reserve'
  string public constant INVALID_EMODE_CATEGORY_PARAMS = '21'; // 'Invalid risk parameters for the eMode category'
  string public constant BRIDGE_PROTOCOL_FEE_INVALID = '22'; // 'Invalid bridge protocol fee'
  string public constant CALLER_MUST_BE_POOL = '23'; // 'The caller of this function must be a pool'
  string public constant INVALID_MINT_AMOUNT = '24'; // 'Invalid amount to mint'
  string public constant INVALID_BURN_AMOUNT = '25'; // 'Invalid amount to burn'
  string public constant INVALID_AMOUNT = '26'; // 'Amount must be greater than 0'
  string public constant RESERVE_INACTIVE = '27'; // 'Action requires an active reserve'
  string public constant RESERVE_FROZEN = '28'; // 'Action cannot be performed because the reserve is frozen'
  string public constant RESERVE_PAUSED = '29'; // 'Action cannot be performed because the reserve is paused'
  string public constant BORROWING_NOT_ENABLED = '30'; // 'Borrowing is not enabled'
  string public constant STABLE_BORROWING_NOT_ENABLED = '31'; // 'Stable borrowing is not enabled'
  string public constant NOT_ENOUGH_AVAILABLE_USER_BALANCE = '32'; // 'User cannot withdraw more than the available balance'
  string public constant INVALID_INTEREST_RATE_MODE_SELECTED = '33'; // 'Invalid interest rate mode selected'
  string public constant COLLATERAL_BALANCE_IS_ZERO = '34'; // 'The collateral balance is 0'
  string public constant HEALTH_FACTOR_LOWER_THAN_LIQUIDATION_THRESHOLD = '35'; // 'Health factor is lesser than the liquidation threshold'
  string public constant COLLATERAL_CANNOT_COVER_NEW_BORROW = '36'; // 'There is not enough collateral to cover a new borrow'
  string public constant COLLATERAL_SAME_AS_BORROWING_CURRENCY = '37'; // 'Collateral is (mostly) the same currency that is being borrowed'
  string public constant AMOUNT_BIGGER_THAN_MAX_LOAN_SIZE_STABLE = '38'; // 'The requested amount is greater than the max loan size in stable rate mode'
  string public constant NO_DEBT_OF_SELECTED_TYPE = '39'; // 'For repayment of a specific type of debt, the user needs to have debt that type'
  string public constant NO_EXPLICIT_AMOUNT_TO_REPAY_ON_BEHALF = '40'; // 'To repay on behalf of a user an explicit amount to repay is needed'
  string public constant NO_OUTSTANDING_STABLE_DEBT = '41'; // 'User does not have outstanding stable rate debt on this reserve'
  string public constant NO_OUTSTANDING_VARIABLE_DEBT = '42'; // 'User does not have outstanding variable rate debt on this reserve'
  string public constant UNDERLYING_BALANCE_ZERO = '43'; // 'The underlying balance needs to be greater than 0'
  string public constant INTEREST_RATE_REBALANCE_CONDITIONS_NOT_MET = '44'; // 'Interest rate rebalance conditions were not met'
  string public constant HEALTH_FACTOR_NOT_BELOW_THRESHOLD = '45'; // 'Health factor is not below the threshold'
  string public constant COLLATERAL_CANNOT_BE_LIQUIDATED = '46'; // 'The collateral chosen cannot be liquidated'
  string public constant SPECIFIED_CURRENCY_NOT_BORROWED_BY_USER = '47'; // 'User did not borrow the specified currency'
  string public constant SAME_BLOCK_BORROW_REPAY = '48'; // 'Borrow and repay in same block is not allowed'
  string public constant INCONSISTENT_FLASHLOAN_PARAMS = '49'; // 'Inconsistent flashloan parameters'
  string public constant BORROW_CAP_EXCEEDED = '50'; // 'Borrow cap is exceeded'
  string public constant SUPPLY_CAP_EXCEEDED = '51'; // 'Supply cap is exceeded'
  string public constant UNBACKED_MINT_CAP_EXCEEDED = '52'; // 'Unbacked mint cap is exceeded'
  string public constant DEBT_CEILING_EXCEEDED = '53'; // 'Debt ceiling is exceeded'
  string public constant PTOKEN_SUPPLY_NOT_ZERO = '54'; // 'PToken supply is not zero'
  string public constant STABLE_DEBT_NOT_ZERO = '55'; // 'Stable debt supply is not zero'
  string public constant VARIABLE_DEBT_SUPPLY_NOT_ZERO = '56'; // 'Variable debt supply is not zero'
  string public constant LTV_VALIDATION_FAILED = '57'; // 'Ltv validation failed'
  string public constant INCONSISTENT_EMODE_CATEGORY = '58'; // 'Inconsistent eMode category'
  string public constant PRICE_ORACLE_SENTINEL_CHECK_FAILED = '59'; // 'Price oracle sentinel validation failed'
  string public constant ASSET_NOT_BORROWABLE_IN_ISOLATION = '60'; // 'Asset is not borrowable in isolation mode'
  string public constant RESERVE_ALREADY_INITIALIZED = '61'; // 'Reserve has already been initialized'
  string public constant USER_IN_ISOLATION_MODE = '62'; // 'User is in isolation mode'
  string public constant INVALID_LTV = '63'; // 'Invalid ltv parameter for the reserve'
  string public constant INVALID_LIQ_THRESHOLD = '64'; // 'Invalid liquidity threshold parameter for the reserve'
  string public constant INVALID_LIQ_BONUS = '65'; // 'Invalid liquidity bonus parameter for the reserve'
  string public constant INVALID_DECIMALS = '66'; // 'Invalid decimals parameter of the underlying asset of the reserve'
  string public constant INVALID_RESERVE_FACTOR = '67'; // 'Invalid reserve factor parameter for the reserve'
  string public constant INVALID_BORROW_CAP = '68'; // 'Invalid borrow cap for the reserve'
  string public constant INVALID_SUPPLY_CAP = '69'; // 'Invalid supply cap for the reserve'
  string public constant INVALID_LIQUIDATION_PROTOCOL_FEE = '70'; // 'Invalid liquidation protocol fee for the reserve'
  string public constant INVALID_EMODE_CATEGORY = '71'; // 'Invalid eMode category for the reserve'
  string public constant INVALID_UNBACKED_MINT_CAP = '72'; // 'Invalid unbacked mint cap for the reserve'
  string public constant INVALID_DEBT_CEILING = '73'; // 'Invalid debt ceiling for the reserve
  string public constant INVALID_RESERVE_INDEX = '74'; // 'Invalid reserve index'
  string public constant ACL_ADMIN_CANNOT_BE_ZERO = '75'; // 'ACL admin cannot be set to the zero address'
  string public constant INCONSISTENT_PARAMS_LENGTH = '76'; // 'Array parameters that should be equal length are not'
  string public constant ZERO_ADDRESS_NOT_VALID = '77'; // 'Zero address not valid'
  string public constant INVALID_EXPIRATION = '78'; // 'Invalid expiration'
  string public constant INVALID_SIGNATURE = '79'; // 'Invalid signature'
  string public constant OPERATION_NOT_SUPPORTED = '80'; // 'Operation not supported'
  string public constant DEBT_CEILING_NOT_ZERO = '81'; // 'Debt ceiling is not zero'
  string public constant ASSET_NOT_LISTED = '82'; // 'Asset is not listed'
  string public constant INVALID_OPTIMAL_USAGE_RATIO = '83'; // 'Invalid optimal usage ratio'
  string public constant INVALID_OPTIMAL_STABLE_TO_TOTAL_DEBT_RATIO = '84'; // 'Invalid optimal stable to total debt ratio'
  string public constant UNDERLYING_CANNOT_BE_RESCUED = '85'; // 'The underlying asset cannot be rescued'
  string public constant ADDRESSES_PROVIDER_ALREADY_ADDED = '86'; // 'Reserve has already been added to reserve list'
  string public constant POOL_ADDRESSES_DO_NOT_MATCH = '87'; // 'The token implementation pool address and the pool address provided by the initializing pool do not match'
  string public constant STABLE_BORROWING_ENABLED = '88'; // 'Stable borrowing is enabled'
  string public constant SILOED_BORROWING_VIOLATION = '89'; // 'User is trying to borrow multiple assets including a siloed one'
  string public constant RESERVE_DEBT_NOT_ZERO = '90'; // the total debt of the reserve needs to be 0
  string public constant INVALID_USER_BALANCE = '91'; // Invalid user balance
  string public constant INVALID_TOTAL_BALANCE = '92'; // Invalid total balance
  string public constant INSUFFICIENT_USER_SUPPLY_BALANCE = '93'; // Insufficient user supply balance
  string public constant INSUFFICIENT_USER_BORROW_BALANCE = '94'; // Insufficient user borrow balance
  string public constant INSUFFICIENT_USER_PRXY_BALANCE = '95'; // Insufficient user prxy balance
  string public constant INSUFFICIENT_TOTAL_SUPPLY_BALANCE = '96'; // Insufficient total supply balance
  string public constant INSUFFICIENT_TOTAL_BORROW_BALANCE = '97'; // Insufficient total borrow balance
  string public constant INSUFFICIENT_TOTAL_PRXY_BALANCE = '98'; // Insufficient total prxy balance
  string public constant INVALID_REFERRAL_CODE = '99'; // Invalid referral code
  string public constant INCORRECT_REFERRAL_CODE = '100'; // Referral code must be equal to the referral code of first supply 
  string public constant MUST_ZERO_BORROW_BALANCE = '101'; // Must zero borrow balance
  string public constant EARLY_REDUX_SPLIT = '102'; // Early redux split
  string public constant USER_NOT_SUPPLIER = '103'; // User isn't supplier
  string public constant CALLER_NOT_POOL_RESUPPLIER = '104'; // Caller isn't pool re-supplier
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.10;

/**
 * @title WadRayMath library
 * @author Aave
 * @notice Provides functions to perform calculations with Wad and Ray units
 * @dev Provides mul and div function for wads (decimal numbers with 18 digits of precision) and rays (decimal numbers
 * with 27 digits of precision)
 * @dev Operations are rounded. If a value is >=.5, will be rounded up, otherwise rounded down.
 **/
library WadRayMath {
  // HALF_WAD and HALF_RAY expressed with extended notation as constant with operations are not supported in Yul assembly
  uint256 internal constant WAD = 1e18;
  uint256 internal constant HALF_WAD = 0.5e18;

  uint256 internal constant RAY = 1e27;
  uint256 internal constant HALF_RAY = 0.5e27;

  uint256 internal constant WAD_RAY_RATIO = 1e9;

  /**
   * @dev Multiplies two wad, rounding half up to the nearest wad
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Wad
   * @param b Wad
   * @return c = a*b, in wad
   **/
  function wadMul(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= (type(uint256).max - HALF_WAD) / b
    assembly {
      if iszero(or(iszero(b), iszero(gt(a, div(sub(not(0), HALF_WAD), b))))) {
        revert(0, 0)
      }

      c := div(add(mul(a, b), HALF_WAD), WAD)
    }
  }

  /**
   * @dev Divides two wad, rounding half up to the nearest wad
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Wad
   * @param b Wad
   * @return c = a/b, in wad
   **/
  function wadDiv(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= (type(uint256).max - halfB) / WAD
    assembly {
      if or(iszero(b), iszero(iszero(gt(a, div(sub(not(0), div(b, 2)), WAD))))) {
        revert(0, 0)
      }

      c := div(add(mul(a, WAD), div(b, 2)), b)
    }
  }

  /**
   * @notice Multiplies two ray, rounding half up to the nearest ray
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Ray
   * @param b Ray
   * @return c = a raymul b
   **/
  function rayMul(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= (type(uint256).max - HALF_RAY) / b
    assembly {
      if iszero(or(iszero(b), iszero(gt(a, div(sub(not(0), HALF_RAY), b))))) {
        revert(0, 0)
      }

      c := div(add(mul(a, b), HALF_RAY), RAY)
    }
  }

  /**
   * @notice Divides two ray, rounding half up to the nearest ray
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Ray
   * @param b Ray
   * @return c = a raydiv b
   **/
  function rayDiv(uint256 a, uint256 b) internal pure returns (uint256 c) {
    // to avoid overflow, a <= (type(uint256).max - halfB) / RAY
    assembly {
      if or(iszero(b), iszero(iszero(gt(a, div(sub(not(0), div(b, 2)), RAY))))) {
        revert(0, 0)
      }

      c := div(add(mul(a, RAY), div(b, 2)), b)
    }
  }

  /**
   * @dev Casts ray down to wad
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Ray
   * @return b = a converted to wad, rounded half up to the nearest wad
   **/
  function rayToWad(uint256 a) internal pure returns (uint256 b) {
    assembly {
      b := div(a, WAD_RAY_RATIO)
      let remainder := mod(a, WAD_RAY_RATIO)
      if iszero(lt(remainder, div(WAD_RAY_RATIO, 2))) {
        b := add(b, 1)
      }
    }
  }

  /**
   * @dev Converts wad up to ray
   * @dev assembly optimized for improved gas savings, see https://twitter.com/transmissions11/status/1451131036377571328
   * @param a Wad
   * @return b = a converted in ray
   **/
  function wadToRay(uint256 a) internal pure returns (uint256 b) {
    // to avoid overflow, b/WAD_RAY_RATIO == a
    assembly {
      b := mul(a, WAD_RAY_RATIO)

      if iszero(eq(div(b, WAD_RAY_RATIO), a)) {
        revert(0, 0)
      }
    }
  }
}

// SPDX-License-Identifier: AGPL-3.0
pragma solidity 0.8.10;

import {IPool} from './IPool.sol';

/**
 * @title IInitializablePToken
 * @author Aave
 * @notice Interface for the initialize function on PToken
 **/
interface IInitializablePToken {
  /**
   * @dev Emitted when an pToken is initialized
   * @param supplyingAsset The address of the supplying asset
   * @param pool The address of the associated pool
   * @param pTokenDecimals The decimals of the underlying
   * @param pTokenName The name of the pToken
   * @param pTokenSymbol The symbol of the pToken
   * @param params A set of encoded parameters for additional initialization
   **/
  event Initialized(
    address indexed supplyingAsset,
    address indexed pool,
    uint8 pTokenDecimals,
    string pTokenName,
    string pTokenSymbol,
    bytes params
  );

  /**
   * @notice Initializes the pToken
   * @param pool The pool contract that is initializing this contract
   * @param supplyingAsset The address of the supplying asset of this pToken
   * @param pTokenDecimals The decimals of the pToken, same as the supplying asset's
   * @param pTokenName The name of the pToken
   * @param pTokenSymbol The symbol of the pToken
   * @param params A set of encoded parameters for additional initialization
   */
  function initialize(
    IPool pool,
    address supplyingAsset,
    uint8 pTokenDecimals,
    string calldata pTokenName,
    string calldata pTokenSymbol,
    bytes calldata params
  ) external;
}

// SPDX-License-Identifier: AGPL-3.0
pragma solidity 0.8.10;

/**
 * @title IPoolAddressesProvider
 * @author Aave
 * @notice Defines the basic interface for a Pool Addresses Provider.
 **/
interface IPoolAddressesProvider {
  /**
   * @dev Emitted when the market identifier is updated.
   * @param oldMarketId The old id of the market
   * @param newMarketId The new id of the market
   */
  event MarketIdSet(string indexed oldMarketId, string indexed newMarketId);

  /**
   * @dev Emitted when the pool is updated.
   * @param oldAddress The old address of the Pool
   * @param newAddress The new address of the Pool
   */
  event PoolUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the pool configurator is updated.
   * @param oldAddress The old address of the PoolConfigurator
   * @param newAddress The new address of the PoolConfigurator
   */
  event PoolConfiguratorUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the price oracle is updated.
   * @param oldAddress The old address of the PriceOracle
   * @param newAddress The new address of the PriceOracle
   */
  event PriceOracleUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the ACL manager is updated.
   * @param oldAddress The old address of the ACLManager
   * @param newAddress The new address of the ACLManager
   */
  event ACLManagerUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the ACL admin is updated.
   * @param oldAddress The old address of the ACLAdmin
   * @param newAddress The new address of the ACLAdmin
   */
  event ACLAdminUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the price oracle sentinel is updated.
   * @param oldAddress The old address of the PriceOracleSentinel
   * @param newAddress The new address of the PriceOracleSentinel
   */
  event PriceOracleSentinelUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the pool data provider is updated.
   * @param oldAddress The old address of the PoolDataProvider
   * @param newAddress The new address of the PoolDataProvider
   */
  event PoolDataProviderUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the prxy treasury is updated.
   * @param oldAddress The old address of the PrxyTreasury
   * @param newAddress The new address of the PrxyTreasury
   */
  event PrxyTreasuryUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the trading wallet is updated.
   * @param oldAddress The old address of the trading wallet
   * @param newAddress The new address of the trading wallet
   */
  event TradingWalletUpdated(address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when a new proxy is created.
   * @param id The identifier of the proxy
   * @param proxyAddress The address of the created proxy contract
   * @param implementationAddress The address of the implementation contract
   */
  event ProxyCreated(
    bytes32 indexed id,
    address indexed proxyAddress,
    address indexed implementationAddress
  );

  /**
   * @dev Emitted when a new non-proxied contract address is registered.
   * @param id The identifier of the contract
   * @param oldAddress The address of the old contract
   * @param newAddress The address of the new contract
   */
  event AddressSet(bytes32 indexed id, address indexed oldAddress, address indexed newAddress);

  /**
   * @dev Emitted when the implementation of the proxy registered with id is updated
   * @param id The identifier of the contract
   * @param proxyAddress The address of the proxy contract
   * @param oldImplementationAddress The address of the old implementation contract
   * @param newImplementationAddress The address of the new implementation contract
   */
  event AddressSetAsProxy(
    bytes32 indexed id,
    address indexed proxyAddress,
    address oldImplementationAddress,
    address indexed newImplementationAddress
  );

  /**
   * @notice Returns the id of the Aave market to which this contract points to.
   * @return The market id
   **/
  function getMarketId() external view returns (string memory);

  /**
   * @notice Associates an id with a specific PoolAddressesProvider.
   * @dev This can be used to create an onchain registry of PoolAddressesProviders to
   * identify and validate multiple Aave markets.
   * @param newMarketId The market id
   */
  function setMarketId(string calldata newMarketId) external;

  /**
   * @notice Returns an address by its identifier.
   * @dev The returned address might be an EOA or a contract, potentially proxied
   * @dev It returns ZERO if there is no registered address with the given id
   * @param id The id
   * @return The address of the registered for the specified id
   */
  function getAddress(bytes32 id) external view returns (address);

  /**
   * @notice General function to update the implementation of a proxy registered with
   * certain `id`. If there is no proxy registered, it will instantiate one and
   * set as implementation the `newImplementationAddress`.
   * @dev IMPORTANT Use this function carefully, only for ids that don't have an explicit
   * setter function, in order to avoid unexpected consequences
   * @param id The id
   * @param newImplementationAddress The address of the new implementation
   */
  function setAddressAsProxy(bytes32 id, address newImplementationAddress) external;

  /**
   * @notice Sets an address for an id replacing the address saved in the addresses map.
   * @dev IMPORTANT Use this function carefully, as it will do a hard replacement
   * @param id The id
   * @param newAddress The address to set
   */
  function setAddress(bytes32 id, address newAddress) external;

  /**
   * @notice Returns the address of the Pool proxy.
   * @return The Pool proxy address
   **/
  function getPool() external view returns (address);

  /**
   * @notice Updates the implementation of the Pool, or creates a proxy
   * setting the new `pool` implementation when the function is called for the first time.
   * @param newPoolImpl The new Pool implementation
   **/
  function setPoolImpl(address newPoolImpl) external;

  /**
   * @notice Returns the address of the PoolConfigurator proxy.
   * @return The PoolConfigurator proxy address
   **/
  function getPoolConfigurator() external view returns (address);

  /**
   * @notice Updates the implementation of the PoolConfigurator, or creates a proxy
   * setting the new `PoolConfigurator` implementation when the function is called for the first time.
   * @param newPoolConfiguratorImpl The new PoolConfigurator implementation
   **/
  function setPoolConfiguratorImpl(address newPoolConfiguratorImpl) external;

  /**
   * @notice Returns the address of the price oracle.
   * @return The address of the PriceOracle
   */
  function getPriceOracle() external view returns (address);

  /**
   * @notice Updates the address of the price oracle.
   * @param newPriceOracle The address of the new PriceOracle
   */
  function setPriceOracle(address newPriceOracle) external;

  /**
   * @notice Returns the address of the ACL manager.
   * @return The address of the ACLManager
   */
  function getACLManager() external view returns (address);

  /**
   * @notice Updates the address of the ACL manager.
   * @param newAclManager The address of the new ACLManager
   **/
  function setACLManager(address newAclManager) external;

  /**
   * @notice Returns the address of the ACL admin.
   * @return The address of the ACL admin
   */
  function getACLAdmin() external view returns (address);

  /**
   * @notice Updates the address of the ACL admin.
   * @param newAclAdmin The address of the new ACL admin
   */
  function setACLAdmin(address newAclAdmin) external;

  /**
   * @notice Returns the address of the price oracle sentinel.
   * @return The address of the PriceOracleSentinel
   */
  function getPriceOracleSentinel() external view returns (address);

  /**
   * @notice Updates the address of the price oracle sentinel.
   * @param newPriceOracleSentinel The address of the new PriceOracleSentinel
   **/
  function setPriceOracleSentinel(address newPriceOracleSentinel) external;

  /**
   * @notice Returns the address of the data provider.
   * @return The address of the DataProvider
   */
  function getPoolDataProvider() external view returns (address);

  /**
   * @notice Updates the address of the data provider.
   * @param newDataProvider The address of the new DataProvider
   **/
  function setPoolDataProvider(address newDataProvider) external;

  /**
   * @notice Returns the address of the prxy treasury.
   * @return The address of the PrxyTreasury
   */
  function getPrxyTreasury() external view returns (address);

  /**
   * @notice Updates the address of the prxy treasury.
   * @param newPrxyTreasury The address of the new PrxyTreasury
   **/
  function setPrxyTreasury(address newPrxyTreasury) external;

  /**
   * @notice Returns the address of the trading wallet.
   * @return The address of the trading wallet
   */
  function getTradingWallet() external view returns (address);

  /**
   * @notice Updates the address of the trading wallet.
   * @param newTradingWallet The address of the trading wallet
   **/
  function setTradingWallet(address newTradingWallet) external;

  /**
   * @notice Returns the address of the dao wallet.
   * @return The address of the dao wallet
   */
  function getDaoWallet() external view returns (address);

  /**
   * @notice Updates the address of the dao wallet.
   * @param newDaoWallet The address of the dao wallet
   **/
  function setDaoWallet(address newDaoWallet) external;
}

// SPDX-License-Identifier: agpl-3.0
pragma solidity 0.8.10;

import {IERC20} from './IERC20.sol';

interface IERC20Detailed is IERC20 {
  function name() external view returns (string memory);

  function symbol() external view returns (string memory);

  function decimals() external view returns (uint8);
}

// SPDX-License-Identifier: agpl-3.0
pragma solidity 0.8.10;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
  /**
   * @dev Returns the amount of tokens in existence.
   */
  function totalSupply() external view returns (uint256);

  /**
   * @dev Returns the amount of tokens owned by `account`.
   */
  function balanceOf(address account) external view returns (uint256);

  /**
   * @dev Moves `amount` tokens from the caller's account to `recipient`.
   *
   * Returns a boolean value indicating whether the operation succeeded.
   *
   * Emits a {Transfer} event.
   */
  function transfer(address recipient, uint256 amount) external returns (bool);

  /**
   * @dev Returns the remaining number of tokens that `spender` will be
   * allowed to spend on behalf of `owner` through {transferFrom}. This is
   * zero by default.
   *
   * This value changes when {approve} or {transferFrom} are called.
   */
  function allowance(address owner, address spender) external view returns (uint256);

  /**
   * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
   *
   * Returns a boolean value indicating whether the operation succeeded.
   *
   * IMPORTANT: Beware that changing an allowance with this method brings the risk
   * that someone may use both the old and the new allowance by unfortunate
   * transaction ordering. One possible solution to mitigate this race
   * condition is to first reduce the spender's allowance to 0 and set the
   * desired value afterwards:
   * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
   *
   * Emits an {Approval} event.
   */
  function approve(address spender, uint256 amount) external returns (bool);

  /**
   * @dev Moves `amount` tokens from `sender` to `recipient` using the
   * allowance mechanism. `amount` is then deducted from the caller's
   * allowance.
   *
   * Returns a boolean value indicating whether the operation succeeded.
   *
   * Emits a {Transfer} event.
   */
  function transferFrom(
    address sender,
    address recipient,
    uint256 amount
  ) external returns (bool);

  /**
   * @dev Emitted when `value` tokens are moved from one account (`from`) to
   * another (`to`).
   *
   * Note that `value` may be zero.
   */
  event Transfer(address indexed from, address indexed to, uint256 value);

  /**
   * @dev Emitted when the allowance of a `spender` for an `owner` is set by
   * a call to {approve}. `value` is the new allowance.
   */
  event Approval(address indexed owner, address indexed spender, uint256 value);
}

// SPDX-License-Identifier: LGPL-3.0-or-later
pragma solidity 0.8.10;

import {IERC20} from '../../openzeppelin/contracts/IERC20.sol';

/// @title Gnosis Protocol v2 Safe ERC20 Transfer Library
/// @author Gnosis Developers
/// @dev Gas-efficient version of Openzeppelin's SafeERC20 contract.
library GPv2SafeERC20 {
  /// @dev Wrapper around a call to the ERC20 function `transfer` that reverts
  /// also when the token returns `false`.
  function safeTransfer(
    IERC20 token,
    address to,
    uint256 value
  ) internal {
    bytes4 selector_ = token.transfer.selector;

    // solhint-disable-next-line no-inline-assembly
    assembly {
      let freeMemoryPointer := mload(0x40)
      mstore(freeMemoryPointer, selector_)
      mstore(add(freeMemoryPointer, 4), and(to, 0xffffffffffffffffffffffffffffffffffffffff))
      mstore(add(freeMemoryPointer, 36), value)

      if iszero(call(gas(), token, 0, freeMemoryPointer, 68, 0, 0)) {
        returndatacopy(0, 0, returndatasize())
        revert(0, returndatasize())
      }
    }

    require(getLastTransferResult(token), 'GPv2: failed transfer');
  }

  /// @dev Wrapper around a call to the ERC20 function `transferFrom` that
  /// reverts also when the token returns `false`.
  function safeTransferFrom(
    IERC20 token,
    address from,
    address to,
    uint256 value
  ) internal {
    bytes4 selector_ = token.transferFrom.selector;

    // solhint-disable-next-line no-inline-assembly
    assembly {
      let freeMemoryPointer := mload(0x40)
      mstore(freeMemoryPointer, selector_)
      mstore(add(freeMemoryPointer, 4), and(from, 0xffffffffffffffffffffffffffffffffffffffff))
      mstore(add(freeMemoryPointer, 36), and(to, 0xffffffffffffffffffffffffffffffffffffffff))
      mstore(add(freeMemoryPointer, 68), value)

      if iszero(call(gas(), token, 0, freeMemoryPointer, 100, 0, 0)) {
        returndatacopy(0, 0, returndatasize())
        revert(0, returndatasize())
      }
    }

    require(getLastTransferResult(token), 'GPv2: failed transferFrom');
  }

  /// @dev Verifies that the last return was a successful `transfer*` call.
  /// This is done by checking that the return data is either empty, or
  /// is a valid ABI encoded boolean.
  function getLastTransferResult(IERC20 token) private view returns (bool success) {
    // NOTE: Inspecting previous return data requires assembly. Note that
    // we write the return data to memory 0 in the case where the return
    // data size is 32, this is OK since the first 64 bytes of memory are
    // reserved by Solidy as a scratch space that can be used within
    // assembly blocks.
    // <https://docs.soliditylang.org/en/v0.7.6/internals/layout_in_memory.html>
    // solhint-disable-next-line no-inline-assembly
    assembly {
      /// @dev Revert with an ABI encoded Solidity error with a message
      /// that fits into 32-bytes.
      ///
      /// An ABI encoded Solidity error has the following memory layout:
      ///
      /// ------------+----------------------------------
      ///  byte range | value
      /// ------------+----------------------------------
      ///  0x00..0x04 |        selector("Error(string)")
      ///  0x04..0x24 |      string offset (always 0x20)
      ///  0x24..0x44 |                    string length
      ///  0x44..0x64 | string value, padded to 32-bytes
      function revertWithMessage(length, message) {
        mstore(0x00, '\x08\xc3\x79\xa0')
        mstore(0x04, 0x20)
        mstore(0x24, length)
        mstore(0x44, message)
        revert(0x00, 0x64)
      }

      switch returndatasize()
      // Non-standard ERC20 transfer without return.
      case 0 {
        // NOTE: When the return data size is 0, verify that there
        // is code at the address. This is done in order to maintain
        // compatibility with Solidity calling conventions.
        // <https://docs.soliditylang.org/en/v0.7.6/control-structures.html#external-function-calls>
        if iszero(extcodesize(token)) {
          revertWithMessage(20, 'GPv2: not a contract')
        }

        success := 1
      }
      // Standard ERC20 transfer returning boolean success value.
      case 32 {
        returndatacopy(0, 0, returndatasize())

        // NOTE: For ABI encoding v1, any non-zero value is accepted
        // as `true` for a boolean. In order to stay compatible with
        // OpenZeppelin's `SafeERC20` library which is known to work
        // with the existing ERC20 implementation we care about,
        // make sure we return success for any non-zero return value
        // from the `transfer*` call.
        success := iszero(iszero(mload(0)))
      }
      default {
        revertWithMessage(31, 'GPv2: malformed transfer result')
      }
    }
  }
}

// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.10;

library DataTypes {
  struct ReserveData {
    // stores the reserve configuration
    ReserveConfigurationMap configuration;
    // supplying asset address
    address supplyingAsset;
    // prxy asset address
    address prxyAsset;
    // the supply rate.
    uint256 supplyRate;   // Dynamic Rate
    // the borrow rate.
    uint256 borrowRate;   // Fixed Rate
    // the uncollateral prxy rate.
    uint256 prxyRate;     // Fixed Rate
    // the collateral prxy rate.
    uint256 prxyRate2;     // Fixed Rate
    // the split rate.
    uint256 splitRate;    // Fixed Rate
    //the id of the reserve. Represents the position in the list of the active reserves
    uint16 id;
    //pToken address
    address pTokenAddress;
  }

  struct ReserveConfigurationMap {
    //bit 0-15: LTV
    //bit 16-31: Liq. threshold
    //bit 32-47: Liq. bonus
    //bit 48-55: Decimals
    //bit 56: reserve is active
    //bit 57: reserve is frozen
    //bit 58: borrowing is enabled
    //bit 59: stable rate borrowing enabled
    //bit 60: asset is paused
    //bit 61: borrowing in isolation mode is enabled
    //bit 62-63: reserved
    //bit 64-79: reserve factor
    //bit 80-115 borrow cap in whole tokens, borrowCap == 0 => no cap
    //bit 116-151 supply cap in whole tokens, supplyCap == 0 => no cap
    //bit 152-167 liquidation protocol fee
    //bit 168-175 eMode category
    //bit 176-211 unbacked mint cap in whole tokens, unbackedMintCap == 0 => minting disabled
    //bit 212-251 debt ceiling for isolation mode with (ReserveConfiguration::DEBT_CEILING_DECIMALS) decimals
    //bit 252-255 unused
    uint256 data;
  }

  struct UserList {
    address[] list;
    mapping(address => uint256) ids;
  }

  struct ExecuteSupplyParams {
    address asset;
    uint256 amount;
    uint16 referralCode;
    uint256 reservesCount;
  }

  struct ExecuteBorrowParams {
    address asset;
    address borrowingAsset;
    uint256 amount;
    uint16 referralCode;
    address oracle;
    uint256 reservesCount;
  }

  struct ExecuteWithdrawParams {
    address asset;
    uint256 amount;
    address oracle;
    uint256 reservesCount;
  }

  struct ExecuteRepayParams {
    address asset;
    address borrowingAsset;
    uint256 amount;
    uint256 reservesCount;
  }

  struct ExecuteLiquidationCallParams {
    address user;
    address asset;
    address oracle;
    uint256 reservesCount;
  }

  struct InitReserveParams {
    address supplyingAsset;
    uint256 borrowRate;
    uint256 prxyRate;
    uint256 prxyRate2;
    uint256 splitRate;
    address pTokenAddress;
    uint16 reservesCount;
    uint16 maxNumberReserves;
  }

  struct CalculateUserAccountDataParams {
    address user;
    address asset;
    address oracle;
    uint256 reservesCount;
  }

  struct ExecuteSplitReduxInterestParams {
    address asset;
    address daoWallet;
    address oracle;
    uint256 reservesCount;
    uint256 elapsedTime;
  }

  struct ExecuteClaimPrxyParams {
    address asset;
    uint256 amount;
    address prxyTreasury;
  }

  struct ExecuteSwitchSupplyModeParams {
    address asset;
  }

  struct ExecuteAddFundParams {
    address asset;
    uint256 amount;
  }

  struct ExecuteRemoveFundParams {
    address asset;
    uint256 amount;
  }

  struct ExecuteRecoverTokenParams {
    address user;
    address asset;
    uint256 amount;
  }
}