/**
 *Submitted for verification at polygonscan.com on 2022-02-28
*/

// SPDX-License-Identifier: UNLICENSED

// File: interfaces/IVikings.sol

pragma solidity 0.8.7;

interface IVikings {
    function ownerOf(uint256 tokenId) external view returns (address owner);
    function isVikingLocked(uint256 vikingId) external view returns (bool vikingStatus);
    function mintByType(address _owner, uint256 _type) external;
    function lockToken(uint256 tokenId, bool status) external;
    function tokenStrength(uint256 tokenId) external view returns (uint256 strength);
}
// File: interfaces/ICurve.sol


pragma solidity 0.8.7;

interface ICurve {
    function curve (uint256 currentTime, uint256 exponent, uint256 minimumPercentage, uint256 latestOpeningTime) external returns (uint256);
}
// File: interfaces/IAaveIncentivesController.sol


pragma solidity 0.8.7;

interface IAaveIncentivesController {
  event RewardsClaimed(address indexed user, address indexed to, address indexed claimer, uint256 amount);

  function getRewardsBalance(address[] calldata assets, address user) external view returns (uint256);
  function claimRewards(address[] calldata assets, uint256 amount, address to) external returns (uint256);
  function getUserUnclaimedRewards(address user) external view returns (uint256);
  function REWARD_TOKEN() external view returns (address);
}
// File: interfaces/ILendingPool.sol


pragma solidity 0.8.7;

interface ILendingPool {
  event Deposit(address indexed reserve, address user, address indexed onBehalfOf, uint256 amount, uint16 indexed referral);
  event Withdraw(address indexed reserve, address indexed user, address indexed to, uint256 amount);
  
  function deposit(address asset, uint256 amount, address onBehalfOf, uint16 referralCode) external;
  function withdraw(address asset, uint256 amount, address to) external returns (uint256);
}
// File: interfaces/IERC20.sol


pragma solidity 0.8.7;

interface IERC20 {
    function transfer(address recipient, uint256 amount) external returns (bool);
    function balanceOf(address account) external view returns (uint256);
    function approve(address spender, uint256 amount) external returns (bool);
    function allowance(address _owner, address spender) external view returns (uint256);
    function increaseAllowance(address spender, uint256 addedValue) external;
}
// File: @chainlink/contracts/src/v0.8/VRFRequestIDBase.sol


pragma solidity ^0.8.0;

contract VRFRequestIDBase {

  /**
   * @notice returns the seed which is actually input to the VRF coordinator
   *
   * @dev To prevent repetition of VRF output due to repetition of the
   * @dev user-supplied seed, that seed is combined in a hash with the
   * @dev user-specific nonce, and the address of the consuming contract. The
   * @dev risk of repetition is mostly mitigated by inclusion of a blockhash in
   * @dev the final seed, but the nonce does protect against repetition in
   * @dev requests which are included in a single block.
   *
   * @param _userSeed VRF seed input provided by user
   * @param _requester Address of the requesting contract
   * @param _nonce User-specific nonce at the time of the request
   */
  function makeVRFInputSeed(
    bytes32 _keyHash,
    uint256 _userSeed,
    address _requester,
    uint256 _nonce
  )
    internal
    pure
    returns (
      uint256
    )
  {
    return uint256(keccak256(abi.encode(_keyHash, _userSeed, _requester, _nonce)));
  }

  /**
   * @notice Returns the id for this request
   * @param _keyHash The serviceAgreement ID to be used for this request
   * @param _vRFInputSeed The seed to be passed directly to the VRF
   * @return The id for this request
   *
   * @dev Note that _vRFInputSeed is not the seed passed by the consuming
   * @dev contract, but the one generated by makeVRFInputSeed
   */
  function makeRequestId(
    bytes32 _keyHash,
    uint256 _vRFInputSeed
  )
    internal
    pure
    returns (
      bytes32
    )
  {
    return keccak256(abi.encodePacked(_keyHash, _vRFInputSeed));
  }
}
// File: @chainlink/contracts/src/v0.8/interfaces/LinkTokenInterface.sol


pragma solidity ^0.8.0;

interface LinkTokenInterface {

  function allowance(
    address owner,
    address spender
  )
    external
    view
    returns (
      uint256 remaining
    );

  function approve(
    address spender,
    uint256 value
  )
    external
    returns (
      bool success
    );

  function balanceOf(
    address owner
  )
    external
    view
    returns (
      uint256 balance
    );

  function decimals()
    external
    view
    returns (
      uint8 decimalPlaces
    );

  function decreaseApproval(
    address spender,
    uint256 addedValue
  )
    external
    returns (
      bool success
    );

  function increaseApproval(
    address spender,
    uint256 subtractedValue
  ) external;

  function name()
    external
    view
    returns (
      string memory tokenName
    );

  function symbol()
    external
    view
    returns (
      string memory tokenSymbol
    );

  function totalSupply()
    external
    view
    returns (
      uint256 totalTokensIssued
    );

  function transfer(
    address to,
    uint256 value
  )
    external
    returns (
      bool success
    );

  function transferAndCall(
    address to,
    uint256 value,
    bytes calldata data
  )
    external
    returns (
      bool success
    );

  function transferFrom(
    address from,
    address to,
    uint256 value
  )
    external
    returns (
      bool success
    );

}

// File: @chainlink/contracts/src/v0.8/VRFConsumerBase.sol


pragma solidity ^0.8.0;



/** ****************************************************************************
 * @notice Interface for contracts using VRF randomness
 * *****************************************************************************
 * @dev PURPOSE
 *
 * @dev Reggie the Random Oracle (not his real job) wants to provide randomness
 * @dev to Vera the verifier in such a way that Vera can be sure he's not
 * @dev making his output up to suit himself. Reggie provides Vera a public key
 * @dev to which he knows the secret key. Each time Vera provides a seed to
 * @dev Reggie, he gives back a value which is computed completely
 * @dev deterministically from the seed and the secret key.
 *
 * @dev Reggie provides a proof by which Vera can verify that the output was
 * @dev correctly computed once Reggie tells it to her, but without that proof,
 * @dev the output is indistinguishable to her from a uniform random sample
 * @dev from the output space.
 *
 * @dev The purpose of this contract is to make it easy for unrelated contracts
 * @dev to talk to Vera the verifier about the work Reggie is doing, to provide
 * @dev simple access to a verifiable source of randomness.
 * *****************************************************************************
 * @dev USAGE
 *
 * @dev Calling contracts must inherit from VRFConsumerBase, and can
 * @dev initialize VRFConsumerBase's attributes in their constructor as
 * @dev shown:
 *
 * @dev   contract VRFConsumer {
 * @dev     constuctor(<other arguments>, address _vrfCoordinator, address _link)
 * @dev       VRFConsumerBase(_vrfCoordinator, _link) public {
 * @dev         <initialization with other arguments goes here>
 * @dev       }
 * @dev   }
 *
 * @dev The oracle will have given you an ID for the VRF keypair they have
 * @dev committed to (let's call it keyHash), and have told you the minimum LINK
 * @dev price for VRF service. Make sure your contract has sufficient LINK, and
 * @dev call requestRandomness(keyHash, fee, seed), where seed is the input you
 * @dev want to generate randomness from.
 *
 * @dev Once the VRFCoordinator has received and validated the oracle's response
 * @dev to your request, it will call your contract's fulfillRandomness method.
 *
 * @dev The randomness argument to fulfillRandomness is the actual random value
 * @dev generated from your seed.
 *
 * @dev The requestId argument is generated from the keyHash and the seed by
 * @dev makeRequestId(keyHash, seed). If your contract could have concurrent
 * @dev requests open, you can use the requestId to track which seed is
 * @dev associated with which randomness. See VRFRequestIDBase.sol for more
 * @dev details. (See "SECURITY CONSIDERATIONS" for principles to keep in mind,
 * @dev if your contract could have multiple requests in flight simultaneously.)
 *
 * @dev Colliding `requestId`s are cryptographically impossible as long as seeds
 * @dev differ. (Which is critical to making unpredictable randomness! See the
 * @dev next section.)
 *
 * *****************************************************************************
 * @dev SECURITY CONSIDERATIONS
 *
 * @dev A method with the ability to call your fulfillRandomness method directly
 * @dev could spoof a VRF response with any random value, so it's critical that
 * @dev it cannot be directly called by anything other than this base contract
 * @dev (specifically, by the VRFConsumerBase.rawFulfillRandomness method).
 *
 * @dev For your users to trust that your contract's random behavior is free
 * @dev from malicious interference, it's best if you can write it so that all
 * @dev behaviors implied by a VRF response are executed *during* your
 * @dev fulfillRandomness method. If your contract must store the response (or
 * @dev anything derived from it) and use it later, you must ensure that any
 * @dev user-significant behavior which depends on that stored value cannot be
 * @dev manipulated by a subsequent VRF request.
 *
 * @dev Similarly, both miners and the VRF oracle itself have some influence
 * @dev over the order in which VRF responses appear on the blockchain, so if
 * @dev your contract could have multiple VRF requests in flight simultaneously,
 * @dev you must ensure that the order in which the VRF responses arrive cannot
 * @dev be used to manipulate your contract's user-significant behavior.
 *
 * @dev Since the ultimate input to the VRF is mixed with the block hash of the
 * @dev block in which the request is made, user-provided seeds have no impact
 * @dev on its economic security properties. They are only included for API
 * @dev compatability with previous versions of this contract.
 *
 * @dev Since the block hash of the block which contains the requestRandomness
 * @dev call is mixed into the input to the VRF *last*, a sufficiently powerful
 * @dev miner could, in principle, fork the blockchain to evict the block
 * @dev containing the request, forcing the request to be included in a
 * @dev different block with a different hash, and therefore a different input
 * @dev to the VRF. However, such an attack would incur a substantial economic
 * @dev cost. This cost scales with the number of blocks the VRF oracle waits
 * @dev until it calls responds to a request.
 */
abstract contract VRFConsumerBase is VRFRequestIDBase {

  /**
   * @notice fulfillRandomness handles the VRF response. Your contract must
   * @notice implement it. See "SECURITY CONSIDERATIONS" above for important
   * @notice principles to keep in mind when implementing your fulfillRandomness
   * @notice method.
   *
   * @dev VRFConsumerBase expects its subcontracts to have a method with this
   * @dev signature, and will call it once it has verified the proof
   * @dev associated with the randomness. (It is triggered via a call to
   * @dev rawFulfillRandomness, below.)
   *
   * @param requestId The Id initially returned by requestRandomness
   * @param randomness the VRF output
   */
  function fulfillRandomness(
    bytes32 requestId,
    uint256 randomness
  )
    internal
    virtual;

  /**
   * @dev In order to keep backwards compatibility we have kept the user
   * seed field around. We remove the use of it because given that the blockhash
   * enters later, it overrides whatever randomness the used seed provides.
   * Given that it adds no security, and can easily lead to misunderstandings,
   * we have removed it from usage and can now provide a simpler API.
   */
  uint256 constant private USER_SEED_PLACEHOLDER = 0;

  /**
   * @notice requestRandomness initiates a request for VRF output given _seed
   *
   * @dev The fulfillRandomness method receives the output, once it's provided
   * @dev by the Oracle, and verified by the vrfCoordinator.
   *
   * @dev The _keyHash must already be registered with the VRFCoordinator, and
   * @dev the _fee must exceed the fee specified during registration of the
   * @dev _keyHash.
   *
   * @dev The _seed parameter is vestigial, and is kept only for API
   * @dev compatibility with older versions. It can't *hurt* to mix in some of
   * @dev your own randomness, here, but it's not necessary because the VRF
   * @dev oracle will mix the hash of the block containing your request into the
   * @dev VRF seed it ultimately uses.
   *
   * @param _keyHash ID of public key against which randomness is generated
   * @param _fee The amount of LINK to send with the request
   *
   * @return requestId unique ID for this request
   *
   * @dev The returned requestId can be used to distinguish responses to
   * @dev concurrent requests. It is passed as the first argument to
   * @dev fulfillRandomness.
   */
  function requestRandomness(
    bytes32 _keyHash,
    uint256 _fee
  )
    internal
    returns (
      bytes32 requestId
    )
  {
    LINK.transferAndCall(vrfCoordinator, _fee, abi.encode(_keyHash, USER_SEED_PLACEHOLDER));
    // This is the seed passed to VRFCoordinator. The oracle will mix this with
    // the hash of the block containing this request to obtain the seed/input
    // which is finally passed to the VRF cryptographic machinery.
    uint256 vRFSeed  = makeVRFInputSeed(_keyHash, USER_SEED_PLACEHOLDER, address(this), nonces[_keyHash]);
    // nonces[_keyHash] must stay in sync with
    // VRFCoordinator.nonces[_keyHash][this], which was incremented by the above
    // successful LINK.transferAndCall (in VRFCoordinator.randomnessRequest).
    // This provides protection against the user repeating their input seed,
    // which would result in a predictable/duplicate output, if multiple such
    // requests appeared in the same block.
    nonces[_keyHash] = nonces[_keyHash] + 1;
    return makeRequestId(_keyHash, vRFSeed);
  }

  LinkTokenInterface immutable internal LINK;
  address immutable private vrfCoordinator;

  // Nonces for each VRF key from which randomness has been requested.
  //
  // Must stay in sync with VRFCoordinator[_keyHash][this]
  mapping(bytes32 /* keyHash */ => uint256 /* nonce */) private nonces;

  /**
   * @param _vrfCoordinator address of VRFCoordinator contract
   * @param _link address of LINK token contract
   *
   * @dev https://docs.chain.link/docs/link-token-contracts
   */
  constructor(
    address _vrfCoordinator,
    address _link
  ) {
    vrfCoordinator = _vrfCoordinator;
    LINK = LinkTokenInterface(_link);
  }

  // rawFulfillRandomness is called by VRFCoordinator when it receives a valid VRF
  // proof. rawFulfillRandomness then calls fulfillRandomness, after validating
  // the origin of the call
  function rawFulfillRandomness(
    bytes32 requestId,
    uint256 randomness
  )
    external
  {
    require(msg.sender == vrfCoordinator, "Only VRFCoordinator can fulfill");
    fulfillRandomness(requestId, randomness);
  }
}

// File: @uniswap/lib/contracts/libraries/TransferHelper.sol



pragma solidity >=0.6.0;

// helper methods for interacting with ERC20 tokens and sending ETH that do not consistently return true/false
library TransferHelper {
    function safeApprove(
        address token,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('approve(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x095ea7b3, to, value));
        require(
            success && (data.length == 0 || abi.decode(data, (bool))),
            'TransferHelper::safeApprove: approve failed'
        );
    }

    function safeTransfer(
        address token,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, to, value));
        require(
            success && (data.length == 0 || abi.decode(data, (bool))),
            'TransferHelper::safeTransfer: transfer failed'
        );
    }

    function safeTransferFrom(
        address token,
        address from,
        address to,
        uint256 value
    ) internal {
        // bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x23b872dd, from, to, value));
        require(
            success && (data.length == 0 || abi.decode(data, (bool))),
            'TransferHelper::transferFrom: transferFrom failed'
        );
    }

    function safeTransferETH(address to, uint256 value) internal {
        (bool success, ) = to.call{value: value}(new bytes(0));
        require(success, 'TransferHelper::safeTransferETH: ETH transfer failed');
    }
}

// File: @openzeppelin/contracts/utils/Counters.sol


// OpenZeppelin Contracts v4.4.1 (utils/Counters.sol)

pragma solidity ^0.8.0;

/**
 * @title Counters
 * @author Matt Condon (@shrugs)
 * @dev Provides counters that can only be incremented, decremented or reset. This can be used e.g. to track the number
 * of elements in a mapping, issuing ERC721 ids, or counting request ids.
 *
 * Include with `using Counters for Counters.Counter;`
 */
library Counters {
    struct Counter {
        // This variable should never be directly accessed by users of the library: interactions must be restricted to
        // the library's function. As of Solidity v0.5.2, this cannot be enforced, though there is a proposal to add
        // this feature: see https://github.com/ethereum/solidity/issues/4637
        uint256 _value; // default: 0
    }

    function current(Counter storage counter) internal view returns (uint256) {
        return counter._value;
    }

    function increment(Counter storage counter) internal {
        unchecked {
            counter._value += 1;
        }
    }

    function decrement(Counter storage counter) internal {
        uint256 value = counter._value;
        require(value > 0, "Counter: decrement overflow");
        unchecked {
            counter._value = value - 1;
        }
    }

    function reset(Counter storage counter) internal {
        counter._value = 0;
    }
}

// File: @openzeppelin/contracts/utils/Context.sol


// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// File: @openzeppelin/contracts/access/Ownable.sol


// OpenZeppelin Contracts v4.4.1 (access/Ownable.sol)

pragma solidity ^0.8.0;


/**
 * @dev Contract module which provides a basic access control mechanism, where
 * there is an account (an owner) that can be granted exclusive access to
 * specific functions.
 *
 * By default, the owner account will be the one that deploys the contract. This
 * can later be changed with {transferOwnership}.
 *
 * This module is used through inheritance. It will make available the modifier
 * `onlyOwner`, which can be applied to your functions to restrict their use to
 * the owner.
 */
abstract contract Ownable is Context {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    /**
     * @dev Initializes the contract setting the deployer as the initial owner.
     */
    constructor() {
        _transferOwnership(_msgSender());
    }

    /**
     * @dev Returns the address of the current owner.
     */
    function owner() public view virtual returns (address) {
        return _owner;
    }

    /**
     * @dev Throws if called by any account other than the owner.
     */
    modifier onlyOwner() {
        require(owner() == _msgSender(), "Ownable: caller is not the owner");
        _;
    }

    /**
     * @dev Leaves the contract without owner. It will not be possible to call
     * `onlyOwner` functions anymore. Can only be called by the current owner.
     *
     * NOTE: Renouncing ownership will leave the contract without an owner,
     * thereby removing any functionality that is only available to the owner.
     */
    function renounceOwnership() public virtual onlyOwner {
        _transferOwnership(address(0));
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Can only be called by the current owner.
     */
    function transferOwnership(address newOwner) public virtual onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        _transferOwnership(newOwner);
    }

    /**
     * @dev Transfers ownership of the contract to a new account (`newOwner`).
     * Internal function without access restriction.
     */
    function _transferOwnership(address newOwner) internal virtual {
        address oldOwner = _owner;
        _owner = newOwner;
        emit OwnershipTransferred(oldOwner, newOwner);
    }
}

// File: BoatGame.sol


pragma solidity 0.8.7;










contract BoatGame is Ownable, VRFConsumerBase {
    using Counters for Counters.Counter;
    
    enum Status {
        Join,
        Stake,
        Claim
    }
    
    struct StakeData {
        uint256 minPrice;
        uint256 minBNBStake;
        uint256 stakeUSDT;
        uint256 boatInterest;
    }

    struct CurveData {
        uint256 exponent;
        uint256 minimumPercentage;
        uint256 stakeStartTime;
        uint256 latestOpeningTime;
    }
    
    struct Boat {
        Status status;
        uint256 joinDeadline;
        uint256 minParticipants;
        uint256 currentParticipants;
        uint256 winnerIndex;
        uint256 randomNumber;
        uint256 actualOpeningTime;
        uint256 totalStrength;
        uint256 randomValue;
        StakeData stakeData;
        CurveData curveData;
    }

    uint256 constant public DIV_FACTOR = 100000;
    uint256 constant public STRENGTH_FACTOR = 10000;
    uint256 constant public PID = 1;
    uint256 constant public MINT_TYPE = 6;

    uint256 public feePercentage = 50000;//50 percentage 
    uint256 public penaltyFactor = 10000;//10 percentage of penalty
    address public feeReceiver;

    mapping (uint256 => Boat) public boats;
    mapping(address => mapping(uint256 => uint256)) public userStakesAmUSDT;
    mapping(address => mapping(uint256 => uint256)) public userStakesUSDT;
    mapping(uint256 => address[]) public boatParticipants;
    mapping(uint256 => mapping(address => uint256)) public vikingsLocked;

    Counters.Counter public boatsNumber;
    
    ILendingPool public lendingPool;
    IAaveIncentivesController public claimContract;
    IERC20 public usdToken;
    IERC20 public amUSDToken;
    IVikings public vikingsContract;
    ICurve public curveContract;

    bytes32 internal keyHash = 0xf86195cf7690c55907b2b611ebb7343a6f649bff128701cc542f0569e2c549da;
    uint256 internal feeLink = 1e14; // 0.0001 LINK POLYGON MATIC

    uint256 private totalStakeUSD;
    uint256 private maticBalance;

    event UserClaim(address user, uint256 userUSDT, uint256 userWithdraw,uint256 userPercentage);
    event UserParticipation(address user, uint256 boatId, uint256 amount, uint256 investAmount);
    event Withdraw(uint256 boatId, uint256 stakeTotal, uint256 investAmount, uint256 fee, uint256 actualOpeningTime, uint256 latestOpeningTime);
    event UserExit(address user, uint256 userUSDT);
    event ExtendedJoinDeadline(uint256 boatId, uint256 joinDeadline);
    event RequestValues(bytes32 requestId);
    event RandomnessEvent(bytes32 requestId);
    
    constructor (ILendingPool _lendingPool, IERC20 _usdToken, IAaveIncentivesController _claimContract, IERC20 _amUSDToken, address _feeReceiver, ICurve _curve, IVikings _vikingsContract)
    VRFConsumerBase(0x3d2341ADb2D31f1c5530cDC622016af293177AE0, 0xb0897686c545045aFc77CF20eC7A532E3120E0F1) {
        lendingPool = _lendingPool;
        usdToken = _usdToken;
        claimContract = _claimContract;
        amUSDToken = _amUSDToken;
        feeReceiver = _feeReceiver;
        curveContract = _curve;
        vikingsContract = _vikingsContract;
        usdToken.approve(address(_lendingPool), type(uint256).max);
    }

    function setPenaltyFactor(uint256 _penaltyFactor) external onlyOwner {
        penaltyFactor = _penaltyFactor;
    }

    function setFeeReceiver(address _feeReceiver) external onlyOwner {
        feeReceiver = _feeReceiver;
    }

    function setFeePercentage(uint256 _feePercentage) external onlyOwner {
        feePercentage = _feePercentage;
    }

    function increaseAllowance(uint256 _addedValue) external onlyOwner {
        require(_addedValue > 0, "BoatGame: The added values can not be 0");
        usdToken.increaseAllowance(address(lendingPool), _addedValue);
    }

    function claimRewards(address[] memory _claimAssets, address _to) external onlyOwner {
        lendingPool.withdraw(address(usdToken), type(uint256).max, _to);
        claimContract.claimRewards(_claimAssets, type(uint256).max, _to); 
    }

    function goToNextState(uint256 _boatId) external onlyOwner {
        require(_boatId <= boatsNumber.current(), "BoatGame: The boat id does not exist");
        require(block.timestamp >= boats[_boatId].joinDeadline, "BoatGame: The join deadline is not finished");
        if (boats[_boatId].status == Status.Join) {
            if(boats[_boatId].stakeData.stakeUSDT >= boats[_boatId].stakeData.minBNBStake && boats[_boatId].currentParticipants >= boats[_boatId].minParticipants) {
                boats[_boatId].status = Status(uint256(boats[_boatId].status) + 1);
                boats[_boatId].curveData.stakeStartTime = block.timestamp;
            } else {
                boats[_boatId].joinDeadline = block.timestamp + 1 days;
                emit ExtendedJoinDeadline(_boatId,  boats[_boatId].joinDeadline);
            }
        }
    }

    function addBoat(uint256 _minPrice, uint256 _minParticipants, uint256 _joinDeadline, uint256 _latestOpeningTime, uint256 _minBNBStake, uint256 _exponent, uint256 _minimumPercentage) external onlyOwner {
        require(_minPrice > 0, "BoatGame: Minimun price is wrong, not enough price");
        require(_minParticipants > 0, "BoatGame: Minimun participants are wrong, not enough");
        uint256 newBoatId = boatsNumber.current();
        boats[newBoatId].status = Status.Join;
        boats[newBoatId].joinDeadline = block.timestamp + _joinDeadline;
        boats[newBoatId].stakeData.minPrice = _minPrice;
        boats[newBoatId].stakeData.minBNBStake = _minBNBStake;
        boats[newBoatId].minParticipants = _minParticipants;
        boats[newBoatId].curveData.latestOpeningTime = _latestOpeningTime;
        boats[newBoatId].curveData.exponent = _exponent;
        boats[newBoatId].curveData.minimumPercentage = _minimumPercentage;
        boatsNumber.increment();
    }

    function checkBoats() external onlyOwner {
        for (uint256 i = 0; i < boatsNumber.current(); i++) {
            if(boats[i].status == Status.Stake) {
                uint256 result = curveContract.curve((block.timestamp - boats[i].curveData.stakeStartTime), boats[i].curveData.exponent, boats[i].curveData.minimumPercentage, boats[i].curveData.latestOpeningTime);
                uint randomNumber = uint(keccak256(abi.encode(boats[i].randomNumber, boats[i].curveData.stakeStartTime, boats[i].stakeData.stakeUSDT))) % 10000;
                if (randomNumber < result) {
                    _endStakeAndWithdraw(i);
                }
            }
        }
    }

    function participate(uint256 _boatId, uint256 vikingId, uint256 _amount) external {
        require(_boatId <= boatsNumber.current(), "BoatGame: The boat id does not exist");
        require(boats[_boatId].status == Status.Join && boats[_boatId].joinDeadline >= block.timestamp, "BoatGame: Boat participation is not open");
        require(userStakesUSDT[_msgSender()][_boatId] == 0, "BoatGame: The user is already on the boat");
        require(_amount > 0 && _amount >= boats[_boatId].stakeData.minPrice, "BoatGame: The amount to participate is wrong");
        require(usdToken.balanceOf(_msgSender()) >= boats[_boatId].stakeData.minPrice && usdToken.balanceOf(_msgSender()) >= _amount, "BoatGame: User balance is not enough");
        require(vikingsContract.ownerOf(vikingId) == _msgSender(), "BoatGame: The user does not own the viking id");
        require(!vikingsContract.isVikingLocked(vikingId), "BoatGame: The viking is lock");
        vikingsContract.lockToken(vikingId, true);
        vikingsLocked[_boatId][_msgSender()] = vikingId;
        boats[_boatId].currentParticipants = boats[_boatId].currentParticipants + 1;
        boatParticipants[_boatId].push(_msgSender());
        userStakesUSDT[_msgSender()][_boatId] = _amount;
        uint256 nftStrength = getStrengthRate(vikingsContract.tokenStrength(vikingsLocked[_boatId][_msgSender()]));
        boats[_boatId].totalStrength = boats[_boatId].totalStrength + _amount + nftStrength;
        boats[_boatId].stakeData.stakeUSDT = boats[_boatId].stakeData.stakeUSDT + _amount;
        totalStakeUSD =  totalStakeUSD + _amount;
        TransferHelper.safeTransferFrom(address(usdToken), _msgSender(), address(this), _amount);
        lendingPool.deposit(address(usdToken), _amount, address(this), 0);
        emit UserParticipation(_msgSender(), _boatId, userStakesUSDT[_msgSender()][_boatId], userStakesAmUSDT[_msgSender()][_boatId]);
    }
    
    function claimBoat(uint256 _boatId) external {
        require(boats[_boatId].stakeData.stakeUSDT > 0, "BoatGame: Invalid claim, stake total insufficient");
        require(boats[_boatId].status == Status.Claim, "BoatGame: The participation is still active");
        require(userStakesUSDT[_msgSender()][_boatId] > 0, "BoatGame: The user has not right to claim");
        uint256 userUSDT = userStakesUSDT[_msgSender()][_boatId];
        userStakesUSDT[_msgSender()][_boatId] = 0;
        uint256 userPercentage = (userUSDT * DIV_FACTOR) / boats[_boatId].stakeData.stakeUSDT;
        uint256 userInterest = (boats[_boatId].stakeData.boatInterest * userPercentage) / DIV_FACTOR;
        if(boatParticipants[_boatId][boats[_boatId].winnerIndex] == _msgSender()) {
            vikingsContract.mintByType(_msgSender(), MINT_TYPE);
        }
        userStakesAmUSDT[_msgSender()][_boatId] = userUSDT + userInterest;
        vikingsContract.lockToken(vikingsLocked[_boatId][_msgSender()], false);
        TransferHelper.safeTransfer(address(usdToken), _msgSender(), userStakesAmUSDT[_msgSender()][_boatId]);
        emit UserClaim(_msgSender(), userUSDT, userStakesAmUSDT[_msgSender()][_boatId], userPercentage);
    }

    function userExitBoat(uint256 _boatId) external {
        require(_boatId <= boatsNumber.current(), "BoatGame: The boat id does not exist");
        require(boats[_boatId].status == Status.Join, "BoatGame: The status is not Join, exit option is not available");
        require(userStakesUSDT[_msgSender()][_boatId] > 0, "BoatGame: The user has not right to claim");
        uint256 userUSDT = userStakesUSDT[_msgSender()][_boatId];
        boats[_boatId].stakeData.stakeUSDT = boats[_boatId].stakeData.stakeUSDT - userUSDT;
        totalStakeUSD =  totalStakeUSD - userUSDT;
        userStakesUSDT[_msgSender()][_boatId] = 0;
        userStakesAmUSDT[_msgSender()][_boatId] = 0;
        _deleteFromArray(boatParticipants[_boatId], _msgSender());
        boats[_boatId].currentParticipants--;
        uint256 nftStrength = getStrengthRate(vikingsContract.tokenStrength(vikingsLocked[_boatId][_msgSender()]));
        boats[_boatId].totalStrength = boats[_boatId].totalStrength - userUSDT - nftStrength;
        vikingsContract.lockToken(vikingsLocked[_boatId][_msgSender()], false);
        lendingPool.withdraw(address(usdToken), userUSDT, _msgSender());
        emit UserExit(_msgSender(), userUSDT);
    }

    function requestRandomValues() public onlyOwner returns (bytes32 requestId) {
        require(LINK.balanceOf(address(this)) >= feeLink, "BoatGame: Not enough LINK, fill contract with LINK");
        requestId = requestRandomness(keyHash, feeLink);
        emit RequestValues(requestId);
    }

    function getStrengthRate(uint256 _value) public pure returns (uint256) {
        return ((_value * 1e6) * STRENGTH_FACTOR)  / DIV_FACTOR;
    }

    function _endStakeAndWithdraw(uint256 _boatId) internal {
        require(_boatId <= boatsNumber.current(), "BoatGame: The boat id does not exist");
        require(boats[_boatId].status == Status.Stake, "BoatGame: Boat is not in STAKE state");
        boats[_boatId].status = Status.Claim;
        uint256 stakeUSDT = boats[_boatId].stakeData.stakeUSDT;
        boats[_boatId].actualOpeningTime = block.timestamp;
        boats[_boatId].randomValue = _randomBoatIndex(_boatId);
        boats[_boatId].winnerIndex = _checkWinner(_boatId);
        uint256 aTokenBalance = amUSDToken.balanceOf(address(this));
        uint256 currentBenefits = aTokenBalance - totalStakeUSD;
        uint256 boatBenefit = (currentBenefits * stakeUSDT) / totalStakeUSD;
        uint256 fee = boatBenefit * feePercentage / DIV_FACTOR;
        boats[_boatId].stakeData.boatInterest = boatBenefit - fee;
        totalStakeUSD = totalStakeUSD - stakeUSDT;
        uint256 boatWithdraw = stakeUSDT + boats[_boatId].stakeData.boatInterest;
        lendingPool.withdraw(address(usdToken), fee, feeReceiver);
        lendingPool.withdraw(address(usdToken), boatWithdraw, address(this));
        emit Withdraw(_boatId, stakeUSDT, boatBenefit, fee, boats[_boatId].actualOpeningTime, boats[_boatId].curveData.latestOpeningTime);
    }

    function _randomBoatIndex(uint256 _boatId) internal view returns (uint256) {
        return uint256(keccak256(abi.encodePacked(boats[_boatId].stakeData.stakeUSDT, boats[_boatId].randomNumber))) % boats[_boatId].totalStrength;
    }

    function _checkWinner(uint256 _boatId) internal view returns (uint) {
        uint result = 0;
        uint currentStrength = 0;
        for(uint i = 0; i < boatParticipants[_boatId].length; i++) {
            address user = boatParticipants[_boatId][i];
            uint256 nftStrength = getStrengthRate(vikingsContract.tokenStrength(vikingsLocked[_boatId][user]));
            currentStrength = currentStrength + userStakesUSDT[user][_boatId] + nftStrength;
            if (currentStrength >= boats[_boatId].randomValue) {
                result = i;
                break;
            }
        }
        return result;
    }

    function _expandRandomAux(uint256 randomValue, uint256 n) internal pure returns (uint256[] memory expandedValues) {
        expandedValues = new uint256[](n);
        for (uint256 i = 0; i < n; i++) {
            expandedValues[i] = uint256(keccak256(abi.encode(randomValue, i)));
        }
    }

    function _deleteFromArray(address[] storage _array, address _userAddress) internal {
        uint index = 0;
        bool isFound = false;
        uint256 arrayLength = _array.length;
        for(uint i = 0; i < arrayLength; i++) {
            if(_array[i] == _userAddress) {
                index = i;
                isFound = true;
                break;
            }
        }
        if(isFound) {
            _array[index] = _array[arrayLength - 1];
            _array.pop();
        }
    }

    function fulfillRandomness(bytes32 requestId, uint256 randomness) internal override {
        uint256[] memory expandedValues = _expandRandomAux(randomness, boatsNumber.current());
        for (uint256 i = 0; i < expandedValues.length; i++) {
            boats[i].randomNumber = expandedValues[i];
        }
        emit RandomnessEvent(requestId);
    }
}