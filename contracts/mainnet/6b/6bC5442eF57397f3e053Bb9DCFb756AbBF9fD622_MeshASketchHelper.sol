// SPDX-License-Identifier: MIT

/////////////participatory art project by Meshed.xyz///////////////////////////////////////////////////////////////////////////////////
//                                                                                                                                   //
//                                                                                                                                   //
//                                                                          `.:+/.`                                                  //
//                                                                       `-/oyyyyyo:`                                                //
//                                                                   `.:+syyyyyyyyyys/.                                              //
//                                                                .:+syyyyyyyyyyyyyyyyyo:`                                           //
//                                                            `-/osyssosyyyyhhhhhhhhhhhyys/.                                         //
//                                                        `.:osysssosyyyhhysoyhhyhhhhhhhhhyyo:`                                      //
//                                                    `.:+syssyyyshhhhhss+++////+oyhhhhhhhhhhys/.                                    //
//                                                 `-/osyyyyooshhhhyso+++/////////+osyhhhhhhhhhhyo:`                                 //
//                                             `.:+osyyhyssyyhhyss+++///////////////+osyhhhhhhhhhhys+-`                              //
//                                         `.:+oyysyyssyyhhhysooo++///////////////////+syyhhhhhhhhhhhyo:`                            //
//                                      `-/oyyhsshyyyhhhyyooooo+////////+///////////////+oyyhhhhhhhhhhhys/.`                         //
//                                  `.:+syyshhhyhhhhhysooooo++/////++++///////////////////+oyyhhhhhhhhhhhhyo:`                       //
//                             ``-/oyyhyyyshhhhhyyooooo+++++////////++++/////+/////////////+oyyhhhhhhhhhhhhys/.`                     //
//                           `-/oyyhyyyhshhhhhysooooo+++////++///////++++++/+///++/////////////+syhhhhhhhhhhhhhyo:`                  //
//                       `.:+syhhhhhyhhhhhhsooooo+++////+////++//+++++++++++++++++++/////////////+oyhhhhhhhhhhhhhhs/.                //
//                    `-/syhhhhyyhhhhhhyoooooo+++/////+++++/++++++++++++++++++++++++//++////////////oyhhhhhhhhhhhhhs/.``````         //
//                `./oyhhhhyhhhhhhhhsoo+oo+++//////////++++++++++++++++++++++++++++///++/+////////////+shhhhhhhhhh/`````....`        //
//             .:+yhhhhhhhhhhhhhyooosoo++//////////+++++++++++++++++++++++++++++++++++/++//+/////////////oyyyhhddy.``.......``       //
//         `-+syhhhhhhhhhhhhhsooosoo+++////////////+++++++++++++++++++++++++++++++++++++++//++/+//++///////sshddmo....`.`````..      //
//      -:+yhhhhhhhhhhhhhhhoossoo++++///////+////++++++++++++++++++++++++++++++++++++++++++++++++///////+oyhhhhdmh:........../+/:`   //
//     :sy++yhhhhhhhhhhhhhyso++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++//////+oyhhhhhhhhddhs+::-..-.:+syyo   //
//     -+oys+ohhhhhhhhhhhhhso++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++/////+oyhhhhhhhhhhhhhhhhhyo+/+syyyso+   //
//    `.ooosysoshhhhhhhhhhhhyo++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++/////+syhhhhhhhhhhhhhhhhhys+//oyyyso++o:   //
//    ``.+ooosysoyhhhhhhhhhhhhyo+++++++++++++++++++++++++++++++++++++++++++++++++++++++////++syhhhhhhhhhhhhhhhhhhs+//oyyyso++oo/-`   //
//      `.:ooooyyssyhhhhhhhhhhhhyo++++++++++++++++++++++++++++++++++++++++++++++++++++//++syhhhhhhhhhhhhhhhhhys+//oyyysooooo/-`      //
//       `.-/ooosyysshhhhhhhhhhhhhso++++++++++++++++++++++++++++++++++++++++++++++///+osyhhhhhhhhhhhhhhhhhys+:/oyyysooooo+-`         //
//        `.-:+sooshysyhhhhhhhhhhhhhso+++++++++++++++++++++++++++++++++++++++++///+osyhhhhhhhhhhhhhhhhhys/::osyysooooo+:`            //
//         ``.-/osooyhysyhhhhhhhhhhhhhs+++++++++++++++++++++++++++++++++++++///+oshhhhhhhhhhhhhhhhhhhs+::+yhysooooo+:`               //
//           `.-:/soosyhyyhhhhhhhhhhhhhys+++++++++++++++++++++++++++++++++//+oyhhhhhhhhhhhhhhhhhhhs+::+yhysooooso:`                  //
//            ``.-:+sooshhyyhhhhhhhhhhhhhyo++++++++++++++++++++++++++++//+oyhhhhhhhhhhhhhhhhhhhs/--+shysooooso:.                     //
//              `.-:/osooyhhyyhhhhhhhhhhhhhyo++++++++++++++++++++++++/+oyhhhhhhhhhhhhhhhhhhhs/--+shyysoooso/.                        //
//                `.-:/ssooyhhyhhhhhhhhhhhhhhyo+++++++++++++++++++/+syhhhhhhhhhhhhhhhhhhhs/--+yhyssoooso/.                           //
//                 ``.-:+ssosyhhhhhhhhhhhhhhhhhso++++++++++++++/+syhhhhhhhhhhhhhhhhhhhs/--+shyssoooso/.                              //
//                   `.-:/osooshhhhhhhhhhddhhhhhhs+++++++++/+osyhhhhhhhhhhhhhhhhhhhs+-:+syyysoooso/.`                                //
//                     `.-:/ssooyhhhhhhhhdhhhhhhhhhs++++++osyhhhhhhhhhhhhhhhhhhhs+::+syyysoooso/-`                                   //
//                      `.-:/+ssosyhhhhhhhhhhhhhhhhhyooosyhhhhhhhhhhhhhhhhhhhs+::/syyysoooso+-`                                      //
//                        `.-:/oysoshhhhhhhhhhhdhy+:-...--/shhddhhhhhhhhhhs+::/syyysoooso+-`                                         //
//                         ``-:/+ssooyhddhhhhhdh+...`.......+hhhhhdddhhs+::/syyysoooso+-`                                            //
//                           `.-:/oysosyhdhhddds...........`.yhhddhhy+::/syyysoooss+:.                                               //
//                             `.-:/oyssshhdddms.........````sdhhyo::/syhyssoooso:.                                                  //
//                              ``-::+syssyhddmh..........``.yyo/:/syhyssoosso:.                                                     //
//                                `.-::+ysssydddo-..........:/:/syhyssoooso/.                                                        //
//                                  `.-:/oyssshddho/:---::::+shhyssoooss/.                                                           //
//                                    `.-:/syssshddddhs+/+shhyssoooss+.                                                              //
//                                      ``.:/yyssyhhho+shhyssoooss+-`                                                                //
//                                        `.-:+yyssyyhyyssoooss+-`                                                                   //
//                                          `.-:oyssyssoooss+-`                                                                      //
//                                           ``.:/sysoosso:`                                                                         //
//                                             `.-:+ss+:.                                                                            //
//                                               ``..`                                                                               //
//                                                                                                                                   //
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
pragma solidity ^0.8.4;

import "base64-sol/base64.sol";
import "./IMeshASketchHelper.sol";

contract MeshASketchHelper is IMeshASketchHelper {
    bytes constant addressAlphabet = "0123456789abcdef";
    bytes constant addressAttributePrefix =
        '{"trait_type":"Contributor","value":"';

    /**
     * JSONifys the token information
     */
    function getMetadata(
        address payable[] memory contributorAddresses,
        uint256 remainingContributions,
        string memory path
    ) external pure override returns (string memory) {
        return
            string(
                abi.encodePacked(
                    "data:application/json;base64,",
                    Base64.encode(
                        bytes(
                            abi.encodePacked(
                                '{"name":"',
                                'Mesh A Sketch","attributes":[{"trait_type":"Contributions remaining","display_type":"number","value":',
                                toString(remainingContributions),
                                "},",
                                serializeAddresses(contributorAddresses),
                                '],"image":"',
                                abi.encodePacked(
                                    "data:image/svg+xml;base64,",
                                    Base64.encode(
                                        bytes(
                                            abi.encodePacked(
                                                '<svg height="200" width="200" xmlns="http://www.w3.org/2000/svg"><path d="',
                                                path,
                                                '" fill="none" stroke="#000" stroke-width="1"/></svg>'
                                            )
                                        )
                                    )
                                ),
                                '"}'
                            )
                        )
                    )
                )
            );
    }

    /**
     * Openzepplin implementation from Strings.sol
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value)
        public
        pure
        override
        returns (string memory)
    {
        if (value == 0) {
            return "0";
        }
        uint256 temp = value;
        uint256 digits;
        while (temp != 0) {
            digits++;
            temp /= 10;
        }
        bytes memory buffer = new bytes(digits);
        while (value != 0) {
            digits -= 1;
            buffer[digits] = bytes1(uint8(48 + uint256(value % 10)));
            value /= 10;
        }
        return string(buffer);
    }

    function toString(bytes memory data) public pure returns (string memory) {
        bytes memory str = new bytes(2 + data.length * 2);

        for (uint256 i = 0; i < data.length; i++) {}
        return string(str);
    }

    /**
     * String length = 42 char addresses + comma and -1 for the final comma
     */
    function serializeAddresses(address payable[] memory addresses)
        internal
        pure
        returns (string memory)
    {
        /* 42 char addresses with quotations plus one comma, less final last comma */
        bytes memory commaSeparatedAddresses = new bytes(
            addresses.length * 82 - 1
        );
        uint256 currentWriteIndex = 0;
        for (uint256 i = 0; i < addresses.length; i++) {
            /* Only add comma if not doing first iteration */
            if (i > 0) {
                commaSeparatedAddresses[currentWriteIndex++] = ",";
            }

            for (uint256 j = 0; j < addressAttributePrefix.length; j++) {
                commaSeparatedAddresses[
                    currentWriteIndex++
                ] = addressAttributePrefix[j];
            }

            bytes memory addressBytes = abi.encodePacked(addresses[i]);
            commaSeparatedAddresses[currentWriteIndex++] = "0";
            commaSeparatedAddresses[currentWriteIndex++] = "x";
            for (uint256 j = 0; j < addressBytes.length; j++) {
                commaSeparatedAddresses[currentWriteIndex++] = addressAlphabet[
                    uint256(uint8(addressBytes[j] >> 4))
                ];
                commaSeparatedAddresses[currentWriteIndex++] = addressAlphabet[
                    uint256(uint8(addressBytes[j] & 0x0f))
                ];
            }

            commaSeparatedAddresses[currentWriteIndex++] = '"';
            commaSeparatedAddresses[currentWriteIndex++] = "}";
        }
        return string(commaSeparatedAddresses);
    }
}

// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0;

/// @title Base64
/// @author Brecht Devos - <[emailÂ protected]>
/// @notice Provides functions for encoding/decoding base64
library Base64 {
    string internal constant TABLE_ENCODE = 'ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/';
    bytes  internal constant TABLE_DECODE = hex"0000000000000000000000000000000000000000000000000000000000000000"
                                            hex"00000000000000000000003e0000003f3435363738393a3b3c3d000000000000"
                                            hex"00000102030405060708090a0b0c0d0e0f101112131415161718190000000000"
                                            hex"001a1b1c1d1e1f202122232425262728292a2b2c2d2e2f303132330000000000";

    function encode(bytes memory data) internal pure returns (string memory) {
        if (data.length == 0) return '';

        // load the table into memory
        string memory table = TABLE_ENCODE;

        // multiply by 4/3 rounded up
        uint256 encodedLen = 4 * ((data.length + 2) / 3);

        // add some extra buffer at the end required for the writing
        string memory result = new string(encodedLen + 32);

        assembly {
            // set the actual output length
            mstore(result, encodedLen)

            // prepare the lookup table
            let tablePtr := add(table, 1)

            // input ptr
            let dataPtr := data
            let endPtr := add(dataPtr, mload(data))

            // result ptr, jump over length
            let resultPtr := add(result, 32)

            // run over the input, 3 bytes at a time
            for {} lt(dataPtr, endPtr) {}
            {
                // read 3 bytes
                dataPtr := add(dataPtr, 3)
                let input := mload(dataPtr)

                // write 4 characters
                mstore8(resultPtr, mload(add(tablePtr, and(shr(18, input), 0x3F))))
                resultPtr := add(resultPtr, 1)
                mstore8(resultPtr, mload(add(tablePtr, and(shr(12, input), 0x3F))))
                resultPtr := add(resultPtr, 1)
                mstore8(resultPtr, mload(add(tablePtr, and(shr( 6, input), 0x3F))))
                resultPtr := add(resultPtr, 1)
                mstore8(resultPtr, mload(add(tablePtr, and(        input,  0x3F))))
                resultPtr := add(resultPtr, 1)
            }

            // padding with '='
            switch mod(mload(data), 3)
            case 1 { mstore(sub(resultPtr, 2), shl(240, 0x3d3d)) }
            case 2 { mstore(sub(resultPtr, 1), shl(248, 0x3d)) }
        }

        return result;
    }

    function decode(string memory _data) internal pure returns (bytes memory) {
        bytes memory data = bytes(_data);

        if (data.length == 0) return new bytes(0);
        require(data.length % 4 == 0, "invalid base64 decoder input");

        // load the table into memory
        bytes memory table = TABLE_DECODE;

        // every 4 characters represent 3 bytes
        uint256 decodedLen = (data.length / 4) * 3;

        // add some extra buffer at the end required for the writing
        bytes memory result = new bytes(decodedLen + 32);

        assembly {
            // padding with '='
            let lastBytes := mload(add(data, mload(data)))
            if eq(and(lastBytes, 0xFF), 0x3d) {
                decodedLen := sub(decodedLen, 1)
                if eq(and(lastBytes, 0xFFFF), 0x3d3d) {
                    decodedLen := sub(decodedLen, 1)
                }
            }

            // set the actual output length
            mstore(result, decodedLen)

            // prepare the lookup table
            let tablePtr := add(table, 1)

            // input ptr
            let dataPtr := data
            let endPtr := add(dataPtr, mload(data))

            // result ptr, jump over length
            let resultPtr := add(result, 32)

            // run over the input, 4 characters at a time
            for {} lt(dataPtr, endPtr) {}
            {
               // read 4 characters
               dataPtr := add(dataPtr, 4)
               let input := mload(dataPtr)

               // write 3 bytes
               let output := add(
                   add(
                       shl(18, and(mload(add(tablePtr, and(shr(24, input), 0xFF))), 0xFF)),
                       shl(12, and(mload(add(tablePtr, and(shr(16, input), 0xFF))), 0xFF))),
                   add(
                       shl( 6, and(mload(add(tablePtr, and(shr( 8, input), 0xFF))), 0xFF)),
                               and(mload(add(tablePtr, and(        input , 0xFF))), 0xFF)
                    )
                )
                mstore(resultPtr, shl(232, output))
                resultPtr := add(resultPtr, 3)
            }
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.4;

interface IMeshASketchHelper {
    /**
     * JSONifys the token information
     */
    function getMetadata(
        address payable[] memory contributorAddresses,
        uint256 remainingContributions,
        string memory path
    ) external pure returns (string memory);

    function toString(uint256 value) external view returns (string memory);
}