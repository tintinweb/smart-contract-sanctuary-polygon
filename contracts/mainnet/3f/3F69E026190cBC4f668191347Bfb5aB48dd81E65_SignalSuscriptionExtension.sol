// SPDX-License-Identifier: MIT

pragma solidity >=0.6.0 <0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `recipient`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `sender` to `recipient` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address sender, address recipient, uint256 amount) external returns (bool);

    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);
}

/*
    Copyright 2020 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0

*/

pragma solidity 0.6.10;

/**
 * @title AddressArrayUtils
 * @author Set Protocol
 *
 * Utility functions to handle Address Arrays
 *
 * CHANGELOG:
 * - 4/21/21: Added validatePairsWithArray methods
 */
library AddressArrayUtils {

    /**
     * Finds the index of the first occurrence of the given element.
     * @param A The input array to search
     * @param a The value to find
     * @return Returns (index and isIn) for the first occurrence starting from index 0
     */
    function indexOf(address[] memory A, address a) internal pure returns (uint256, bool) {
        uint256 length = A.length;
        for (uint256 i = 0; i < length; i++) {
            if (A[i] == a) {
                return (i, true);
            }
        }
        return (uint256(-1), false);
    }

    /**
    * Returns true if the value is present in the list. Uses indexOf internally.
    * @param A The input array to search
    * @param a The value to find
    * @return Returns isIn for the first occurrence starting from index 0
    */
    function contains(address[] memory A, address a) internal pure returns (bool) {
        (, bool isIn) = indexOf(A, a);
        return isIn;
    }

    /**
    * Returns true if there are 2 elements that are the same in an array
    * @param A The input array to search
    * @return Returns boolean for the first occurrence of a duplicate
    */
    function hasDuplicate(address[] memory A) internal pure returns(bool) {
        require(A.length > 0, "A is empty");

        for (uint256 i = 0; i < A.length - 1; i++) {
            address current = A[i];
            for (uint256 j = i + 1; j < A.length; j++) {
                if (current == A[j]) {
                    return true;
                }
            }
        }
        return false;
    }

    /**
     * @param A The input array to search
     * @param a The address to remove
     * @return Returns the array with the object removed.
     */
    function remove(address[] memory A, address a)
        internal
        pure
        returns (address[] memory)
    {
        (uint256 index, bool isIn) = indexOf(A, a);
        if (!isIn) {
            revert("Address not in array.");
        } else {
            (address[] memory _A,) = pop(A, index);
            return _A;
        }
    }

    /**
     * @param A The input array to search
     * @param a The address to remove
     */
    function removeStorage(address[] storage A, address a)
        internal
    {
        (uint256 index, bool isIn) = indexOf(A, a);
        if (!isIn) {
            revert("Address not in array.");
        } else {
            uint256 lastIndex = A.length - 1; // If the array would be empty, the previous line would throw, so no underflow here
            if (index != lastIndex) { A[index] = A[lastIndex]; }
            A.pop();
        }
    }

    /**
    * Removes specified index from array
    * @param A The input array to search
    * @param index The index to remove
    * @return Returns the new array and the removed entry
    */
    function pop(address[] memory A, uint256 index)
        internal
        pure
        returns (address[] memory, address)
    {
        uint256 length = A.length;
        require(index < A.length, "Index must be < A length");
        address[] memory newAddresses = new address[](length - 1);
        for (uint256 i = 0; i < index; i++) {
            newAddresses[i] = A[i];
        }
        for (uint256 j = index + 1; j < length; j++) {
            newAddresses[j - 1] = A[j];
        }
        return (newAddresses, A[index]);
    }

    /**
     * Returns the combination of the two arrays
     * @param A The first array
     * @param B The second array
     * @return Returns A extended by B
     */
    function extend(address[] memory A, address[] memory B) internal pure returns (address[] memory) {
        uint256 aLength = A.length;
        uint256 bLength = B.length;
        address[] memory newAddresses = new address[](aLength + bLength);
        for (uint256 i = 0; i < aLength; i++) {
            newAddresses[i] = A[i];
        }
        for (uint256 j = 0; j < bLength; j++) {
            newAddresses[aLength + j] = B[j];
        }
        return newAddresses;
    }

    /**
     * Validate that address and uint array lengths match. Validate address array is not empty
     * and contains no duplicate elements.
     *
     * @param A         Array of addresses
     * @param B         Array of uint
     */
    function validatePairsWithArray(address[] memory A, uint[] memory B) internal pure {
        require(A.length == B.length, "Array length mismatch");
        _validateLengthAndUniqueness(A);
    }

    /**
     * Validate that address and bool array lengths match. Validate address array is not empty
     * and contains no duplicate elements.
     *
     * @param A         Array of addresses
     * @param B         Array of bool
     */
    function validatePairsWithArray(address[] memory A, bool[] memory B) internal pure {
        require(A.length == B.length, "Array length mismatch");
        _validateLengthAndUniqueness(A);
    }

    /**
     * Validate that address and string array lengths match. Validate address array is not empty
     * and contains no duplicate elements.
     *
     * @param A         Array of addresses
     * @param B         Array of strings
     */
    function validatePairsWithArray(address[] memory A, string[] memory B) internal pure {
        require(A.length == B.length, "Array length mismatch");
        _validateLengthAndUniqueness(A);
    }

    /**
     * Validate that address array lengths match, and calling address array are not empty
     * and contain no duplicate elements.
     *
     * @param A         Array of addresses
     * @param B         Array of addresses
     */
    function validatePairsWithArray(address[] memory A, address[] memory B) internal pure {
        require(A.length == B.length, "Array length mismatch");
        _validateLengthAndUniqueness(A);
    }

    /**
     * Validate that address and bytes array lengths match. Validate address array is not empty
     * and contains no duplicate elements.
     *
     * @param A         Array of addresses
     * @param B         Array of bytes
     */
    function validatePairsWithArray(address[] memory A, bytes[] memory B) internal pure {
        require(A.length == B.length, "Array length mismatch");
        _validateLengthAndUniqueness(A);
    }

    /**
     * Validate address array is not empty and contains no duplicate elements.
     *
     * @param A          Array of addresses
     */
    function _validateLengthAndUniqueness(address[] memory A) internal pure {
        require(A.length > 0, "Array length must be > 0");
        require(!hasDuplicate(A), "Cannot duplicate addresses");
    }
}

/*
    Copyright 2020 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/
pragma solidity 0.6.10;

interface IController {
    function addSet(address _jasperVault) external;
    function feeRecipient() external view returns(address);
    function getModuleFee(address _module, uint256 _feeType) external view returns(uint256);
    function isModule(address _module) external view returns(bool);
    function isSet(address _jasperVault) external view returns(bool);
    function isSystemContract(address _contractAddress) external view returns (bool);
    function resourceId(uint256 _id) external view returns(address);
}

/*
    Copyright 2020 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/
pragma solidity 0.6.10;

interface IIntegrationRegistry {
    function addIntegration(address _module, string memory _id, address _wrapper) external;
    function getIntegrationAdapter(address _module, string memory _id) external view returns(address);
    function getIntegrationAdapterWithHash(address _module, bytes32 _id) external view returns(address);
    function isValidIntegration(address _module, string memory _id) external view returns(bool);
}

/*
    Copyright 2020 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/
pragma solidity 0.6.10;
pragma experimental "ABIEncoderV2";

import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

/**
 * @title IJasperVault
 * @author Set Protocol
 *
 * Interface for operating with SetTokens.
 */
interface IJasperVault is IERC20 {
    /* ============ Enums ============ */

    enum ModuleState {
        NONE,
        PENDING,
        INITIALIZED
    }

    /* ============ Structs ============ */
    /**
     * The base definition of a SetToken Position
     *
     * @param component           Address of token in the Position
     * @param module              If not in default state, the address of associated module
     * @param unit                Each unit is the # of components per 10^18 of a SetToken
     * @param positionState       Position ENUM. Default is 0; External is 1
     * @param data                Arbitrary data
     */
    struct Position {
        address component;
        address module;
        int256 unit;
        uint8 positionState;
        bytes data;
        uint256 coinType;
    }

    /**
     * A struct that stores a component's cash position details and external positions
     * This data structure allows O(1) access to a component's cash position units and
     * virtual units.
     *
     * @param virtualUnit               Virtual value of a component's DEFAULT position. Stored as virtual for efficiency
     *                                  updating all units at once via the position multiplier. Virtual units are achieved
     *                                  by dividing a "real" value by the "positionMultiplier"
     * @param componentIndex
     * @param externalPositionModules   List of external modules attached to each external position. Each module
     *                                  maps to an external position
     * @param externalPositions         Mapping of module => ExternalPosition struct for a given component
     */
    struct ComponentPosition {
        int256 virtualUnit;
        uint256 coinType;
        address[] externalPositionModules;
        mapping(address => ExternalPosition) externalPositions;
    }

    /**
     * A struct that stores a component's external position details including virtual unit and any
     * auxiliary data.
     *
     * @param virtualUnit       Virtual value of a component's EXTERNAL position.
     * @param data              Arbitrary data
     */
    struct ExternalPosition {
        uint256 coinType;
        int256 virtualUnit;
        bytes data;
    }

    /* ============ Functions ============ */
    function controller() external view returns (address);

    function editDefaultPositionCoinType(
        address _component,
        uint256 coinType
    ) external;

    function editExternalPositionCoinType(
        address _component,
        address _module,
        uint256 coinType
    ) external;

    function addComponent(address _component) external;

    function removeComponent(address _component) external;

    function editDefaultPositionUnit(
        address _component,
        int256 _realUnit
    ) external;

    function addExternalPositionModule(
        address _component,
        address _positionModule
    ) external;

    function removeExternalPositionModule(
        address _component,
        address _positionModule
    ) external;

    function editExternalPositionUnit(
        address _component,
        address _positionModule,
        int256 _realUnit
    ) external;

    function editExternalPositionData(
        address _component,
        address _positionModule,
        bytes calldata _data
    ) external;

    function invoke(
        address _target,
        uint256 _value,
        bytes calldata _data
    ) external returns (bytes memory);

    function editPositionMultiplier(int256 _newMultiplier) external;

    function mint(address _account, uint256 _quantity) external;

    function burn(address _account, uint256 _quantity) external;

    function lock() external;

    function unlock() external;

    function addModule(address _module) external;

    function removeModule(address _module) external;

    function initializeModule() external;

    function setManager(address _manager) external;

    function manager() external view returns (address);

    function moduleStates(address _module) external view returns (ModuleState);

    function getModules() external view returns (address[] memory);

    function getDefaultPositionRealUnit(
        address _component
    ) external view returns (int256);

    function getExternalPositionRealUnit(
        address _component,
        address _positionModule
    ) external view returns (int256);

    function getComponents() external view returns (address[] memory);

    function getExternalPositionModules(
        address _component
    ) external view returns (address[] memory);

    function getExternalPositionData(
        address _component,
        address _positionModule
    ) external view returns (bytes memory);

    function isExternalPositionModule(
        address _component,
        address _module
    ) external view returns (bool);

    function isComponent(address _component) external view returns (bool);

    function positionMultiplier() external view returns (int256);

    function getPositions() external view returns (Position[] memory);

    function getTotalComponentRealUnits(
        address _component
    ) external view returns (int256);

    function isInitializedModule(address _module) external view returns (bool);

    function isPendingModule(address _module) external view returns (bool);

    function isLocked() external view returns (bool);

    function masterToken() external view returns (address);

    function setBaseProperty(string memory _name,    string memory _symbol) external;    
    function setBaseFeeAndToken(address _masterToken,uint256 _followFee,uint256 _profitShareFee) external;

     function followFee() external view returns(uint256);
     function profitShareFee() external view returns(uint256);

}

/*
    Copyright 2020 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/
pragma solidity 0.6.10;

/**
 * @title IPriceOracle
 * @author Set Protocol
 *
 * Interface for interacting with PriceOracle
 */
interface IPriceOracle {

    /* ============ Functions ============ */

    function getPrice(address _assetOne, address _assetTwo) external view returns (uint256);
    function masterQuoteAsset() external view returns (address);
}

/*
    Copyright 2020 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/
pragma solidity 0.6.10;

import { IJasperVault } from "../interfaces/IJasperVault.sol";

interface ISetValuer {
    function calculateSetTokenValuation(IJasperVault _jasperVault, address _quoteAsset) external view returns (uint256);
}

/*
    Copyright 2020 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/
pragma solidity 0.6.10;
import {IJasperVault} from "./IJasperVault.sol";

interface ISignalSuscriptionModule {
    function subscribe(IJasperVault _jasperVault, address target) external;

    function unsubscribe(IJasperVault _jasperVault, address target) external;

    function exectueFollowStart(address _jasperVault) external;
    function exectueFollowEnd(address _jasperVault) external;
    
    function isExectueFollow(address _jasperVault) external view returns (bool);
  
    function warningLine() external view returns(uint256);

    function unsubscribeLine() external view returns(uint256);

    function handleFee(IJasperVault _jasperVault) external;

    function udpate_allowedCopytrading(
        IJasperVault _jasperVault, 
        bool can_copy_trading
    ) external;

    function get_followers(address target)
        external
        view
        returns (address[] memory);

    function get_signal_provider(IJasperVault _jasperVault)
        external
        view
        returns (address);
}

/*
    Copyright 2022 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/

pragma solidity 0.6.10;

import {IJasperVault} from "../../interfaces/IJasperVault.sol";
import {ISignalSuscriptionModule} from "../../interfaces/ISignalSuscriptionModule.sol";

import {BaseGlobalExtension} from "../lib/BaseGlobalExtension.sol";
import {IDelegatedManager} from "../interfaces/IDelegatedManager.sol";
import {IManagerCore} from "../interfaces/IManagerCore.sol";

interface IOwnable{
    function owner() external returns(address);
}


/**
 * @title TradeExtension
 * @author Set Protocol
 *
 * Smart contract global extension which provides DelegatedManager privileged operator(s) the ability to trade on a DEX
 * and the owner the ability to restrict operator(s) permissions with an asset whitelist.
 */
contract SignalSuscriptionExtension is BaseGlobalExtension {
    /* ============ Events ============ */

    event SignalSuscriptionExtensionInitialized(
        address indexed _jasperVault,
        address indexed _delegatedManager
    );

    event SetFee(
        IJasperVault indexed _jasperVault,
        uint256 _followFee,
        uint256 _profitShareFee
    );

    // event SetSubscribeTarget(
    //      address indexed _jasperVault,
    //      address target
    // );
    event SetSubscribeStatus(IJasperVault indexed _jasperVault, uint256 status);

    event SetWhiteList(
        IJasperVault indexed _jasperVault,
        address user,
        bool status
    );

    /* ============ State Variables ============ */

    // Instance of SignalSuscriptionModule
    ISignalSuscriptionModule public immutable signalSuscriptionModule;

    //whiteList
    mapping(IJasperVault => mapping(address => bool)) public whiteList;

    mapping(IJasperVault => bool) public allowSubscribe;
    
    
    /* ============ Modifiers ============ */
    modifier ValidWhitelist(IJasperVault _jasperVault) {
        require(
            allowSubscribe[_jasperVault],
            "jasperVault not allow subscribe"
        );
        address owner=IOwnable(msg.sender).owner();
        require(
            whiteList[_jasperVault][owner],
            "user is not in the whitelist"
        );
        _;
    }

    /* ============ Constructor ============ */

    constructor(
        IManagerCore _managerCore,
        ISignalSuscriptionModule _signalSuscriptionModule
    ) public BaseGlobalExtension(_managerCore) {
        signalSuscriptionModule = _signalSuscriptionModule;
    }

    /* ============ External Functions ============ */
    function setWhiteListAndSubscribeStatus(
        IJasperVault _jasperVault,
        address[] memory _addList,
        address[] memory _delList,
        bool _status
    ) external onlyOperator(_jasperVault) {
        allowSubscribe[_jasperVault] = _status;
        for (uint256 i = 0; i < _addList.length; i++) {
            whiteList[_jasperVault][_addList[i]] = true;
            emit SetWhiteList(_jasperVault, _addList[i], true);
        }
        for (uint256 i = 0; i < _delList.length; i++) {
            whiteList[_jasperVault][_delList[i]] = false;
            emit SetWhiteList(_jasperVault, _delList[i], false);
        }
    }

    /**
     * ONLY OWNER: Initializes SignalSuscriptionModule on the JasperVault associated with the DelegatedManager.
     *
     * @param _delegatedManager     Instance of the DelegatedManager to initialize the TradeModule for
     */
    function initializeModule(
        IDelegatedManager _delegatedManager
    ) external onlyOwnerAndValidManager(_delegatedManager) {
        require(
            _delegatedManager.isInitializedExtension(address(this)),
            "Extension must be initialized"
        );

        _initializeModule(_delegatedManager.jasperVault(), _delegatedManager);
    }

    /**
     * ONLY OWNER: Initializes TradeExtension to the DelegatedManager.
     *
     * @param _delegatedManager     Instance of the DelegatedManager to initialize
     */
    function initializeExtension(
        IDelegatedManager _delegatedManager
    ) external onlyOwnerAndValidManager(_delegatedManager) {
        require(
            _delegatedManager.isPendingExtension(address(this)),
            "Extension must be pending"
        );

        IJasperVault jasperVault = _delegatedManager.jasperVault();

        _initializeExtension(jasperVault, _delegatedManager);

        emit SignalSuscriptionExtensionInitialized(
            address(jasperVault),
            address(_delegatedManager)
        );
    }

    /**
     * ONLY OWNER: Initializes TradeExtension to the DelegatedManager and TradeModule to the JasperVault
     *
     * @param _delegatedManager     Instance of the DelegatedManager to initialize
     */
    function initializeModuleAndExtension(
        IDelegatedManager _delegatedManager
    ) external onlyOwnerAndValidManager(_delegatedManager) {
        require(
            _delegatedManager.isPendingExtension(address(this)),
            "Extension must be pending"
        );

        IJasperVault jasperVault = _delegatedManager.jasperVault();

        _initializeExtension(jasperVault, _delegatedManager);
        _initializeModule(jasperVault, _delegatedManager);

        emit SignalSuscriptionExtensionInitialized(
            address(jasperVault),
            address(_delegatedManager)
        );
    }

    /**
     * ONLY MANAGER: Remove an existing JasperVault and DelegatedManager tracked by the TradeExtension
     */
    function removeExtension() external override {
        IDelegatedManager delegatedManager = IDelegatedManager(msg.sender);
        IJasperVault jasperVault = delegatedManager.jasperVault();

        _removeExtension(jasperVault, delegatedManager);
    }

    function editSubscribeFee(
        IJasperVault _jasperVault,
        address _masterToken,
        uint256 _followFee,
        uint256 _profitShareFee
    ) external onlySettle(_jasperVault) onlyOperator(_jasperVault) {
        address[] memory followers = signalSuscriptionModule.get_followers(
            address(_jasperVault)
        );
        for (uint256 i = 0; i < followers.length; i++) {
            bytes memory callData = abi.encodeWithSelector(
                ISignalSuscriptionModule.unsubscribe.selector,
                followers[i],
                address(_jasperVault)
            );
            _invokeManager(
                _manager(IJasperVault(followers[i])),
                address(signalSuscriptionModule),
                callData
            );
            _manager(IJasperVault(followers[i])).setSubscribeStatus(2);
            emit SetSubscribeStatus(IJasperVault(followers[i]), 2);
        }
        _manager(_jasperVault).setBaseFeeAndToken(
            _masterToken,
            _followFee,
            _profitShareFee
        );
        emit SetFee(_jasperVault, _followFee, _profitShareFee);
    }

    function subscribe(
        IJasperVault _jasperVault,
        address target
    )
        external
        onlySettle(_jasperVault)
        ValidWhitelist(IJasperVault(target))
        onlyOperator(_jasperVault)
    {
        bytes memory callData = abi.encodeWithSelector(
            ISignalSuscriptionModule.subscribe.selector,
            _jasperVault,
            target
        );
        _invokeManager(
            _manager(_jasperVault),
            address(signalSuscriptionModule),
            callData
        );
        _manager(_jasperVault).setSubscribeStatus(1);
        emit SetSubscribeStatus(_jasperVault, 1);
    }

    function unsubscribe(
        IJasperVault _jasperVault,
        address target
    )
        external
        onlySubscribed(_jasperVault)
        ValidWhitelist(IJasperVault(target))
        onlyOperator(_jasperVault)
    {
        bytes memory callData = abi.encodeWithSelector(
            ISignalSuscriptionModule.unsubscribe.selector,
            _jasperVault,
            target
        );
        _invokeManager(
            _manager(_jasperVault),
            address(signalSuscriptionModule),
            callData
        );
        _manager(_jasperVault).setSubscribeStatus(2);
        emit SetSubscribeStatus(_jasperVault, 2);
    }

    function exectueFollowEnd(address _jasperVault) external {
        bytes memory callData = abi.encodeWithSelector(
            ISignalSuscriptionModule.exectueFollowEnd.selector,
            _jasperVault
        );
        _invokeManager(
            _manager(IJasperVault(_jasperVault)),
            address(signalSuscriptionModule),
            callData
        );
    }

    /* ============ view Functions ============ */
    function getFollowers(
        address _jasperVault
    ) external view returns (address[] memory) {
        return signalSuscriptionModule.get_followers(_jasperVault);
    }

    function getExectueFollow(
        address _jasperVault
    ) external view returns (bool) {
        return signalSuscriptionModule.isExectueFollow(_jasperVault);
    }

    function warnLine() external view returns (uint256) {
        return signalSuscriptionModule.warningLine();
    }

    function unsubscribeLine() external view returns (uint256) {
        return signalSuscriptionModule.unsubscribeLine();
    }

    /* ============ Internal Functions ============ */

    /**
     * Internal function to initialize TradeModule on the JasperVault associated with the DelegatedManager.
     *
     * @param _jasperVault             Instance of the JasperVault corresponding to the DelegatedManager
     * @param _delegatedManager     Instance of the DelegatedManager to initialize the TradeModule for
     */
    function _initializeModule(
        IJasperVault _jasperVault,
        IDelegatedManager _delegatedManager
    ) internal {
        bytes memory callData = abi.encodeWithSignature(
            "initialize(address)",
            _jasperVault
        );
        _invokeManager(
            _delegatedManager,
            address(signalSuscriptionModule),
            callData
        );
    }
}

/*
    Copyright 2021 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/

pragma solidity 0.6.10;
pragma experimental "ABIEncoderV2";

import {IJasperVault} from "../../interfaces/IJasperVault.sol";

interface IDelegatedManager {
    function interactManager(address _module, bytes calldata _encoded) external;

    function initializeExtension() external;

    function transferTokens(
        address _token,
        address _destination,
        uint256 _amount
    ) external;

    function factoryReset(
        uint256 _newFeeSplit,
        uint256 _managerFees,
        uint256 _delay
    ) external;

    function updateOwnerFeeSplit(uint256 _newFeeSplit) external;

    function updateOwnerFeeRecipient(address _newFeeRecipient) external;

    function setMethodologist(address _newMethodologist) external;

    function transferOwnership(address _owner) external;

    function isAllowedAdapter(address _adapter) external view returns (bool);

    function jasperVault() external view returns (IJasperVault);

    function owner() external view returns (address);

    function methodologist() external view returns (address);

    function operatorAllowlist(address _operator) external view returns (bool);

    function assetAllowlist(address _asset) external view returns (bool);

    function useAssetAllowlist() external view returns (bool);

    function isAllowedAsset(address _asset) external view returns (bool);

    function isPendingExtension(
        address _extension
    ) external view returns (bool);

    function isInitializedExtension(
        address _extension
    ) external view returns (bool);

    function getExtensions() external view returns (address[] memory);

    function getOperators() external view returns (address[] memory);

    function getAllowedAssets() external view returns (address[] memory);

    function ownerFeeRecipient() external view returns (address);

    function ownerFeeSplit() external view returns (uint256);

    function subscribeStatus() external view returns (uint256);

    function setSubscribeStatus(uint256) external;

    function getAdapters() external view returns (address[] memory);

    function setBaseFeeAndToken(address _masterToken,uint256 _followFee,uint256 _profitShareFee) external;
    function setBaseProperty(string memory _name,string memory _symbol) external;
    
}

/*
    Copyright 2022 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/
pragma solidity 0.6.10;

interface IManagerCore {
    function addManager(address _manager) external;
    function isExtension(address _extension) external view returns(bool);
    function isFactory(address _factory) external view returns(bool);
    function isManager(address _manager) external view returns(bool);
    function owner() external view returns(address);
}

/*
    Copyright 2022 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/

pragma solidity 0.6.10;

import {AddressArrayUtils} from "@setprotocol/set-protocol-v2/contracts/lib/AddressArrayUtils.sol";
import {IJasperVault} from "../../interfaces/IJasperVault.sol";

import {IDelegatedManager} from "../interfaces/IDelegatedManager.sol";
import {IManagerCore} from "../interfaces/IManagerCore.sol";

import {IController} from "../../interfaces/IController.sol";
import {ResourceIdentifier} from "../../protocol/lib/ResourceIdentifier.sol";

/**
 * @title BaseGlobalExtension
 * @author Set Protocol
 *
 * Abstract class that houses common global extension-related functions. Global extensions must
 * also have their own initializeExtension function (not included here because interfaces will vary).
 */
abstract contract BaseGlobalExtension {
    using AddressArrayUtils for address[];
    using ResourceIdentifier for IController;
    /* ============ Events ============ */

    event ExtensionRemoved(
        address indexed _jasperVault,
        address indexed _delegatedManager
    );

    /* ============ State Variables ============ */

    // Address of the ManagerCore
    IManagerCore public immutable managerCore;

    // Mapping from Set Token to DelegatedManager
    mapping(IJasperVault => IDelegatedManager) public setManagers;

    /* ============ Modifiers ============ */

    /**
     * Throws if the sender is not the JasperVault manager contract owner
     */
    modifier onlyOwner(IJasperVault _jasperVault) {
        require(msg.sender == _manager(_jasperVault).owner(), "Must be owner");
        _;
    }

    /**
     * Throws if the sender is not the JasperVault methodologist
     */
    modifier onlyMethodologist(IJasperVault _jasperVault) {
        require(
            msg.sender == _manager(_jasperVault).methodologist(),
            "Must be methodologist"
        );
        _;
    }

    modifier onlyUnSubscribed(IJasperVault _jasperVault) {
        require(
            _manager(_jasperVault).subscribeStatus()==2,
            "jasperVault not unsubscribed"
        );
        _;
    }


    modifier onlySubscribed(IJasperVault _jasperVault) {
        require(
            _manager(_jasperVault).subscribeStatus()==1,
            "jasperVault not subscribed"
        );
        _;
    }


    modifier onlySettle(IJasperVault _jasperVault) {
        require(
            _manager(_jasperVault).subscribeStatus()==0,
            "jasperVault not unsettle"
        );
        _;
    }


    modifier onlyNotSubscribed(IJasperVault _jasperVault) {
        require(
            _manager(_jasperVault).subscribeStatus()!=1,
            "jasperVault not unsettle"
        );
        _;
    }

    /**
     * Throws if the sender is not a JasperVault operator
     */
    modifier onlyOperator(IJasperVault _jasperVault) {
        require(
            _manager(_jasperVault).operatorAllowlist(msg.sender),
            "Must be approved operator"
        );
        _;
    }

    modifier ValidAdapter(
        IJasperVault _jasperVault,
        address _module,
        string memory _integrationName
    ) {
        bool isValid = ValidAdapterByModule(
            _jasperVault,
            _module,
            _integrationName
        );
        require(isValid, "Must be allowed adapter");
        _;
    }

    /**
     * Throws if the sender is not the JasperVault manager contract owner or if the manager is not enabled on the ManagerCore
     */
    modifier onlyOwnerAndValidManager(IDelegatedManager _delegatedManager) {
        require(msg.sender == _delegatedManager.owner(), "Must be owner");
        require(
            managerCore.isManager(address(_delegatedManager)),
            "Must be ManagerCore-enabled manager"
        );
        _;
    }

    /**
     * Throws if asset is not allowed to be held by the Set
     */
    modifier onlyAllowedAsset(IJasperVault _jasperVault, address _asset) {
        require(
            _manager(_jasperVault).isAllowedAsset(_asset),
            "Must be allowed asset"
        );
        _;
    }

    /* ============ Constructor ============ */

    /**
     * Set state variables
     *
     * @param _managerCore             Address of managerCore contract
     */
    constructor(IManagerCore _managerCore) public {
        managerCore = _managerCore;
    }

    /* ============ External Functions ============ */

    function ValidAdapterByModule(
        IJasperVault _jasperVault,
        address _module,
        string memory _integrationName
    ) public view returns (bool) {
        address controller = _jasperVault.controller();
        bytes32 _integrationHash = keccak256(bytes(_integrationName));
        address adapter = IController(controller)
            .getIntegrationRegistry()
            .getIntegrationAdapterWithHash(_module, _integrationHash);
        return _manager(_jasperVault).isAllowedAdapter(adapter);
    }

    /**
     * ONLY MANAGER: Deletes JasperVault/Manager state from extension. Must only be callable by manager!
     */
    function removeExtension() external virtual;

    /* ============ Internal Functions ============ */

    /**
     * Invoke call from manager
     *
     * @param _delegatedManager      Manager to interact with
     * @param _module                Module to interact with
     * @param _encoded               Encoded byte data
     */
    function _invokeManager(
        IDelegatedManager _delegatedManager,
        address _module,
        bytes memory _encoded
    ) internal {
        _delegatedManager.interactManager(_module, _encoded);
    }

    /**
     * Internal function to grab manager of passed JasperVault from extensions data structure.
     *
     * @param _jasperVault         JasperVault who's manager is needed
     */
    function _manager(
        IJasperVault _jasperVault
    ) internal view returns (IDelegatedManager) {
        return setManagers[_jasperVault];
    }

    /**
     * Internal function to initialize extension to the DelegatedManager.
     *
     * @param _jasperVault             Instance of the JasperVault corresponding to the DelegatedManager
     * @param _delegatedManager     Instance of the DelegatedManager to initialize
     */
    function _initializeExtension(
        IJasperVault _jasperVault,
        IDelegatedManager _delegatedManager
    ) internal {
        setManagers[_jasperVault] = _delegatedManager;
        _delegatedManager.initializeExtension();
    }

    /**
     * ONLY MANAGER: Internal function to delete JasperVault/Manager state from extension
     */
    function _removeExtension(
        IJasperVault _jasperVault,
        IDelegatedManager _delegatedManager
    ) internal {
        require(
            msg.sender == address(_manager(_jasperVault)),
            "Must be Manager"
        );

        delete setManagers[_jasperVault];

        emit ExtensionRemoved(
            address(_jasperVault),
            address(_delegatedManager)
        );
    }
}

/*
    Copyright 2020 Set Labs Inc.

    Licensed under the Apache License, Version 2.0 (the "License");
    you may not use this file except in compliance with the License.
    You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

    Unless required by applicable law or agreed to in writing, software
    distributed under the License is distributed on an "AS IS" BASIS,
    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
    See the License for the specific language governing permissions and
    limitations under the License.

    SPDX-License-Identifier: Apache License, Version 2.0
*/

pragma solidity 0.6.10;

import { IController } from "../../interfaces/IController.sol";
import { IIntegrationRegistry } from "../../interfaces/IIntegrationRegistry.sol";
import { IPriceOracle } from "../../interfaces/IPriceOracle.sol";
import { ISetValuer } from "../../interfaces/ISetValuer.sol";

/**
 * @title ResourceIdentifier
 * @author Set Protocol
 *
 * A collection of utility functions to fetch information related to Resource contracts in the system
 */
library ResourceIdentifier {

    // IntegrationRegistry will always be resource ID 0 in the system
    uint256 constant internal INTEGRATION_REGISTRY_RESOURCE_ID = 0;
    // PriceOracle will always be resource ID 1 in the system
    uint256 constant internal PRICE_ORACLE_RESOURCE_ID = 1;
    // SetValuer resource will always be resource ID 2 in the system
    uint256 constant internal SET_VALUER_RESOURCE_ID = 2;
    /* ============ Internal ============ */

    /**
     * Gets the instance of integration registry stored on Controller. Note: IntegrationRegistry is stored as index 0 on
     * the Controller
     */
    function getIntegrationRegistry(IController _controller) internal view returns (IIntegrationRegistry) {
        return IIntegrationRegistry(_controller.resourceId(INTEGRATION_REGISTRY_RESOURCE_ID));
    }

    /**
     * Gets instance of price oracle on Controller. Note: PriceOracle is stored as index 1 on the Controller
     */
    function getPriceOracle(IController _controller) internal view returns (IPriceOracle) {
        return IPriceOracle(_controller.resourceId(PRICE_ORACLE_RESOURCE_ID));
    }

    /**
     * Gets the instance of Set valuer on Controller. Note: SetValuer is stored as index 2 on the Controller
     */
    function getSetValuer(IController _controller) internal view returns (ISetValuer) {
        return ISetValuer(_controller.resourceId(SET_VALUER_RESOURCE_ID));
    }
}