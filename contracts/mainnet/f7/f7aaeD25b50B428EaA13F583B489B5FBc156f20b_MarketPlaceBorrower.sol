// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (access/AccessControl.sol)

pragma solidity ^0.8.0;

import "./IAccessControl.sol";
import "../utils/Context.sol";
import "../utils/Strings.sol";
import "../utils/introspection/ERC165.sol";

/**
 * @dev Contract module that allows children to implement role-based access
 * control mechanisms. This is a lightweight version that doesn't allow enumerating role
 * members except through off-chain means by accessing the contract event logs. Some
 * applications may benefit from on-chain enumerability, for those cases see
 * {AccessControlEnumerable}.
 *
 * Roles are referred to by their `bytes32` identifier. These should be exposed
 * in the external API and be unique. The best way to achieve this is by
 * using `public constant` hash digests:
 *
 * ```
 * bytes32 public constant MY_ROLE = keccak256("MY_ROLE");
 * ```
 *
 * Roles can be used to represent a set of permissions. To restrict access to a
 * function call, use {hasRole}:
 *
 * ```
 * function foo() public {
 *     require(hasRole(MY_ROLE, msg.sender));
 *     ...
 * }
 * ```
 *
 * Roles can be granted and revoked dynamically via the {grantRole} and
 * {revokeRole} functions. Each role has an associated admin role, and only
 * accounts that have a role's admin role can call {grantRole} and {revokeRole}.
 *
 * By default, the admin role for all roles is `DEFAULT_ADMIN_ROLE`, which means
 * that only accounts with this role will be able to grant or revoke other
 * roles. More complex role relationships can be created by using
 * {_setRoleAdmin}.
 *
 * WARNING: The `DEFAULT_ADMIN_ROLE` is also its own admin: it has permission to
 * grant and revoke this role. Extra precautions should be taken to secure
 * accounts that have been granted it.
 */
abstract contract AccessControl is Context, IAccessControl, ERC165 {
    struct RoleData {
        mapping(address => bool) members;
        bytes32 adminRole;
    }

    mapping(bytes32 => RoleData) private _roles;

    bytes32 public constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @dev Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     * _Available since v4.1._
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role);
        _;
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IAccessControl).interfaceId || super.supportsInterface(interfaceId);
    }

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) public view virtual override returns (bool) {
        return _roles[role].members[account];
    }

    /**
     * @dev Revert with a standard message if `_msgSender()` is missing `role`.
     * Overriding this function changes the behavior of the {onlyRole} modifier.
     *
     * Format of the revert message is described in {_checkRole}.
     *
     * _Available since v4.6._
     */
    function _checkRole(bytes32 role) internal view virtual {
        _checkRole(role, _msgSender());
    }

    /**
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view virtual {
        if (!hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        Strings.toHexString(account),
                        " is missing role ",
                        Strings.toHexString(uint256(role), 32)
                    )
                )
            );
        }
    }

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) public view virtual override returns (bytes32) {
        return _roles[role].adminRole;
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * May emit a {RoleGranted} event.
     */
    function grantRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _grantRole(role, account);
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * May emit a {RoleRevoked} event.
     */
    function revokeRole(bytes32 role, address account) public virtual override onlyRole(getRoleAdmin(role)) {
        _revokeRole(role, account);
    }

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been revoked `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     *
     * May emit a {RoleRevoked} event.
     */
    function renounceRole(bytes32 role, address account) public virtual override {
        require(account == _msgSender(), "AccessControl: can only renounce roles for self");

        _revokeRole(role, account);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event. Note that unlike {grantRole}, this function doesn't perform any
     * checks on the calling account.
     *
     * May emit a {RoleGranted} event.
     *
     * [WARNING]
     * ====
     * This function should only be called from the constructor when setting
     * up the initial roles for the system.
     *
     * Using this function in any other way is effectively circumventing the admin
     * system imposed by {AccessControl}.
     * ====
     *
     * NOTE: This function is deprecated in favor of {_grantRole}.
     */
    function _setupRole(bytes32 role, address account) internal virtual {
        _grantRole(role, account);
    }

    /**
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = getRoleAdmin(role);
        _roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @dev Grants `role` to `account`.
     *
     * Internal function without access restriction.
     *
     * May emit a {RoleGranted} event.
     */
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!hasRole(role, account)) {
            _roles[role].members[account] = true;
            emit RoleGranted(role, account, _msgSender());
        }
    }

    /**
     * @dev Revokes `role` from `account`.
     *
     * Internal function without access restriction.
     *
     * May emit a {RoleRevoked} event.
     */
    function _revokeRole(bytes32 role, address account) internal virtual {
        if (hasRole(role, account)) {
            _roles[role].members[account] = false;
            emit RoleRevoked(role, account, _msgSender());
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (access/IAccessControl.sol)

pragma solidity ^0.8.0;

/**
 * @dev External interface of AccessControl declared to support ERC165 detection.
 */
interface IAccessControl {
    /**
     * @dev Emitted when `newAdminRole` is set as ``role``'s admin role, replacing `previousAdminRole`
     *
     * `DEFAULT_ADMIN_ROLE` is the starting admin for all roles, despite
     * {RoleAdminChanged} not being emitted signaling this.
     *
     * _Available since v3.1._
     */
    event RoleAdminChanged(bytes32 indexed role, bytes32 indexed previousAdminRole, bytes32 indexed newAdminRole);

    /**
     * @dev Emitted when `account` is granted `role`.
     *
     * `sender` is the account that originated the contract call, an admin role
     * bearer except when using {AccessControl-_setupRole}.
     */
    event RoleGranted(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Emitted when `account` is revoked `role`.
     *
     * `sender` is the account that originated the contract call:
     *   - if using `revokeRole`, it is the admin role bearer
     *   - if using `renounceRole`, it is the role bearer (i.e. `account`)
     */
    event RoleRevoked(bytes32 indexed role, address indexed account, address indexed sender);

    /**
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function hasRole(bytes32 role, address account) external view returns (bool);

    /**
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {AccessControl-_setRoleAdmin}.
     */
    function getRoleAdmin(bytes32 role) external view returns (bytes32);

    /**
     * @dev Grants `role` to `account`.
     *
     * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function grantRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     */
    function revokeRole(bytes32 role, address account) external;

    /**
     * @dev Revokes `role` from the calling account.
     *
     * Roles are often managed via {grantRole} and {revokeRole}: this function's
     * purpose is to provide a mechanism for accounts to lose their privileges
     * if they are compromised (such as when a trusted device is misplaced).
     *
     * If the calling account had been granted `role`, emits a {RoleRevoked}
     * event.
     *
     * Requirements:
     *
     * - the caller must be `account`.
     */
    function renounceRole(bytes32 role, address account) external;
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/draft-IERC20Permit.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 Permit extension allowing approvals to be made via signatures, as defined in
 * https://eips.ethereum.org/EIPS/eip-2612[EIP-2612].
 *
 * Adds the {permit} method, which can be used to change an account's ERC20 allowance (see {IERC20-allowance}) by
 * presenting a message signed by the account. By not relying on {IERC20-approve}, the token holder account doesn't
 * need to send a transaction, and thus is not required to hold Ether at all.
 */
interface IERC20Permit {
    /**
     * @dev Sets `value` as the allowance of `spender` over ``owner``'s tokens,
     * given ``owner``'s signed approval.
     *
     * IMPORTANT: The same issues {IERC20-approve} has related to transaction
     * ordering also apply here.
     *
     * Emits an {Approval} event.
     *
     * Requirements:
     *
     * - `spender` cannot be the zero address.
     * - `deadline` must be a timestamp in the future.
     * - `v`, `r` and `s` must be a valid `secp256k1` signature from `owner`
     * over the EIP712-formatted function arguments.
     * - the signature must use ``owner``'s current nonce (see {nonces}).
     *
     * For more information on the signature format, see the
     * https://eips.ethereum.org/EIPS/eip-2612#specification[relevant EIP
     * section].
     */
    function permit(
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) external;

    /**
     * @dev Returns the current nonce for `owner`. This value must be
     * included whenever a signature is generated for {permit}.
     *
     * Every successful call to {permit} increases ``owner``'s nonce by one. This
     * prevents a signature from being used multiple times.
     */
    function nonces(address owner) external view returns (uint256);

    /**
     * @dev Returns the domain separator used in the encoding of the signature for {permit}, as defined by {EIP712}.
     */
    // solhint-disable-next-line func-name-mixedcase
    function DOMAIN_SEPARATOR() external view returns (bytes32);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (token/ERC20/extensions/IERC20Metadata.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";

/**
 * @dev Interface for the optional metadata functions from the ERC20 standard.
 *
 * _Available since v4.1._
 */
interface IERC20Metadata is IERC20 {
    /**
     * @dev Returns the name of the token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the token.
     */
    function decimals() external view returns (uint8);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.6.0) (token/ERC20/IERC20.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC20 standard as defined in the EIP.
 */
interface IERC20 {
    /**
     * @dev Emitted when `value` tokens are moved from one account (`from`) to
     * another (`to`).
     *
     * Note that `value` may be zero.
     */
    event Transfer(address indexed from, address indexed to, uint256 value);

    /**
     * @dev Emitted when the allowance of a `spender` for an `owner` is set by
     * a call to {approve}. `value` is the new allowance.
     */
    event Approval(address indexed owner, address indexed spender, uint256 value);

    /**
     * @dev Returns the amount of tokens in existence.
     */
    function totalSupply() external view returns (uint256);

    /**
     * @dev Returns the amount of tokens owned by `account`.
     */
    function balanceOf(address account) external view returns (uint256);

    /**
     * @dev Moves `amount` tokens from the caller's account to `to`.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transfer(address to, uint256 amount) external returns (bool);

    /**
     * @dev Returns the remaining number of tokens that `spender` will be
     * allowed to spend on behalf of `owner` through {transferFrom}. This is
     * zero by default.
     *
     * This value changes when {approve} or {transferFrom} are called.
     */
    function allowance(address owner, address spender) external view returns (uint256);

    /**
     * @dev Sets `amount` as the allowance of `spender` over the caller's tokens.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * IMPORTANT: Beware that changing an allowance with this method brings the risk
     * that someone may use both the old and the new allowance by unfortunate
     * transaction ordering. One possible solution to mitigate this race
     * condition is to first reduce the spender's allowance to 0 and set the
     * desired value afterwards:
     * https://github.com/ethereum/EIPs/issues/20#issuecomment-263524729
     *
     * Emits an {Approval} event.
     */
    function approve(address spender, uint256 amount) external returns (bool);

    /**
     * @dev Moves `amount` tokens from `from` to `to` using the
     * allowance mechanism. `amount` is then deducted from the caller's
     * allowance.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(
        address from,
        address to,
        uint256 amount
    ) external returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (token/ERC20/utils/SafeERC20.sol)

pragma solidity ^0.8.0;

import "../IERC20.sol";
import "../extensions/draft-IERC20Permit.sol";
import "../../../utils/Address.sol";

/**
 * @title SafeERC20
 * @dev Wrappers around ERC20 operations that throw on failure (when the token
 * contract returns false). Tokens that return no value (and instead revert or
 * throw on failure) are also supported, non-reverting calls are assumed to be
 * successful.
 * To use this library you can add a `using SafeERC20 for IERC20;` statement to your contract,
 * which allows you to call the safe operations as `token.safeTransfer(...)`, etc.
 */
library SafeERC20 {
    using Address for address;

    function safeTransfer(
        IERC20 token,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transfer.selector, to, value));
    }

    function safeTransferFrom(
        IERC20 token,
        address from,
        address to,
        uint256 value
    ) internal {
        _callOptionalReturn(token, abi.encodeWithSelector(token.transferFrom.selector, from, to, value));
    }

    /**
     * @dev Deprecated. This function has issues similar to the ones found in
     * {IERC20-approve}, and its usage is discouraged.
     *
     * Whenever possible, use {safeIncreaseAllowance} and
     * {safeDecreaseAllowance} instead.
     */
    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }

    function safeIncreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        uint256 newAllowance = token.allowance(address(this), spender) + value;
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
    }

    function safeDecreaseAllowance(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        unchecked {
            uint256 oldAllowance = token.allowance(address(this), spender);
            require(oldAllowance >= value, "SafeERC20: decreased allowance below zero");
            uint256 newAllowance = oldAllowance - value;
            _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, newAllowance));
        }
    }

    function safePermit(
        IERC20Permit token,
        address owner,
        address spender,
        uint256 value,
        uint256 deadline,
        uint8 v,
        bytes32 r,
        bytes32 s
    ) internal {
        uint256 nonceBefore = token.nonces(owner);
        token.permit(owner, spender, value, deadline, v, r, s);
        uint256 nonceAfter = token.nonces(owner);
        require(nonceAfter == nonceBefore + 1, "SafeERC20: permit did not succeed");
    }

    /**
     * @dev Imitates a Solidity high-level call (i.e. a regular function call to a contract), relaxing the requirement
     * on the return value: the return value is optional (but if data is returned, it must not be false).
     * @param token The token targeted by the call.
     * @param data The call data (encoded using abi.encode or one of its variants).
     */
    function _callOptionalReturn(IERC20 token, bytes memory data) private {
        // We need to perform a low level call here, to bypass Solidity's return data size checking mechanism, since
        // we're implementing it ourselves. We use {Address-functionCall} to perform this call, which verifies that
        // the target address contains contract code and also asserts for success in the low-level call.

        bytes memory returndata = address(token).functionCall(data, "SafeERC20: low-level call failed");
        if (returndata.length > 0) {
            // Return data is optional
            require(abi.decode(returndata, (bool)), "SafeERC20: ERC20 operation did not succeed");
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://diligence.consensys.net/posts/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.5.11/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/math/Math.sol)

pragma solidity ^0.8.0;

/**
 * @dev Standard math utilities missing in the Solidity language.
 */
library Math {
    enum Rounding {
        Down, // Toward negative infinity
        Up, // Toward infinity
        Zero // Toward zero
    }

    /**
     * @dev Returns the largest of two numbers.
     */
    function max(uint256 a, uint256 b) internal pure returns (uint256) {
        return a > b ? a : b;
    }

    /**
     * @dev Returns the smallest of two numbers.
     */
    function min(uint256 a, uint256 b) internal pure returns (uint256) {
        return a < b ? a : b;
    }

    /**
     * @dev Returns the average of two numbers. The result is rounded towards
     * zero.
     */
    function average(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b) / 2 can overflow.
        return (a & b) + (a ^ b) / 2;
    }

    /**
     * @dev Returns the ceiling of the division of two numbers.
     *
     * This differs from standard division with `/` in that it rounds up instead
     * of rounding down.
     */
    function ceilDiv(uint256 a, uint256 b) internal pure returns (uint256) {
        // (a + b - 1) / b can overflow on addition, so we distribute.
        return a == 0 ? 0 : (a - 1) / b + 1;
    }

    /**
     * @notice Calculates floor(x * y / denominator) with full precision. Throws if result overflows a uint256 or denominator == 0
     * @dev Original credit to Remco Bloemen under MIT license (https://xn--2-umb.com/21/muldiv)
     * with further edits by Uniswap Labs also under MIT license.
     */
    function mulDiv(
        uint256 x,
        uint256 y,
        uint256 denominator
    ) internal pure returns (uint256 result) {
        unchecked {
            // 512-bit multiply [prod1 prod0] = x * y. Compute the product mod 2^256 and mod 2^256 - 1, then use
            // use the Chinese Remainder Theorem to reconstruct the 512 bit result. The result is stored in two 256
            // variables such that product = prod1 * 2^256 + prod0.
            uint256 prod0; // Least significant 256 bits of the product
            uint256 prod1; // Most significant 256 bits of the product
            assembly {
                let mm := mulmod(x, y, not(0))
                prod0 := mul(x, y)
                prod1 := sub(sub(mm, prod0), lt(mm, prod0))
            }

            // Handle non-overflow cases, 256 by 256 division.
            if (prod1 == 0) {
                return prod0 / denominator;
            }

            // Make sure the result is less than 2^256. Also prevents denominator == 0.
            require(denominator > prod1);

            ///////////////////////////////////////////////
            // 512 by 256 division.
            ///////////////////////////////////////////////

            // Make division exact by subtracting the remainder from [prod1 prod0].
            uint256 remainder;
            assembly {
                // Compute remainder using mulmod.
                remainder := mulmod(x, y, denominator)

                // Subtract 256 bit number from 512 bit number.
                prod1 := sub(prod1, gt(remainder, prod0))
                prod0 := sub(prod0, remainder)
            }

            // Factor powers of two out of denominator and compute largest power of two divisor of denominator. Always >= 1.
            // See https://cs.stackexchange.com/q/138556/92363.

            // Does not overflow because the denominator cannot be zero at this stage in the function.
            uint256 twos = denominator & (~denominator + 1);
            assembly {
                // Divide denominator by twos.
                denominator := div(denominator, twos)

                // Divide [prod1 prod0] by twos.
                prod0 := div(prod0, twos)

                // Flip twos such that it is 2^256 / twos. If twos is zero, then it becomes one.
                twos := add(div(sub(0, twos), twos), 1)
            }

            // Shift in bits from prod1 into prod0.
            prod0 |= prod1 * twos;

            // Invert denominator mod 2^256. Now that denominator is an odd number, it has an inverse modulo 2^256 such
            // that denominator * inv = 1 mod 2^256. Compute the inverse by starting with a seed that is correct for
            // four bits. That is, denominator * inv = 1 mod 2^4.
            uint256 inverse = (3 * denominator) ^ 2;

            // Use the Newton-Raphson iteration to improve the precision. Thanks to Hensel's lifting lemma, this also works
            // in modular arithmetic, doubling the correct bits in each step.
            inverse *= 2 - denominator * inverse; // inverse mod 2^8
            inverse *= 2 - denominator * inverse; // inverse mod 2^16
            inverse *= 2 - denominator * inverse; // inverse mod 2^32
            inverse *= 2 - denominator * inverse; // inverse mod 2^64
            inverse *= 2 - denominator * inverse; // inverse mod 2^128
            inverse *= 2 - denominator * inverse; // inverse mod 2^256

            // Because the division is now exact we can divide by multiplying with the modular inverse of denominator.
            // This will give us the correct result modulo 2^256. Since the preconditions guarantee that the outcome is
            // less than 2^256, this is the final result. We don't need to compute the high bits of the result and prod1
            // is no longer required.
            result = prod0 * inverse;
            return result;
        }
    }

    /**
     * @notice Calculates x * y / denominator with full precision, following the selected rounding direction.
     */
    function mulDiv(
        uint256 x,
        uint256 y,
        uint256 denominator,
        Rounding rounding
    ) internal pure returns (uint256) {
        uint256 result = mulDiv(x, y, denominator);
        if (rounding == Rounding.Up && mulmod(x, y, denominator) > 0) {
            result += 1;
        }
        return result;
    }

    /**
     * @dev Returns the square root of a number. If the number is not a perfect square, the value is rounded down.
     *
     * Inspired by Henry S. Warren, Jr.'s "Hacker's Delight" (Chapter 11).
     */
    function sqrt(uint256 a) internal pure returns (uint256) {
        if (a == 0) {
            return 0;
        }

        // For our first guess, we get the biggest power of 2 which is smaller than the square root of the target.
        //
        // We know that the "msb" (most significant bit) of our target number `a` is a power of 2 such that we have
        // `msb(a) <= a < 2*msb(a)`. This value can be written `msb(a)=2**k` with `k=log2(a)`.
        //
        // This can be rewritten `2**log2(a) <= a < 2**(log2(a) + 1)`
        // → `sqrt(2**k) <= sqrt(a) < sqrt(2**(k+1))`
        // → `2**(k/2) <= sqrt(a) < 2**((k+1)/2) <= 2**(k/2 + 1)`
        //
        // Consequently, `2**(log2(a) / 2)` is a good first approximation of `sqrt(a)` with at least 1 correct bit.
        uint256 result = 1 << (log2(a) >> 1);

        // At this point `result` is an estimation with one bit of precision. We know the true value is a uint128,
        // since it is the square root of a uint256. Newton's method converges quadratically (precision doubles at
        // every iteration). We thus need at most 7 iteration to turn our partial result with one bit of precision
        // into the expected uint128 result.
        unchecked {
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            result = (result + a / result) >> 1;
            return min(result, a / result);
        }
    }

    /**
     * @notice Calculates sqrt(a), following the selected rounding direction.
     */
    function sqrt(uint256 a, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = sqrt(a);
            return result + (rounding == Rounding.Up && result * result < a ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 2, rounded down, of a positive value.
     * Returns 0 if given 0.
     */
    function log2(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >> 128 > 0) {
                value >>= 128;
                result += 128;
            }
            if (value >> 64 > 0) {
                value >>= 64;
                result += 64;
            }
            if (value >> 32 > 0) {
                value >>= 32;
                result += 32;
            }
            if (value >> 16 > 0) {
                value >>= 16;
                result += 16;
            }
            if (value >> 8 > 0) {
                value >>= 8;
                result += 8;
            }
            if (value >> 4 > 0) {
                value >>= 4;
                result += 4;
            }
            if (value >> 2 > 0) {
                value >>= 2;
                result += 2;
            }
            if (value >> 1 > 0) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 2, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log2(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log2(value);
            return result + (rounding == Rounding.Up && 1 << result < value ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 10, rounded down, of a positive value.
     * Returns 0 if given 0.
     */
    function log10(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >= 10**64) {
                value /= 10**64;
                result += 64;
            }
            if (value >= 10**32) {
                value /= 10**32;
                result += 32;
            }
            if (value >= 10**16) {
                value /= 10**16;
                result += 16;
            }
            if (value >= 10**8) {
                value /= 10**8;
                result += 8;
            }
            if (value >= 10**4) {
                value /= 10**4;
                result += 4;
            }
            if (value >= 10**2) {
                value /= 10**2;
                result += 2;
            }
            if (value >= 10**1) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log10(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log10(value);
            return result + (rounding == Rounding.Up && 10**result < value ? 1 : 0);
        }
    }

    /**
     * @dev Return the log in base 256, rounded down, of a positive value.
     * Returns 0 if given 0.
     *
     * Adding one to the result gives the number of pairs of hex symbols needed to represent `value` as a hex string.
     */
    function log256(uint256 value) internal pure returns (uint256) {
        uint256 result = 0;
        unchecked {
            if (value >> 128 > 0) {
                value >>= 128;
                result += 16;
            }
            if (value >> 64 > 0) {
                value >>= 64;
                result += 8;
            }
            if (value >> 32 > 0) {
                value >>= 32;
                result += 4;
            }
            if (value >> 16 > 0) {
                value >>= 16;
                result += 2;
            }
            if (value >> 8 > 0) {
                result += 1;
            }
        }
        return result;
    }

    /**
     * @dev Return the log in base 10, following the selected rounding direction, of a positive value.
     * Returns 0 if given 0.
     */
    function log256(uint256 value, Rounding rounding) internal pure returns (uint256) {
        unchecked {
            uint256 result = log256(value);
            return result + (rounding == Rounding.Up && 1 << (result * 8) < value ? 1 : 0);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.8.0) (utils/Strings.sol)

pragma solidity ^0.8.0;

import "./math/Math.sol";

/**
 * @dev String operations.
 */
library Strings {
    bytes16 private constant _SYMBOLS = "0123456789abcdef";
    uint8 private constant _ADDRESS_LENGTH = 20;

    /**
     * @dev Converts a `uint256` to its ASCII `string` decimal representation.
     */
    function toString(uint256 value) internal pure returns (string memory) {
        unchecked {
            uint256 length = Math.log10(value) + 1;
            string memory buffer = new string(length);
            uint256 ptr;
            /// @solidity memory-safe-assembly
            assembly {
                ptr := add(buffer, add(32, length))
            }
            while (true) {
                ptr--;
                /// @solidity memory-safe-assembly
                assembly {
                    mstore8(ptr, byte(mod(value, 10), _SYMBOLS))
                }
                value /= 10;
                if (value == 0) break;
            }
            return buffer;
        }
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation.
     */
    function toHexString(uint256 value) internal pure returns (string memory) {
        unchecked {
            return toHexString(value, Math.log256(value) + 1);
        }
    }

    /**
     * @dev Converts a `uint256` to its ASCII `string` hexadecimal representation with fixed length.
     */
    function toHexString(uint256 value, uint256 length) internal pure returns (string memory) {
        bytes memory buffer = new bytes(2 * length + 2);
        buffer[0] = "0";
        buffer[1] = "x";
        for (uint256 i = 2 * length + 1; i > 1; --i) {
            buffer[i] = _SYMBOLS[value & 0xf];
            value >>= 4;
        }
        require(value == 0, "Strings: hex length insufficient");
        return string(buffer);
    }

    /**
     * @dev Converts an `address` with fixed length of 20 bytes to its not checksummed ASCII `string` hexadecimal representation.
     */
    function toHexString(address addr) internal pure returns (string memory) {
        return toHexString(uint256(uint160(addr)), _ADDRESS_LENGTH);
    }
}

// SPDX-License-Identifier: GPL-3.0-or-later
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

pragma solidity 0.8.9;

// solhint-disable

/**
 * @dev Reverts if `condition` is false, with a revert reason containing `errorCode`. Only codes up to 999 are
 * supported.
 * Uses the default '0XEQ' prefix for the error code
 */
function _require(bool condition, uint256 errorCode) pure {
    if (!condition) _revert(errorCode);
}

/**
 * @dev Reverts with a revert reason containing `errorCode`. Only codes up to 999 are supported.
 * Uses the default '0XEQ' prefix for the error code
 */
function _revert(uint256 errorCode) pure {
    _revert(errorCode, 0x584551); // This is the raw byte representation of "0XEQ"
}

/**
 * @dev Reverts with a revert reason containing `errorCode`. Only codes up to 999 are supported.
 */
function _revert(uint256 errorCode, bytes3 prefix) pure {
    uint256 prefixUint = uint256(uint24(prefix));
    // We're going to dynamically create a revert string based on the error code, with the following format:
    // '0XEQ#{errorCode}'
    // where the code is left-padded with zeroes to three digits (so they range from 000 to 999).
    //
    // We don't have revert strings embedded in the contract to save bytecode size: it takes much less space to store a
    // number (8 to 16 bits) than the individual string characters.
    //
    // The dynamic string creation algorithm that follows could be implemented in Solidity, but assembly allows for a
    // much denser implementation, again saving bytecode size. Given this function unconditionally reverts, this is a
    // safe place to rely on it without worrying about how its usage might affect e.g. memory contents.
    assembly {
        // First, we need to compute the ASCII representation of the error code. We assume that it is in the 0-999
        // range, so we only need to convert three digits. To convert the digits to ASCII, we add 0x30, the value for
        // the '0' character.

        let units := add(mod(errorCode, 10), 0x30)

        errorCode := div(errorCode, 10)
        let tenths := add(mod(errorCode, 10), 0x30)

        errorCode := div(errorCode, 10)
        let hundreds := add(mod(errorCode, 10), 0x30)

        // With the individual characters, we can now construct the full string.
        // We first append the '#' character (0x23) to the prefix. In the case of '0XEQ', it results in 0x584551 ('0XEQ#')
        // Then, we shift this by 24 (to provide space for the 3 bytes of the error code), and add the
        // characters to it, each shifted by a multiple of 8.
        // The revert reason is then shifted left by 200 bits (256 minus the length of the string, 7 characters * 8 bits
        // per character = 56) to locate it in the most significant part of the 256 slot (the beginning of a byte
        // array).
        let formattedPrefix := shl(24, add(0x23, shl(8, prefixUint)))

        let revertReason := shl(
            200,
            add(
                formattedPrefix,
                add(add(units, shl(8, tenths)), shl(16, hundreds))
            )
        )

        // We can now encode the reason in memory, which can be safely overwritten as we're about to revert. The encoded
        // message will have the following layout:
        // [ revert reason identifier ] [ string location offset ] [ string length ] [ string contents ]

        // The Solidity revert reason identifier is 0x08c739a0, the function selector of the Error(string) function. We
        // also write zeroes to the next 28 bytes of memory, but those are about to be overwritten.
        mstore(
            0x0,
            0x08c379a000000000000000000000000000000000000000000000000000000000
        )
        // Next is the offset to the location of the string, which will be placed immediately after (20 bytes away).
        mstore(
            0x04,
            0x0000000000000000000000000000000000000000000000000000000000000020
        )
        // The string length is fixed: 7 characters.
        mstore(0x24, 7)
        // Finally, the string itself is stored.
        mstore(0x44, revertReason)

        // Even if the string is only 7 bytes long, we need to return a full 32 byte slot containing it. The length of
        // the encoded message is therefore 4 + 32 + 32 + 32 = 100.
        revert(0, 100)
    }
}

library Errors {
    // ACCESS CONTROL

    uint256 internal constant CALLER_NOT_ADMIN = 0;
    uint256 internal constant CALLER_NOT_MAINTAINER = 1;
    uint256 internal constant CALLER_NOT_MINTER = 2;
    uint256 internal constant CALLER_NOT_BURNER = 3;
    uint256 internal constant CALLER_NOT_MARKETPLACE = 4;
    uint256 internal constant CALLER_NOT_VAULT = 5;
    uint256 internal constant CALLER_NOT_MARKETPLACE_BORROWER = 6;

    // COMMON

    uint256 internal constant ARRAY_LENGTH_MISMATCH = 100;
    uint256 internal constant ZERO_ADDRESS = 101;
    uint256 internal constant ZERO_LENGTH_ARRAY = 102;
    uint256 internal constant ZERO_AMOUNT = 103;
    uint256 internal constant NON_ZERO_NUMBER_REQUIRED = 104;
    uint256 internal constant ERROR_IN_TRUST_FORWARDER_CALL = 105;
    uint256 internal constant SAFE_TRANSFER_FAILED = 106;
    uint256 internal constant SAFE_TRANSFER_FROM_FAILED = 107;
    uint256 internal constant SAFE_APPROVE_FAILED = 108;
    uint256 internal constant SAME_TOKENS = 109;
    uint256 internal constant NON_KYC = 110;
    uint256 internal constant INVALID_TOKEN = 111;
    uint256 internal constant RECEIVER_ON_BLACKLIST = 112;
    uint256 internal constant SENDER_ON_BLACKLIST = 113;

    // SBT

    uint256 internal constant CANT_MINT_TWICE = 200;
    uint256 internal constant WRONG_COMMUNITY_NAME = 201;
    uint256 internal constant TRANSFER_NOT_ALLOWED = 202;
    uint256 internal constant COMMUNITY_DOES_NOT_EXIST = 203;
    uint256 internal constant ALREADY_APPROVED_COMMUNITY = 204;
    uint256 internal constant INPUT_LENGTH_IS_GREATER_THAN_TOTAL = 205;

    // FINDER

    uint256 internal constant IMPLEMENTATION_NOT_FOUND = 300;
    uint256 internal constant EMPTY_BYTECODE = 301;

    // TOKENWHITELIST

    uint256 internal constant TOKEN_ALREADY_WHITELISTED = 400;
    uint256 internal constant TOKEN_NOT_WHITELISTED = 401;

    //MARKETPLACE

    uint256 internal constant ZERO_BALANCE = 500;
    uint256 internal constant PROPERTY_DOES_NOT_EXIST = 501;
    uint256 internal constant PROPERTY_ALREADY_EXIST = 502;
    uint256 internal constant EXCEED_TOTAL_LEGAL_SHARES = 503;
    uint256 internal constant WHOLE_NUMBER_REQUIRED = 504;
    uint256 internal constant BUY_PAUSED = 505;
    uint256 internal constant SELL_PAUSED = 506;
    uint256 internal constant INVALID_CURRENCY = 507;
    uint256 internal constant INVALID_BASE_CURRENCY = 508;
    uint256 internal constant INVALID_OUTPUT_CURRENCY = 509;
    uint256 internal constant INVALID_CASE = 510;
    uint256 internal constant INVALID_FEE_PERCENTAGE = 511;
    uint256 internal constant CALL_IDENTITY_FAILED = 512;
    uint256 internal constant LOCK_AMOUNT_LESS_THAN_TOTAL = 513;
    uint256 internal constant INSUFFICIENT_WLEGAL_LIQUIDITY = 514;
    uint256 internal constant INSUFFICIENT_WLEGAL_LIQUIDITY_MP = 515;

    // PRICEFEED

    uint256 internal constant INVALID_DECIMALS = 600;
    uint256 internal constant INVALID_PAIR_NAME = 601;

    // RENTSHARE

    uint256 internal constant CLAIMING_TWICE_A_WEEK = 700;
    uint256 internal constant SAME_SYMBOL = 701;

    // CUSTOMGAUGE

    uint256 internal constant RE_ENTRANCY = 800;
    uint256 internal constant CALLER_NOT_ACCOUNT = 801;
    uint256 internal constant TOO_MANY_REWARD_TOKENS = 802;
    uint256 internal constant REWARD_RATE_IS_ZERO = 803;
    uint256 internal constant PROVIDED_REWARD_TOO_HIGH = 804;

    // VAULTS
    uint256 internal constant WITHDRAWING_BEFORE_TIME = 900;
    uint256 internal constant ALREADY_INITIALIZED = 901;
    uint256 internal constant INVALID_CONTROLLER = 902;
    uint256 internal constant ZERO_SHARES = 903;
    uint256 internal constant ZERO_ASSETS = 904;
    uint256 internal constant ADDRESS_NOT_REGISTERED = 905;
}

// SPDX-License-Identifier: AGPL-3.0-only

pragma solidity 0.8.9;

interface ICustomVault {
    function borrow(uint amount) external;

    function repay(address recepient, uint amount) external;

    function stake(uint256 assets) external returns (uint256 shares);

    function stakeToken() external view returns (address);

    function deposit(
        uint256 assets,
        address receiver
    ) external returns (uint256 shares);
}

// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity 0.8.9;

interface IERC4626StakingPool {
    function borrow(
        uint256 _amount
    ) external returns (uint256);

    function buyPropertyTokens(
        address _propertyToken,
        uint256 _amountOfTokens,
        address _marketPlace,
        uint _repayAmount,
        address _propertyBasecurrency
    ) external;

    function afterRepay(uint256 amount, address marketplace) external;

    function decreaseAssetTotalSupply(uint256 _amount) external;

    function notiftyRewardToGauge(
        uint256 _rewardAmount
    ) external returns (uint256);

    function fees() external view returns (uint256);

    function swapStakeTokenWithCToken(
        address recipient,
        uint256 amountIn,
        address cToken
    ) external;

    function stake(uint256 assets) external returns (uint256 shares);

    function stake(
        uint256 assets,
        address sender
    ) external returns (uint256 shares);

    function stake(
        uint256 assets,
        address cTokensReceiver,
        address passOnPool,
        address sender,
        bool skipPassOnPoolTransfer,
        bool depositToPassOnPoolGauge
    ) external returns (uint256 shares);

    function withdraw(
        uint256 assets,
        address receiver,
        address owner_
    ) external returns (uint256 shares);

    function withdraw(
        uint256 assets,
        address receiver
    ) external returns (uint256 shares);

    function getAllowedCTokenAddresses()
        external
        view
        returns (address[] memory);

    function withdrawFromVaultRouter(
        uint256 _totalAmount,
        uint256 _reservePoolAmount,
        address _receiver,
        address _owner
    ) external;

    function decimals() external view returns (uint8);

    function normalStake(
        uint256 assets,
        address sender
    ) external returns (uint256);

    function stakeToken() external view returns (address);

    function xToken() external view returns (address);

    function gaugeAddress() external view returns (address);
}

// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity 0.8.9;

/**
 * @title interface for storage struct and functions for PoolBorrower.sol
 */
interface IMarketPlaceBorrower {
    struct Storage {
        address poolToBorrowFrom;
        address allowedMarketPlace;
        uint256 amountBorrowed;
        address customVault;
    }

    function borrowTokens(
        uint256 _tokensToBorrowWithoutFees,
        address _propertyToken,
        uint256 _noOfTokens
    ) external;

    function buyPropertyTokens(
        address _propertyToken,
        uint256 _repayAmount,
        uint256 _currentPertokenPrice,
        address _propertyBasecurrency
    ) external;

    function getPoolToBorrowFromAddress() external view returns (address);
}

// SPDX-License-Identifier: Unlicense
pragma solidity 0.8.9;

interface IMarketplaceMeta {
    function getFeeReceiverAddress() external view returns (address);
}

// SPDX-License-Identifier: MIT
pragma solidity 0.8.9;
import "./../Interfaces/IMarketPlaceBorrower.sol";
import "./../Interfaces/IERC4626StakingPool.sol";
import "./../Interfaces/ICustomVault.sol";
import {IERC20, SafeERC20} from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import {IERC20Metadata} from "@openzeppelin/contracts/token/ERC20/extensions/IERC20Metadata.sol";
import "./../core/libs/Errors.sol";

interface IMarketplaceBorrowerMeta {
    function lastProfitSentToGauge() external view returns (uint);
}

// This contract uses the library to set and retrieve state variables
library MarketPlaceBorrowerLib {
    using SafeERC20 for IERC20;

    function initialize(
        IMarketPlaceBorrower.Storage storage _storageParams,
        address _poolToBorrowFrom
    ) internal {
        zeroAddrCheck(_poolToBorrowFrom);
        _storageParams.poolToBorrowFrom = _poolToBorrowFrom;
    }

    function _setMarketPlace(
        IMarketPlaceBorrower.Storage storage _storageParams,
        address _marketPlace
    ) internal {
        zeroAddrCheck(_marketPlace);
        _storageParams.allowedMarketPlace = _marketPlace;
    }

    function _setCustomVault(
        IMarketPlaceBorrower.Storage storage _storageParams,
        address _vault
    ) internal {
        zeroAddrCheck(_vault);
        _storageParams.customVault = _vault;
    }

    function _borrowTokensFromPool(
        IMarketPlaceBorrower.Storage storage _storageParams,
        uint256 _amount
    ) internal returns (uint256) {
        _amountAndCallerVerification(_storageParams, _amount);

        address stakeToken = IERC4626StakingPool(_storageParams.customVault)
            .stakeToken();
        uint scaledBorrowAmount = _amount *
            (10 ** (18 - IERC20Metadata(stakeToken).decimals()));
        // borrowing xTokens from bb pool
        uint actualBorrowAmount = IERC4626StakingPool(
            _storageParams.poolToBorrowFrom
        ).borrow(scaledBorrowAmount);

        address xToken = IERC4626StakingPool(_storageParams.customVault)
            .xToken();
        IERC20(xToken).safeIncreaseAllowance(
            _storageParams.customVault,
            scaledBorrowAmount
        );
        // uint256 noOfToken = IERC4626StakingPool(_storageParams.customVault)
        //     .withdraw(actualBorrowAmount, address(this), address(this));
        // borrowing tokens from custom vault in exchange of xTokens
        ICustomVault(_storageParams.customVault).borrow(actualBorrowAmount);
        uint scalDownBorrowAmount = actualBorrowAmount /
            (10 ** (18 - IERC20Metadata(stakeToken).decimals()));

        // transferring token to caller(Marketplace in this case);
        IERC20(stakeToken).safeTransfer(msg.sender, scalDownBorrowAmount);
        return scalDownBorrowAmount;
    }

    function _buyPropertyTokensForMP(
        IMarketPlaceBorrower.Storage storage _storageParams,
        address _propertyToken,
        uint256 _amountOfTokens,
        uint _repayAmount,
        address _propertyBasecurrency
    ) internal {
        _amountAndCallerVerification(_storageParams, _amountOfTokens);

        IERC4626StakingPool(_storageParams.poolToBorrowFrom).buyPropertyTokens(
            _propertyToken,
            _amountOfTokens,
            _storageParams.allowedMarketPlace,
            _repayAmount,
            _propertyBasecurrency
        );
    }

    function _amountAndCallerVerification(
        IMarketPlaceBorrower.Storage storage _storageParams,
        uint _amountOfTokens
    ) internal view {
        if (_amountOfTokens == 0) {
            _revert(Errors.ZERO_AMOUNT);
        }

        if (msg.sender != _storageParams.allowedMarketPlace) {
            _revert(Errors.CALLER_NOT_MARKETPLACE);
        }
    }

    function _afterRepay(
        IMarketPlaceBorrower.Storage storage _storageParams,
        uint256 _remaining
    ) internal {
        IERC4626StakingPool(_storageParams.poolToBorrowFrom).afterRepay(
            _remaining,
            _storageParams.allowedMarketPlace
        );
    }

    function _repayToCustomVault(
        IMarketPlaceBorrower.Storage storage _storageParams,
        uint _amountToRepay,
        uint _lastProfitSentToGauge
    ) internal {
        address stakeToken = IERC4626StakingPool(_storageParams.customVault)
            .stakeToken();
        // first pulling tokens from the bb pool
        IERC20(stakeToken).transferFrom(
            _storageParams.poolToBorrowFrom, // bb pool
            address(this),
            _amountToRepay
        );
        // address xToken = IERC4626StakingPool(_storageParams.customVault)
        //     .xToken();
        IERC20(stakeToken).safeIncreaseAllowance(
            _storageParams.customVault,
            _amountToRepay
        );
        // now transferring tokens borrowed from custom vault and getting back x tokens
        ICustomVault(_storageParams.customVault).repay(
            _storageParams.poolToBorrowFrom,
            _amountToRepay - _lastProfitSentToGauge
        );

        if (_lastProfitSentToGauge > 0) {
            IERC20(stakeToken).safeIncreaseAllowance(
                _storageParams.customVault,
                _lastProfitSentToGauge
            );
            ICustomVault(_storageParams.customVault).deposit(
                _lastProfitSentToGauge,
                _storageParams.poolToBorrowFrom
            );
        }
    }

    function _updatePoolToBorrowFromAddress(
        IMarketPlaceBorrower.Storage storage _storageParams,
        address _addr
    ) internal {
        zeroAddrCheck(_addr);
        _storageParams.poolToBorrowFrom = _addr;
    }

    function zeroAddrCheck(address _addr) internal pure {
        if (_addr == address(0x00)) {
            _revert(Errors.ZERO_ADDRESS);
        }
    }
}

// SPDX-License-Identifier: AGPL-3.0-only
pragma solidity 0.8.9;

import {IMarketPlaceBorrower} from "./Interfaces/IMarketPlaceBorrower.sol";
import {IMarketplaceMeta} from "./Interfaces/IMarketplaceMeta.sol";
import {MarketPlaceBorrowerLib} from "./libraries/MarketPlaceBorrowerLib.sol";
import "@openzeppelin/contracts/access/AccessControl.sol";
import "./PositionManager.sol";
import "./Interfaces/ICustomVault.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

import "./core/libs/Errors.sol";

/**
 * @title MarketPlaceBorrower
 * @notice When user wants to sell property, this contract borrows from ERC4626 and give asked currency to marketplace.
 */
contract MarketPlaceBorrower is
    IMarketPlaceBorrower,
    PositionManager,
    AccessControl
{
    using MarketPlaceBorrowerLib for Storage;
    using SafeERC20 for IERC20;

    //----------------------------------------
    // Storage
    //----------------------------------------

    /// @notice Role to allow specific operations on contracts that can be performed by this role
    bytes32 public constant MAINTAINER_ROLE = keccak256("Maintainer");

    /// @notice details of all state variables used by this contract
    Storage internal _storageParams;

    //----------------------------------------
    // Modifiers
    //----------------------------------------

    /// @notice Modifier to ensure that caller has "MAINTAINER_ROLE" role
    modifier onlyMaintainer() {
        _require(
            hasRole(MAINTAINER_ROLE, msg.sender),
            Errors.CALLER_NOT_MAINTAINER
        );
        _;
    }

    // events

    event AllowedMarketplaceUpdated(address _marketplace);
    event CustomVaultUpdated(address _vault);
    event PoolToBorrowFromUpdated(address _newPool);

    /**
     * @notice creates Initializes the contract.
     * @param _poolToBorrowFrom address of ERC4626Vault from where to borrow the currency
     * @param _manager address of Manager.sol
     */
    constructor(address _poolToBorrowFrom, address _manager) {
        _storageParams.initialize(_poolToBorrowFrom);
        _grantRole(DEFAULT_ADMIN_ROLE, _manager);
        _grantRole(MAINTAINER_ROLE, msg.sender);
    }

    /**
     * @notice changes the address of Marketplace who can interact with this contract.
     * @param _marketPlace allowed marketplace that can call specific functions in this contract
     */
    function setAllowedMarketPlace(
        address _marketPlace
    ) external onlyMaintainer {
        _storageParams._setMarketPlace(_marketPlace);
        emit AllowedMarketplaceUpdated(_marketPlace);
    }

    /**
     * @notice changes the address of CustomVault where to swap propertyBaseCurrency to XTokens
     * @param _vault CustomVault.sol address where the conversion b/w propertyBaseCurrency to XTokens and reverse is done
     */
    function setCustomVault(address _vault) external onlyMaintainer {
        _storageParams._setCustomVault(_vault);
        emit CustomVaultUpdated(_vault);
    }

    /**
     * @notice for Marketplace: this borrows X-Tokens from ERC4626 vault and convert to baseCurrency and give to Marketplace
     * @param _tokensToBorrowWithoutFees amount of baseCurrency required
     * @param _propertyToken address of WLegal
     * @param _noOfTokens number of proerty tokens transferred to ERC4626Vault from Marketplace in this tx
     */
    function borrowTokens(
        uint256 _tokensToBorrowWithoutFees,
        address _propertyToken,
        uint256 _noOfTokens
    ) external {
        uint256 actualBorrowAmount = _storageParams._borrowTokensFromPool(
            _tokensToBorrowWithoutFees
        );
        addBorrowPosition(_propertyToken, actualBorrowAmount, _noOfTokens);
    }

    /**
     * @notice for Marketplace: this converts baseCurrency to x-Tokens using CustomVault and repays to ERC4626 Vault and buys Property tokens from ERC4626Vault
     * @param _propertyToken address of WLegal
     * @param _repayAmount amount of baseCurrency that is being paid to ERC4626Vault
     * @param _currentPertokenPrice current price of unit Property token
     * @param _propertyBasecurrency address of baseCurrency of property (jTRY in this case)
     */
    function buyPropertyTokens(
        address _propertyToken,
        uint256 _repayAmount,
        uint256 _currentPertokenPrice,
        address _propertyBasecurrency
    ) external {
        uint256 _amountOfTokens = _repayAmount / _currentPertokenPrice;
        _storageParams._buyPropertyTokensForMP(
            _propertyToken,
            _amountOfTokens,
            _repayAmount,
            _propertyBasecurrency
        );

        uint256 remaining = repay(
            _repayAmount,
            _propertyToken,
            _currentPertokenPrice,
            _storageParams.poolToBorrowFrom
        );
        _storageParams._repayToCustomVault(_repayAmount, lastProfitSentToGauge);

        _storageParams._afterRepay(remaining);
    }

    /**
     * @param _newPool address of ERC4626Vault where to borrow x-Tokens from.
     */
    function updatePoolTOBorrowFromAddress(
        address _newPool
    ) external onlyMaintainer {
        _storageParams._updatePoolToBorrowFromAddress(_newPool);
        emit PoolToBorrowFromUpdated(_newPool);
    }

    /**
     * @return poolToBorrowFrom address of ERC4626Vault where to borrow x-Tokens from.
     */
    function getPoolToBorrowFromAddress()
        external
        view
        returns (address poolToBorrowFrom)
    {
        poolToBorrowFrom = _storageParams.poolToBorrowFrom;
    }

    /**
     * @return allowedMarketPlace address of Marketplace who can borrow and buyProperty tokens using this contract.
     */
    function getAllowedMarketplaceAddress()
        external
        view
        returns (address allowedMarketPlace)
    {
        allowedMarketPlace = _storageParams.allowedMarketPlace;
    }
}

// SPDX-License-Identifier: GPL-3.0

pragma solidity 0.8.9;
import "./Interfaces/IERC4626StakingPool.sol";

/**
 * @title PositionManager
 * @notice To keep track of borrow positions b/w ERC4626 and Marketplace and Profit and loss.
 */
contract PositionManager {
    struct BorrowPosition {
        uint256 borrowedAmount;
        uint256 borrowAmountFilled;
        uint256 profitEarned;
        address wLegalAddress;
        uint256 tokenSold;
        uint256 lossFilled;
        uint256 noOfTokens;
        uint256 actualCost;
    }

    /// @notice maps property to all borrow positions this property has.
    mapping(address => BorrowPosition[]) public propertyToPositions;

    /// @notice maps property to last repaid (not neccassarily full) borrow position index
    mapping(address => uint256) public propertyToBorrowCursor;

    /// @notice last amount of profit sent to gauge
    uint public lastProfitSentToGauge;

    /**
     * @notice adds borrow position and keeps track how property tokens are bought by ERC4626Vault and sent x-tokens to Marketplace
     * @param wLegalAddress address of WrappedLegal bought bu ERC4626Vault
     * @param  borrowAmount amount of xTokens sent to MarketplaceBorrower in order to buy property tokens
     * @param  noOfTokens number of property tokens bought by ERC4626Vault in this tx.
     */
    function addBorrowPosition(
        address wLegalAddress,
        uint256 borrowAmount,
        uint256 noOfTokens
    ) internal returns (uint256) {
        BorrowPosition memory p;
        p.borrowedAmount = borrowAmount;
        p.noOfTokens = noOfTokens;
        p.actualCost = borrowAmount / noOfTokens;
        p.wLegalAddress = wLegalAddress;
        propertyToPositions[wLegalAddress].push(p);
        return propertyToPositions[wLegalAddress].length - 1;
    }

    /**
     * @notice updates/closes the positiosn whose borrowed amount has been repaid
     * @param repayAmount amount repaid
     * @param  wLegalAddress address of WLegal token
     * @param  currentPertokenPrice current price of WLegal to calculate profit/loss.
     * @param  poolToBorrowFrom address of ERC4626Vault
     * @return remaining if there remains any amount after repaying all the positions
     */
    function repay(
        uint256 repayAmount,
        address wLegalAddress,
        uint256 currentPertokenPrice,
        address poolToBorrowFrom
    ) internal returns (uint256 remaining) {
        lastProfitSentToGauge = 0;
        // 4500
        remaining = repayAmount;

        BorrowPosition storage _positions;
        //  15 = 4500 / 300
        uint256 sharesCounter = repayAmount / currentPertokenPrice;

        uint256 priceDiff;
        //uint maxProfitOfPosition;
        //uint maxLossOfPosition;
        uint256 totalProfit;
        uint256 totalLoss;
        uint256 pendingAmount;
        uint256 pendingShares;
        uint256 matched;
        uint256 acutalSale;
        while (remaining > 0) {
            _positions = propertyToPositions[wLegalAddress][
                propertyToBorrowCursor[wLegalAddress]
            ];
            // true = 300 >= 175
            bool flag = currentPertokenPrice >= _positions.actualCost; // true means profit: false means loss

            // to found how many shares will be sold in this position.
            // 15  === [10,5]
            // 20 == [10,10]
            // 2 = [2]
            pendingShares = _positions.noOfTokens - _positions.tokenSold;
            // 10
            acutalSale = pendingShares > sharesCounter
                ? sharesCounter
                : pendingShares;
            _positions.tokenSold += acutalSale;
            sharesCounter -= acutalSale;

            if (flag) {
                priceDiff = currentPertokenPrice - _positions.actualCost;
                totalProfit = priceDiff * acutalSale;
            }
            // when loss
            else {
                // we will loss when we even dont receive back our invested amount
                priceDiff = _positions.actualCost - currentPertokenPrice;
                totalLoss = acutalSale * priceDiff;
            }
            // is transaction total profit / loss
            //when profit
            if (flag) {
                //  3000 - 110
                // totalIpust - profit
                if (totalProfit <= remaining) {
                    _positions.profitEarned += totalProfit;
                    remaining -= totalProfit;
                } else {
                    _positions.profitEarned += remaining;
                    remaining -= remaining;
                }
            } else {
                _positions.lossFilled += totalLoss;
            }
            // 2890

            pendingAmount =
                _positions.borrowedAmount -
                _positions.lossFilled -
                _positions.borrowAmountFilled;
            matched = remaining > pendingAmount ? pendingAmount : remaining;
            _positions.borrowAmountFilled += matched;
            remaining -= matched;

            if (
                // 2890 = 0+  2890
                _positions.borrowedAmount ==
                _positions.lossFilled + _positions.borrowAmountFilled ||
                // _positions.profitEarned == profitEarned ||
                // 10 * 300 = 1250+ 1750
                _positions.noOfTokens * currentPertokenPrice ==
                _positions.profitEarned + _positions.borrowAmountFilled
            ) {
                /// Notifiy reward against filled position
                calculateAndNotifyProfitOrLoss(_positions, poolToBorrowFrom);

                propertyToBorrowCursor[wLegalAddress]++;
            }
            if (checkToBreakLoop(wLegalAddress)) {
                break;
            }
        }
        return remaining;
    }

    /**
     * @notice Calculates and notify profit or loss made for the position.
     * @param _position position that is being repayed
     * @param _poolToBorrowFrom address of ERC4626Vault
     */
    function calculateAndNotifyProfitOrLoss(
        BorrowPosition memory _position,
        address _poolToBorrowFrom
    ) internal {
        if (_position.profitEarned == _position.lossFilled) return;

        if (_position.profitEarned > _position.lossFilled) {
            uint256 profitToRealize = _position.profitEarned -
                _position.lossFilled;
            // profit sent to gauge
            lastProfitSentToGauge += IERC4626StakingPool(_poolToBorrowFrom)
                .notiftyRewardToGauge(profitToRealize);
        } else {
            uint256 lossToRealize = _position.lossFilled -
                _position.profitEarned;
            IERC4626StakingPool(_poolToBorrowFrom).decreaseAssetTotalSupply(
                lossToRealize
            );
        }
    }

    /**
     * @notice checks whether if there are further positions to be repaid
     * @return true: when there are no positions remaining to repay. false: there are still position remaining
     */
    function checkToBreakLoop(
        address wLegalAddress
    ) internal view returns (bool) {
        return
            propertyToBorrowCursor[wLegalAddress] >
            propertyToPositions[wLegalAddress].length - 1;
    }

    /**
     * @notice _property address of WLegal Token
     * @return total borrow positions this property has so far.
     */
    function getPropertyTotalPositions(
        address _property
    ) external view returns (uint) {
        return propertyToPositions[_property].length;
    }
}