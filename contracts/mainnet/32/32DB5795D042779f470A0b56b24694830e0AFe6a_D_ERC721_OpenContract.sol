// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (utils/Address.sol)

pragma solidity ^0.8.1;

/**
 * @dev Collection of functions related to the address type
 */
library Address {
    /**
     * @dev Returns true if `account` is a contract.
     *
     * [IMPORTANT]
     * ====
     * It is unsafe to assume that an address for which this function returns
     * false is an externally-owned account (EOA) and not a contract.
     *
     * Among others, `isContract` will return false for the following
     * types of addresses:
     *
     *  - an externally-owned account
     *  - a contract in construction
     *  - an address where a contract will be created
     *  - an address where a contract lived, but was destroyed
     *
     * Furthermore, `isContract` will also return true if the target contract within
     * the same transaction is already scheduled for destruction by `SELFDESTRUCT`,
     * which only has an effect at the end of a transaction.
     * ====
     *
     * [IMPORTANT]
     * ====
     * You shouldn't rely on `isContract` to protect against flash loan attacks!
     *
     * Preventing calls from contracts is highly discouraged. It breaks composability, breaks support for smart wallets
     * like Gnosis Safe, and does not provide security since it can be circumvented by calling from a contract
     * constructor.
     * ====
     */
    function isContract(address account) internal view returns (bool) {
        // This method relies on extcodesize/address.code.length, which returns 0
        // for contracts in construction, since the code is only stored at the end
        // of the constructor execution.

        return account.code.length > 0;
    }

    /**
     * @dev Replacement for Solidity's `transfer`: sends `amount` wei to
     * `recipient`, forwarding all available gas and reverting on errors.
     *
     * https://eips.ethereum.org/EIPS/eip-1884[EIP1884] increases the gas cost
     * of certain opcodes, possibly making contracts go over the 2300 gas limit
     * imposed by `transfer`, making them unable to receive funds via
     * `transfer`. {sendValue} removes this limitation.
     *
     * https://consensys.net/diligence/blog/2019/09/stop-using-soliditys-transfer-now/[Learn more].
     *
     * IMPORTANT: because control is transferred to `recipient`, care must be
     * taken to not create reentrancy vulnerabilities. Consider using
     * {ReentrancyGuard} or the
     * https://solidity.readthedocs.io/en/v0.8.0/security-considerations.html#use-the-checks-effects-interactions-pattern[checks-effects-interactions pattern].
     */
    function sendValue(address payable recipient, uint256 amount) internal {
        require(address(this).balance >= amount, "Address: insufficient balance");

        (bool success, ) = recipient.call{value: amount}("");
        require(success, "Address: unable to send value, recipient may have reverted");
    }

    /**
     * @dev Performs a Solidity function call using a low level `call`. A
     * plain `call` is an unsafe replacement for a function call: use this
     * function instead.
     *
     * If `target` reverts with a revert reason, it is bubbled up by this
     * function (like regular Solidity function calls).
     *
     * Returns the raw returned data. To convert to the expected return value,
     * use https://solidity.readthedocs.io/en/latest/units-and-global-variables.html?highlight=abi.decode#abi-encoding-and-decoding-functions[`abi.decode`].
     *
     * Requirements:
     *
     * - `target` must be a contract.
     * - calling `target` with `data` must not revert.
     *
     * _Available since v3.1._
     */
    function functionCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, "Address: low-level call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`], but with
     * `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        return functionCallWithValue(target, data, 0, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but also transferring `value` wei to `target`.
     *
     * Requirements:
     *
     * - the calling contract must have an ETH balance of at least `value`.
     * - the called Solidity function must be `payable`.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(address target, bytes memory data, uint256 value) internal returns (bytes memory) {
        return functionCallWithValue(target, data, value, "Address: low-level call with value failed");
    }

    /**
     * @dev Same as {xref-Address-functionCallWithValue-address-bytes-uint256-}[`functionCallWithValue`], but
     * with `errorMessage` as a fallback revert reason when `target` reverts.
     *
     * _Available since v3.1._
     */
    function functionCallWithValue(
        address target,
        bytes memory data,
        uint256 value,
        string memory errorMessage
    ) internal returns (bytes memory) {
        require(address(this).balance >= value, "Address: insufficient balance for call");
        (bool success, bytes memory returndata) = target.call{value: value}(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(address target, bytes memory data) internal view returns (bytes memory) {
        return functionStaticCall(target, data, "Address: low-level static call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a static call.
     *
     * _Available since v3.3._
     */
    function functionStaticCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        (bool success, bytes memory returndata) = target.staticcall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(address target, bytes memory data) internal returns (bytes memory) {
        return functionDelegateCall(target, data, "Address: low-level delegate call failed");
    }

    /**
     * @dev Same as {xref-Address-functionCall-address-bytes-string-}[`functionCall`],
     * but performing a delegate call.
     *
     * _Available since v3.4._
     */
    function functionDelegateCall(
        address target,
        bytes memory data,
        string memory errorMessage
    ) internal returns (bytes memory) {
        (bool success, bytes memory returndata) = target.delegatecall(data);
        return verifyCallResultFromTarget(target, success, returndata, errorMessage);
    }

    /**
     * @dev Tool to verify that a low level call to smart-contract was successful, and revert (either by bubbling
     * the revert reason or using the provided one) in case of unsuccessful call or if target was not a contract.
     *
     * _Available since v4.8._
     */
    function verifyCallResultFromTarget(
        address target,
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal view returns (bytes memory) {
        if (success) {
            if (returndata.length == 0) {
                // only check isContract if the call was successful and the return data is empty
                // otherwise we already know that it was a contract
                require(isContract(target), "Address: call to non-contract");
            }
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    /**
     * @dev Tool to verify that a low level call was successful, and revert if it wasn't, either by bubbling the
     * revert reason or using the provided one.
     *
     * _Available since v4.3._
     */
    function verifyCallResult(
        bool success,
        bytes memory returndata,
        string memory errorMessage
    ) internal pure returns (bytes memory) {
        if (success) {
            return returndata;
        } else {
            _revert(returndata, errorMessage);
        }
    }

    function _revert(bytes memory returndata, string memory errorMessage) private pure {
        // Look for revert reason and bubble it up if present
        if (returndata.length > 0) {
            // The easiest way to bubble the revert reason is using memory via assembly
            /// @solidity memory-safe-assembly
            assembly {
                let returndata_size := mload(returndata)
                revert(add(32, returndata), returndata_size)
            }
        } else {
            revert(errorMessage);
        }
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/Context.sol)

pragma solidity ^0.8.0;

/**
 * @dev Provides information about the current execution context, including the
 * sender of the transaction and its data. While these are generally available
 * via msg.sender and msg.data, they should not be accessed in such a direct
 * manner, since when dealing with meta-transactions the account sending and
 * paying for execution may not be the actual sender (as far as an application
 * is concerned).
 *
 * This contract is only required for intermediate, library-like contracts.
 */
abstract contract Context {
    function _msgSender() internal view virtual returns (address) {
        return msg.sender;
    }

    function _msgData() internal view virtual returns (bytes calldata) {
        return msg.data;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/ERC165.sol)

pragma solidity ^0.8.0;

import "./IERC165.sol";

/**
 * @dev Implementation of the {IERC165} interface.
 *
 * Contracts that want to implement ERC165 should inherit from this contract and override {supportsInterface} to check
 * for the additional interface id that will be supported. For example:
 *
 * ```solidity
 * function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
 *     return interfaceId == type(MyInterface).interfaceId || super.supportsInterface(interfaceId);
 * }
 * ```
 *
 * Alternatively, {ERC165Storage} provides an easier to use but more expensive implementation.
 */
abstract contract ERC165 is IERC165 {
    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC165).interfaceId;
    }
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts v4.4.1 (utils/introspection/IERC165.sol)

pragma solidity ^0.8.0;

/**
 * @dev Interface of the ERC165 standard, as defined in the
 * https://eips.ethereum.org/EIPS/eip-165[EIP].
 *
 * Implementers can declare support of contract interfaces, which can then be
 * queried by others ({ERC165Checker}).
 *
 * For an implementation, see {ERC165}.
 */
interface IERC165 {
    /**
     * @dev Returns true if this contract implements the interface defined by
     * `interfaceId`. See the corresponding
     * https://eips.ethereum.org/EIPS/eip-165#how-interfaces-are-identified[EIP section]
     * to learn more about how these ids are created.
     *
     * This function call must use less than 30 000 gas.
     */
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

// SPDX-License-Identifier: MIT
// OpenZeppelin Contracts (last updated v4.9.0) (utils/structs/EnumerableSet.sol)
// This file was procedurally generated from scripts/generate/templates/EnumerableSet.js.

pragma solidity ^0.8.0;

/**
 * @dev Library for managing
 * https://en.wikipedia.org/wiki/Set_(abstract_data_type)[sets] of primitive
 * types.
 *
 * Sets have the following properties:
 *
 * - Elements are added, removed, and checked for existence in constant time
 * (O(1)).
 * - Elements are enumerated in O(n). No guarantees are made on the ordering.
 *
 * ```solidity
 * contract Example {
 *     // Add the library methods
 *     using EnumerableSet for EnumerableSet.AddressSet;
 *
 *     // Declare a set state variable
 *     EnumerableSet.AddressSet private mySet;
 * }
 * ```
 *
 * As of v3.3.0, sets of type `bytes32` (`Bytes32Set`), `address` (`AddressSet`)
 * and `uint256` (`UintSet`) are supported.
 *
 * [WARNING]
 * ====
 * Trying to delete such a structure from storage will likely result in data corruption, rendering the structure
 * unusable.
 * See https://github.com/ethereum/solidity/pull/11843[ethereum/solidity#11843] for more info.
 *
 * In order to clean an EnumerableSet, you can either remove all elements one by one or create a fresh instance using an
 * array of EnumerableSet.
 * ====
 */
library EnumerableSet {
    // To implement this library for multiple types with as little code
    // repetition as possible, we write it in terms of a generic Set type with
    // bytes32 values.
    // The Set implementation uses private functions, and user-facing
    // implementations (such as AddressSet) are just wrappers around the
    // underlying Set.
    // This means that we can only create new EnumerableSets for types that fit
    // in bytes32.

    struct Set {
        // Storage of set values
        bytes32[] _values;
        // Position of the value in the `values` array, plus 1 because index 0
        // means a value is not in the set.
        mapping(bytes32 => uint256) _indexes;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function _add(Set storage set, bytes32 value) private returns (bool) {
        if (!_contains(set, value)) {
            set._values.push(value);
            // The value is stored at length-1, but we add 1 to all indexes
            // and use 0 as a sentinel value
            set._indexes[value] = set._values.length;
            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function _remove(Set storage set, bytes32 value) private returns (bool) {
        // We read and store the value's index to prevent multiple reads from the same storage slot
        uint256 valueIndex = set._indexes[value];

        if (valueIndex != 0) {
            // Equivalent to contains(set, value)
            // To delete an element from the _values array in O(1), we swap the element to delete with the last one in
            // the array, and then remove the last element (sometimes called as 'swap and pop').
            // This modifies the order of the array, as noted in {at}.

            uint256 toDeleteIndex = valueIndex - 1;
            uint256 lastIndex = set._values.length - 1;

            if (lastIndex != toDeleteIndex) {
                bytes32 lastValue = set._values[lastIndex];

                // Move the last value to the index where the value to delete is
                set._values[toDeleteIndex] = lastValue;
                // Update the index for the moved value
                set._indexes[lastValue] = valueIndex; // Replace lastValue's index to valueIndex
            }

            // Delete the slot where the moved value was stored
            set._values.pop();

            // Delete the index for the deleted slot
            delete set._indexes[value];

            return true;
        } else {
            return false;
        }
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function _contains(Set storage set, bytes32 value) private view returns (bool) {
        return set._indexes[value] != 0;
    }

    /**
     * @dev Returns the number of values on the set. O(1).
     */
    function _length(Set storage set) private view returns (uint256) {
        return set._values.length;
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function _at(Set storage set, uint256 index) private view returns (bytes32) {
        return set._values[index];
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function _values(Set storage set) private view returns (bytes32[] memory) {
        return set._values;
    }

    // Bytes32Set

    struct Bytes32Set {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _add(set._inner, value);
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(Bytes32Set storage set, bytes32 value) internal returns (bool) {
        return _remove(set._inner, value);
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(Bytes32Set storage set, bytes32 value) internal view returns (bool) {
        return _contains(set._inner, value);
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(Bytes32Set storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(Bytes32Set storage set, uint256 index) internal view returns (bytes32) {
        return _at(set._inner, index);
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(Bytes32Set storage set) internal view returns (bytes32[] memory) {
        bytes32[] memory store = _values(set._inner);
        bytes32[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }

    // AddressSet

    struct AddressSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(AddressSet storage set, address value) internal returns (bool) {
        return _add(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(AddressSet storage set, address value) internal returns (bool) {
        return _remove(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(AddressSet storage set, address value) internal view returns (bool) {
        return _contains(set._inner, bytes32(uint256(uint160(value))));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(AddressSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(AddressSet storage set, uint256 index) internal view returns (address) {
        return address(uint160(uint256(_at(set._inner, index))));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(AddressSet storage set) internal view returns (address[] memory) {
        bytes32[] memory store = _values(set._inner);
        address[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }

    // UintSet

    struct UintSet {
        Set _inner;
    }

    /**
     * @dev Add a value to a set. O(1).
     *
     * Returns true if the value was added to the set, that is if it was not
     * already present.
     */
    function add(UintSet storage set, uint256 value) internal returns (bool) {
        return _add(set._inner, bytes32(value));
    }

    /**
     * @dev Removes a value from a set. O(1).
     *
     * Returns true if the value was removed from the set, that is if it was
     * present.
     */
    function remove(UintSet storage set, uint256 value) internal returns (bool) {
        return _remove(set._inner, bytes32(value));
    }

    /**
     * @dev Returns true if the value is in the set. O(1).
     */
    function contains(UintSet storage set, uint256 value) internal view returns (bool) {
        return _contains(set._inner, bytes32(value));
    }

    /**
     * @dev Returns the number of values in the set. O(1).
     */
    function length(UintSet storage set) internal view returns (uint256) {
        return _length(set._inner);
    }

    /**
     * @dev Returns the value stored at position `index` in the set. O(1).
     *
     * Note that there are no guarantees on the ordering of values inside the
     * array, and it may change when more values are added or removed.
     *
     * Requirements:
     *
     * - `index` must be strictly less than {length}.
     */
    function at(UintSet storage set, uint256 index) internal view returns (uint256) {
        return uint256(_at(set._inner, index));
    }

    /**
     * @dev Return the entire set in an array
     *
     * WARNING: This operation will copy the entire storage to memory, which can be quite expensive. This is designed
     * to mostly be used by view accessors that are queried without any gas fees. Developers should keep in mind that
     * this function has an unbounded cost, and using it as part of a state-changing function may render the function
     * uncallable if the set grows to a point where copying to memory consumes too much gas to fit in a block.
     */
    function values(UintSet storage set) internal view returns (uint256[] memory) {
        bytes32[] memory store = _values(set._inner);
        uint256[] memory result;

        /// @solidity memory-safe-assembly
        assembly {
            result := store
        }

        return result;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {LibDiamond} from "../libraries/LibDiamond.sol";

import "../interfaces/IERC721.sol";
import "../interfaces/IERC721Receiver.sol";
import "../interfaces/IERC721Metadata.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/utils/Context.sol";
import "@openzeppelin/contracts/utils/introspection/ERC165.sol";

/**
 * @title D_ERC721_OpenContract contract.
 * NOTE: Contract allows Admin of the contract mint ERC721 standard tokens for different addresses, burn them,
 * and dynamically change the metadata of these tokens.
 * Metadata is updated through a providing new-URI system.
 * In order to change URI Admin needs to provide a new URI for the token in changeNFT().
 */
contract D_ERC721_OpenContract is Context, ERC165, IERC721, IERC721Metadata {
    using Address for address;

    /**
     * @dev Modifier to allow only owner to perform certain actions.
     */
    modifier onlyOwner() {
        require(
            LibDiamond.contractOwner() == msg.sender,
            "D_ERC721_OpenContract: Caller is not the owner"
        );
        _;
    }

    /**
     * @dev Emitted when new token minted.
     * @param receiver Address of the token owner.
     * @param uri Initial URI of the token.
     * @param backendTokenKey Backend token key.
     * @param tokenId Id of the created token.
     */
    event DynamicERC721Minted(
        address receiver,
        string uri,
        string backendTokenKey,
        uint256 tokenId
    );

    /**
     * @dev Emitted when the token URI is updated.
     * @param tokenId Id of the updated token.
     * @param newUri URI of the token after changing.
     * @param owner The owner of the updated token.
     */
    event NFTmetadataChanged(uint256 tokenId, string newUri, address owner);

    /**
     * @dev Emitted when token burn occurs.
     * @param tokenId Id of the burned token.
     * @param owner The owner of the burnt token.
     */
    event TokenBurned(uint256 tokenId, address owner);

    /**
     * @dev This is a function to mint unique ERC721 tokens with dynamic URI. Available only for the Admin.
     *
     * Requirements:
     *
     * - The caller must be an `DEFAULT_ADMIN_ROLE`.
     * - `account_` cannot be address zero.
     * - The `uri_` cannot be empty.
     * - `backendTokenKey_` in `LibDiamond.diamondStorage().erc721Storage.backendTokenKeyToId` mapping required to not exist at the moment of minting.
     *
     * @param account_ The address where the token is to be minted.
     * @param uri_ Initial URI of hte token.
     * @param backendTokenKey_ Token key provided from backend.
     *
     * Emits a {DynamicERC721Minted} event.
     */
    function mint(
        address account_,
        string memory uri_,
        string memory backendTokenKey_
    ) external onlyOwner {
        // Validate input
        require(
            bytes(uri_).length != 0,
            "D_ERC721_OpenContract: Uri cannot be empty"
        );

        LibDiamond.ERC721Storage storage ds = _getERC721Storage();
        require(
            ds.backendTokenKeyToId[backendTokenKey_] == 0,
            "D_ERC721_OpenContract: Mint need to be unique"
        );

        // Generate new token
        uint256 newTokenId = ds.tokenId++;
        ds.tokens[newTokenId].uri = uri_;
        ds.backendTokenKeyToId[backendTokenKey_] = newTokenId;
        ds.keyFallback.push(backendTokenKey_);

        // Mint token
        _safeMint(account_, newTokenId);
        emit DynamicERC721Minted(account_, uri_, backendTokenKey_, newTokenId);
    }

    /**
     * @dev This is a function to change NFT metadata. A dynamic type NFT can change NFT metadatas and
     * can be called only by  Admin.
     *
     * Requirements:
     *
     * - The caller must be an `DEFAULT_ADMIN_ROLE`.
     * - `backendTokenKey_` must be greater then 0, otherwise token doesnt exist in `LibDiamond.diamondStorage().erc721Storage.backendTokenKeyToId` mapping.
     * - `uri_` must be lower then length of `levelUri[]` in `Token` struct.
     * - Token must exist.
     *
     * @param backendTokenKey_ Token key provided from backend.
     * @param uri_ Points to a value from the `levelUri[]` to which the token metadata will be updated.
     *
     * Emits a {NFTmetadataChanged} event.
     */
    function changeNFT(
        string memory backendTokenKey_,
        string memory uri_
    ) external onlyOwner {
        LibDiamond.ERC721Storage storage ds = _getERC721Storage();
        require(
            ds.backendTokenKeyToId[backendTokenKey_] != 0,
            "D_ERC721_OpenContract: No token exist with this name"
        );
        require(bytes(uri_).length != 0, "D_ERC721_OpenContract: URI is empty");
        uint256 tokenId = ds.backendTokenKeyToId[backendTokenKey_];
        require(_exists(tokenId), "D_ERC721_OpenContract: Token nonexistent");
        ds.tokens[tokenId].uri = uri_;
        emit NFTmetadataChanged(tokenId, uri_, ownerOf(tokenId));
    }

    /**
     * @dev Burns `tokenId_` if the Admin calls the function.
     *
     * Requirements:
     *
     * - The caller must be an `DEFAULT_ADMIN_ROLE`.
     * - Token must exist.
     *
     * @param tokenId_ Token Id of the token to be burned.
     *
     * Emits a {TokenBurned} event.
     */
    function burn(uint256 tokenId_) external onlyOwner {
        require(_exists(tokenId_), "D_ERC721_OpenContract: Token nonexistent");
        address ownerOfToken = ownerOf(tokenId_);
        _burn(tokenId_);
        emit TokenBurned(tokenId_, ownerOfToken);
    }

    /**
     * @dev Returns owner of the token. Available only for the Admin.
     *
     * @param backendTokenKey_ Token key provided by backend.
     */
    function tokenOwner(
        string memory backendTokenKey_
    ) external view returns (address) {
        uint256 tokenId = getTokenKeyId(backendTokenKey_);
        return ownerOf(tokenId);
    }

    /**
     * @dev Returns Token struct with actual URI data by providing tokenId.
     */
    function getTokenData(
        uint256 tokenId_
    ) external view returns (LibDiamond.Token memory) {
        return LibDiamond.Token({uri: _getTokenURI(tokenId_)});
    }

    /**
     * @dev Returns array of all token keys.
     */
    function getTokenKeyList() external view returns (string[] memory) {
        return _getERC721Storage().keyFallback;
    }

    /**
     * @dev Returns contract Uri.
     */
    function contractURI() external view returns (string memory) {
        return _getERC721Storage().contractUri;
    }

    /**
     * @dev Returns maximum supply.
     */
    function getMaximumSupply() public view returns (uint256) {
        return _getERC721Storage().maximumSupply;
    }

    /**
     * @dev This is a function to query a Uri for an individual token by providing the tokenId.
     *
     * Requirements:
     *
     * - Token must exist.
     *
     * @param tokenId_ Token Id of the token for the URI query.
     */
    function tokenURI(
        uint256 tokenId_
    ) public view virtual override returns (string memory) {
        require(
            _exists(tokenId_),
            "D_ERC721_OpenContract: URI query for nonexistent token"
        );
        return _getTokenURI(tokenId_);
    }

    /**
     * @dev See {IERC165-supportsInterface}.
     */
    function supportsInterface(
        bytes4 interfaceId
    ) public view virtual override(ERC165, IERC165) returns (bool) {
        return
            interfaceId == type(IERC721).interfaceId ||
            interfaceId == type(IERC721Metadata).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    /**
     * @dev See {IERC721-balanceOf}.
     */
    function balanceOf(
        address owner
    ) public view virtual override returns (uint256) {
        require(
            owner != address(0),
            "ERC721: address zero is not a valid owner"
        );
        return _getERC721Storage().balances[owner];
    }

    /**
     * @dev See {IERC721-ownerOf}.
     */
    function ownerOf(
        uint256 tokenId
    ) public view virtual override returns (address) {
        address owner = _ownerOf(tokenId);
        require(owner != address(0), "ERC721: invalid token ID");
        return owner;
    }

    /**
     * @dev See {IERC721Metadata-name}.
     */
    function name() public view virtual override returns (string memory) {
        return _getERC721Storage().name;
    }

    /**
     * @dev See {IERC721Metadata-symbol}.
     */
    function symbol() public view virtual override returns (string memory) {
        return _getERC721Storage().symbol;
    }

    /**
     * @dev See {IERC721-approve}.
     */
    function approve(address to, uint256 tokenId) public virtual override {
        address owner = ownerOf(tokenId);
        require(to != owner, "ERC721: approval to current owner");

        require(
            _msgSender() == owner || isApprovedForAll(owner, _msgSender()),
            "ERC721: approve caller is not token owner or approved for all"
        );

        _approve(to, tokenId);
    }

    /**
     * @dev See {IERC721-getApproved}.
     */
    function getApproved(
        uint256 tokenId
    ) public view virtual override returns (address) {
        _requireMinted(tokenId);

        return _getERC721Storage().tokenApprovals[tokenId];
    }

    /**
     * @dev See {IERC721-setApprovalForAll}.
     */
    function setApprovalForAll(
        address operator,
        bool approved
    ) public virtual override {
        _setApprovalForAll(_msgSender(), operator, approved);
    }

    /**
     * @dev See {IERC721-isApprovedForAll}.
     */
    function isApprovedForAll(
        address owner,
        address operator
    ) public view virtual override returns (bool) {
        return _getERC721Storage().operatorApprovals[owner][operator];
    }

    /**
     * @dev See {IERC721-transferFrom}.
     */
    function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        //solhint-disable-next-line max-line-length
        require(
            _isApprovedOrOwner(_msgSender(), tokenId) ||
                LibDiamond.contractOwner() == msg.sender,
            "ERC721: caller is not token owner, contract owner or approved"
        );

        _transfer(from, to, tokenId);
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        safeTransferFrom(from, to, tokenId, "");
    }

    /**
     * @dev See {IERC721-safeTransferFrom}.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) public virtual override {
        require(
            _isApprovedOrOwner(_msgSender(), tokenId) ||
                LibDiamond.contractOwner() == msg.sender,
            "ERC721: caller is not token owner, contract owner or approved"
        );

        _safeTransfer(from, to, tokenId, data);
    }

    /**
     * @dev Returns TokenId by providing backend token key.
     * @param tokenKey_ The key of the token in the backend.
     * @return The ID of the token.
     */
    function getTokenKeyId(
        string memory tokenKey_
    ) public view returns (uint256) {
        return _getERC721Storage().backendTokenKeyToId[tokenKey_];
    }

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * `data` is additional data, it has no specified format and it is sent in call to `to`.
     *
     * This internal function is equivalent to {safeTransferFrom}, and can be used to e.g.
     * implement alternative mechanisms to perform token transfer, such as signature-based.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeTransfer(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) internal virtual {
        _transfer(from, to, tokenId);
        require(
            _checkOnERC721Received(from, to, tokenId, data),
            "ERC721: transfer to non ERC721Receiver implementer"
        );
    }

    /**
     * @dev Returns the ERC721Storage.
     * @return ERC721Storage.
     */
    function _getERC721Storage()
        internal
        view
        returns (LibDiamond.ERC721Storage storage)
    {
        return LibDiamond.diamondStorage().erc721Storage;
    }

    /**
     * @dev Returns the token storage.
     * @param tokenId The ID of the token.
     * @return Token storage.
     */
    function _getTokenStorage(
        uint256 tokenId
    ) internal view returns (LibDiamond.Token storage) {
        return _getERC721Storage().tokens[tokenId];
    }

    /**
     * @dev Returns the URI of a given token ID.
     * @param tokenId The ID of the token.
     * @return The URI of the token.
     */
    function _getTokenURI(
        uint256 tokenId
    ) internal view returns (string memory) {
        require(
            _exists(tokenId),
            "D_ERC721_OpenContract: URI query for nonexistent token"
        );
        return _getTokenStorage(tokenId).uri;
    }

    /**
     * @dev Base URI for computing {tokenURI}. If set, the resulting URI for each
     * token will be the concatenation of the `baseURI` and the `tokenId`. Empty
     * by default, can be overridden in child contracts.
     */
    function _baseURI() internal view virtual returns (string memory) {
        return "";
    }

    /**
     * @dev Returns the owner of the `tokenId`. Does NOT revert if token doesn't exist
     */
    function _ownerOf(uint256 tokenId) internal view virtual returns (address) {
        return _getERC721Storage().owners[tokenId];
    }

    /**
     * @dev Returns whether `tokenId` exists.
     *
     * Tokens can be managed by their owner or approved accounts via {approve} or {setApprovalForAll}.
     *
     * Tokens start existing when they are minted (`_mint`),
     * and stop existing when they are burned (`_burn`).
     */
    function _exists(uint256 tokenId) internal view virtual returns (bool) {
        return _ownerOf(tokenId) != address(0);
    }

    /**
     * @dev Returns whether `spender` is allowed to manage `tokenId`.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function _isApprovedOrOwner(
        address spender,
        uint256 tokenId
    ) internal view virtual returns (bool) {
        address owner = ownerOf(tokenId);
        return (spender == owner ||
            isApprovedForAll(owner, spender) ||
            getApproved(tokenId) == spender);
    }

    /**
     * @dev Safely mints `tokenId` and transfers it to `to`.
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function _safeMint(address to, uint256 tokenId) internal virtual {
        _safeMint(to, tokenId, "");
    }

    /**
     * @dev Same as {xref-ERC721-_safeMint-address-uint256-}[`_safeMint`], with an additional `data` parameter which is
     * forwarded in {IERC721Receiver-onERC721Received} to contract recipients.
     */
    function _safeMint(
        address to,
        uint256 tokenId,
        bytes memory data
    ) internal virtual {
        _mint(to, tokenId);
        require(
            _checkOnERC721Received(address(0), to, tokenId, data),
            "ERC721: transfer to non ERC721Receiver implementer"
        );
    }

    /**
     * @dev Mints `tokenId` and transfers it to `to`.
     *
     * WARNING: Usage of this method is discouraged, use {_safeMint} whenever possible
     *
     * Requirements:
     *
     * - `tokenId` must not exist.
     * - `to` cannot be the zero address.
     *
     * Emits a {Transfer} event.
     */
    function _mint(address to, uint256 tokenId) internal virtual {
        require(to != address(0), "ERC721: mint to the zero address");
        require(!_exists(tokenId), "ERC721: token already minted");

        _beforeTokenTransfer(address(0), to, tokenId, 1);

        // Check that tokenId was not minted by `_beforeTokenTransfer` hook
        require(!_exists(tokenId), "ERC721: token already minted");
        LibDiamond.ERC721Storage storage ds = _getERC721Storage();
        unchecked {
            // Will not overflow unless all 2**256 token ids are minted to the same owner.
            // Given that tokens are minted one by one, it is impossible in practice that
            // this ever happens. Might change if we allow batch minting.
            // The ERC fails to describe this case.
            ds.balances[to] += 1;
        }

        ds.owners[tokenId] = to;

        emit Transfer(address(0), to, tokenId);

        _afterTokenTransfer(address(0), to, tokenId, 1);
    }

    /**
     * @dev Destroys `tokenId`.
     * The approval is cleared when the token is burned.
     * This is an internal function that does not check if the sender is authorized to operate on the token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     *
     * Emits a {Transfer} event.
     */
    function _burn(uint256 tokenId) internal virtual {
        address owner = ownerOf(tokenId);

        _beforeTokenTransfer(owner, address(0), tokenId, 1);

        // Update ownership in case tokenId was transferred by `_beforeTokenTransfer` hook
        owner = ownerOf(tokenId);
        LibDiamond.ERC721Storage storage ds = _getERC721Storage();
        // Clear approvals
        delete ds.tokenApprovals[tokenId];

        unchecked {
            // Cannot overflow, as that would require more tokens to be burned/transferred
            // out than the owner initially received through minting and transferring in.
            ds.balances[owner] -= 1;
        }
        delete ds.owners[tokenId];

        emit Transfer(owner, address(0), tokenId);

        _afterTokenTransfer(owner, address(0), tokenId, 1);
    }

    /**
     * @dev Transfers `tokenId` from `from` to `to`.
     *  As opposed to {transferFrom}, this imposes no restrictions on msg.sender.
     *
     * Requirements:
     *
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     *
     * Emits a {Transfer} event.
     */
    function _transfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual {
        require(
            ownerOf(tokenId) == from,
            "ERC721: transfer from incorrect owner"
        );
        require(to != address(0), "ERC721: transfer to the zero address");
        _beforeTokenTransfer(from, to, tokenId, 1);
        LibDiamond.ERC721Storage storage ds = _getERC721Storage();
        // Check that tokenId was not transferred by `_beforeTokenTransfer` hook
        require(
            ownerOf(tokenId) == from ||
                LibDiamond.contractOwner() == msg.sender,
            "ERC721: transfer from incorrect owner"
        );

        // Clear approvals from the previous owner
        delete ds.tokenApprovals[tokenId];

        unchecked {
            // `LibDiamond.diamondStorage().erc721Storage.balances[from]` cannot overflow for the same reason as described in `_burn`:
            // `from`'s balance is the number of token held, which is at least one before the current
            // transfer.
            // `LibDiamond.diamondStorage().erc721Storage.balances[to]` could overflow in the conditions described in `_mint`. That would require
            // all 2**256 token ids to be minted, which in practice is impossible.
            ds.balances[from] -= 1;
            ds.balances[to] += 1;
        }
        ds.owners[tokenId] = to;

        emit Transfer(from, to, tokenId);

        _afterTokenTransfer(from, to, tokenId, 1);
    }

    /**
     * @dev Approve `to` to operate on `tokenId`
     *
     * Emits an {Approval} event.
     */
    function _approve(address to, uint256 tokenId) internal virtual {
        _getERC721Storage().tokenApprovals[tokenId] = to;
        emit Approval(ownerOf(tokenId), to, tokenId);
    }

    /**
     * @dev Approve `operator` to operate on all of `owner` tokens
     *
     * Emits an {ApprovalForAll} event.
     */
    function _setApprovalForAll(
        address owner,
        address operator,
        bool approved
    ) internal virtual {
        require(owner != operator, "ERC721: approve to caller");
        _getERC721Storage().operatorApprovals[owner][operator] = approved;
        emit ApprovalForAll(owner, operator, approved);
    }

    /**
     * @dev Reverts if the `tokenId` has not been minted yet.
     */
    function _requireMinted(uint256 tokenId) internal view virtual {
        require(_exists(tokenId), "ERC721: invalid token ID");
    }

    /**
     * @dev Internal function to invoke {IERC721Receiver-onERC721Received} on a target address.
     * The call is not executed if the target address is not a contract.
     *
     * @param from address representing the previous owner of the given token ID
     * @param to target address that will receive the tokens
     * @param tokenId uint256 ID of the token to be transferred
     * @param data bytes optional data to send along with the call
     * @return bool whether the call correctly returned the expected magic value
     */
    function _checkOnERC721Received(
        address from,
        address to,
        uint256 tokenId,
        bytes memory data
    ) private returns (bool) {
        if (to.isContract()) {
            try
                IERC721Receiver(to).onERC721Received(
                    _msgSender(),
                    from,
                    tokenId,
                    data
                )
            returns (bytes4 retval) {
                return retval == IERC721Receiver.onERC721Received.selector;
            } catch (bytes memory reason) {
                if (reason.length == 0) {
                    revert(
                        "ERC721: transfer to non ERC721Receiver implementer"
                    );
                } else {
                    /// @solidity memory-safe-assembly
                    assembly {
                        revert(add(32, reason), mload(reason))
                    }
                }
            }
        } else {
            return true;
        }
    }

    /**
     * @dev Hook that is called before any token transfer. This includes minting and burning. If {ERC721Consecutive} is
     * used, the hook may be called as part of a consecutive (batch) mint, as indicated by `batchSize` greater than 1.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s tokens will be transferred to `to`.
     * - When `from` is zero, the tokens will be minted for `to`.
     * - When `to` is zero, ``from``'s tokens will be burned.
     * - `from` and `to` are never both zero.
     * - `batchSize` is non-zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 firstTokenId,
        uint256 batchSize
    ) internal virtual {}

    /**
     * @dev Hook that is called after any token transfer. This includes minting and burning. If {ERC721Consecutive} is
     * used, the hook may be called as part of a consecutive (batch) mint, as indicated by `batchSize` greater than 1.
     *
     * Calling conditions:
     *
     * - When `from` and `to` are both non-zero, ``from``'s tokens were transferred to `to`.
     * - When `from` is zero, the tokens were minted for `to`.
     * - When `to` is zero, ``from``'s tokens were burned.
     * - `from` and `to` are never both zero.
     * - `batchSize` is non-zero.
     *
     * To learn more about hooks, head to xref:ROOT:extending-contracts.adoc#using-hooks[Using Hooks].
     */
    function _afterTokenTransfer(
        address from,
        address to,
        uint256 firstTokenId,
        uint256 batchSize
    ) internal virtual {}

    /**
     * @dev Unsafe write access to the balances, used by extensions that "mint" tokens using an {ownerOf} override.
     *
     * WARNING: Anyone calling this MUST ensure that the balances remain consistent with the ownership. The invariant
     * being that for any address `a` the value returned by `balanceOf(a)` must be equal to the number of tokens such
     * that `ownerOf(tokenId)` is `a`.
     */
    // solhint-disable-next-line func-name-mixedcase
    function __unsafe_increaseBalance(
        address account,
        uint256 amount
    ) internal {
        _getERC721Storage().balances[account] += amount;
    }
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

/**
 * @title Diamond Cut Interface
 * @dev This interface lays out the structure for adding, replacing, or removing functions
 * in a Diamond contract architecture. It also includes the logic for executing a function
 * with delegatecall.
 */
interface IDiamondCut {
    enum FacetCutAction {
        Add,
        Replace,
        Remove
    }
    // Add=0, Replace=1, Remove=2

    struct FacetCut {
        address facetAddress;
        FacetCutAction action;
        bytes4[] functionSelectors;
    }

    /**
     * @notice This function allows to add, replace, or remove any number of functions
     * and optionally execute a function with delegatecall.
     * @param diamondCut_ An array of FacetCut structs containing the facet addresses
     * and function selectors for the cut.
     * @param init_ The address of the contract or facet to execute calldata_.
     * @param calldata_ A bytes array containing the function call data,
     * including function selector and arguments. calldata_ is executed with delegatecall on init_.
     */
    function diamondCut(FacetCut[] calldata diamondCut_, address init_, bytes calldata calldata_) external;

    /**
     * @dev Emitted after a successful `diamondCut` operation.
     * @param diamondCut_ The array of FacetCut structs that was passed to the function.
     * @param init_ The address of the contract or facet that was executed with delegatecall.
     * @param calldata_ The function call data that was passed to the function.
     */
    event DiamondCut(FacetCut[] diamondCut_, address init_, bytes calldata_);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";

/**
 * @dev Required interface of an ERC721 compliant contract.
 */
interface IERC721 is IERC165 {
    /**
     * @dev Emitted when `tokenId` token is transferred from `from` to `to`.
     */
    event Transfer(
        address indexed from,
        address indexed to,
        uint256 indexed tokenId
    );

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `tokenId` token.
     */
    event Approval(
        address indexed owner,
        address indexed approved,
        uint256 indexed tokenId
    );

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its assets.
     */
    event ApprovalForAll(
        address indexed owner,
        address indexed operator,
        bool approved
    );

    /**
     * @dev Returns the number of tokens in ``owner``'s account.
     */
    function balanceOf(address owner) external view returns (uint256 balance);

    /**
     * @dev Returns the owner of the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function ownerOf(uint256 tokenId) external view returns (address owner);

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId,
        bytes calldata data
    ) external;

    /**
     * @dev Safely transfers `tokenId` token from `from` to `to`, checking first that contract recipients
     * are aware of the ERC721 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must exist and be owned by `from`.
     * - If the caller is not `from`, it must have been allowed to move this token by either {approve} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it must implement {IERC721Receiver-onERC721Received}, which is called upon a safe transfer.
     *
     * Emits a {Transfer} event.
     */
    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) external;

    /**
     * @dev Transfers `tokenId` token from `from` to `to`.
     *
     * WARNING: Note that the caller is responsible to confirm that the recipient is capable of receiving ERC721
     * or else they may be permanently lost. Usage of {safeTransferFrom} prevents loss, though the caller must
     * understand this adds an external call which potentially creates a reentrancy vulnerability.
     *
     * Requirements:
     *
     * - `from` cannot be the zero address.
     * - `to` cannot be the zero address.
     * - `tokenId` token must be owned by `from`.
     * - If the caller is not `from`, it must be approved to move this token by either {approve} or {setApprovalForAll}.
     *
     * Emits a {Transfer} event.
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to transfer `tokenId` token to another account.
     * The approval is cleared when the token is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved operator.
     * - `tokenId` must exist.
     *
     * Emits an {Approval} event.
     */
    function approve(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferFrom} or {safeTransferFrom} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns the account approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getApproved(
        uint256 tokenId
    ) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the assets of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(
        address owner,
        address operator
    ) external view returns (bool);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional metadata extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
interface IERC721Metadata is IERC721 {
    /**
     * @dev Returns the token collection name.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the token collection symbol.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the Uniform Resource Identifier (URI) for `tokenId` token.
     */
    function tokenURI(uint256 tokenId) external view returns (string memory);
}

// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/**
 * @title ERC721 token receiver interface
 * @dev Interface for any contract that wants to support safeTransfers
 * from ERC721 asset contracts.
 */
interface IERC721Receiver {
    /**
     * @dev Whenever an {IERC721} `tokenId` token is transferred to this contract via {IERC721-safeTransferFrom}
     * by `operator` from `from`, this function is called.
     *
     * It must return its Solidity selector to confirm the token transfer.
     * If any other value is returned or the interface is not implemented by the recipient, the transfer will be reverted.
     *
     * The selector can be obtained in Solidity with `IERC721Receiver.onERC721Received.selector`.
     */
    function onERC721Received(
        address operator,
        address from,
        uint256 tokenId,
        bytes calldata data
    ) external returns (bytes4);
}

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {IDiamondCut} from "../interfaces/IDiamondCut.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

/// Remember to add the loupe functions from DiamondLoupeFacet to the diamond.
/// The loupe functions are required by the EIP2535 Diamonds standard

error InitializationFunctionReverted(
    address _initializationContractAddress,
    bytes _calldata
);

/**
 * @title LibDiamond - A library implementing the EIP-2535 Diamond Standard
 * @dev This library is responsible for managing the storage and functionality related to diamonds.
 * It provides functions for adding, updating, and removing facets and their function selectors,
 * as well as managing contract ownership and supported interfaces.
 */
library LibDiamond {
    struct Token {
        string uri;
    }

    /// Basic storage for ERC721 tokens
    struct ERC721Storage {
        /// replace _owners mapping in OpenZeppelin contract
        mapping(uint256 => address) owners;
        ///replace _balances mapping in OpenZeppelin contract
        mapping(address => uint256) balances;
        /// replace _tokenApprovals mapping in OpenZeppelin contract
        mapping(uint256 => address) tokenApprovals;
        /// replace _operatorApprovals mapping in OpenZeppelin contract
        mapping(address => mapping(address => bool)) operatorApprovals;
        /// tokenId => token URI
        mapping(uint256 => string) tokenIdToURI;
        uint256 tokenId;
        uint256 maximumSupply;
        string name;
        string symbol;
        string contractUri;
        /// backend keys
        string[] keyFallback;
        /// Mapping from token ID to Token struct.
        mapping(uint256 => Token) tokens;
        /// Mapping from backend Token Key to Token Id.
        mapping(string => uint256) backendTokenKeyToId;
        ///Field for storing timestampses
        mapping(uint256 => uint256) tokenTimestamps;
    }

    /// Basic storage for ERC721 tokens
    struct ERC1155Storage {
        // Mapping from token ID to account balances
        mapping(uint256 => mapping(address => uint256)) balances;
        // Mapping from account to operator approvals
        mapping(address => mapping(address => bool)) operatorApprovals;
        mapping(uint256 => string) uris;
        // Used as the URI for all token types by relying on ID substitution, e.g. https://token-cdn-domain/{id}.json
        string uri;
    }

    /// Basic storage for ERC20 tokens
    struct ERC20Storage {
        mapping(address => uint256) balances;
        mapping(address => mapping(address => uint256)) allowances;
        uint256 totalSupply;
        string name;
        string symbol;
        uint8 decimals;
    }

    /// Item for Marketplace
    struct Item {
        uint256 id;
        string name;
        string description;
        uint256 price;
        uint256 itemTimestamp;
    }
    /// Basic storage for a marketplace
    struct MarketplaceStorage {
        /// itemId => Item struct
        mapping(uint256 => Item) items;
        /// owner => list of items owned
        mapping(address => uint256[]) ownerItems;
        ///itemId => owner
        mapping(uint256 => address) itemOwners;
        /// itemId => price
        mapping(uint256 => uint256) itemPrices;
        mapping(address => EnumerableSet.UintSet) ownedItems;
    }

    struct CustomType {
        /// Custom type
        uint256 exampleField1;
        address exampleField2;
    }

    /// Generic Struct storage to accommodate any type of contract
    struct GenericStorage {
        /// Dynamic key-value storage for uints
        mapping(bytes32 => uint256) uintStorage;
        /// Dynamic key-value storage for addresses
        mapping(bytes32 => address) addressStorage;
        /// Dynamic key-value storage for bytes
        mapping(bytes32 => bytes) bytesStorage;
        /// Dynamic key-value storage for strings
        mapping(bytes32 => string) stringStorage;
        /// Dynamic key-value storage for bools
        mapping(bytes32 => bool) boolStorage;
        /// Nested mapping
        mapping(bytes32 => mapping(bytes32 => uint256)) nestedMappingStorage;
        /// Custom type storage
        mapping(bytes32 => CustomType) customTypeStorage;
        /// Custom type array storage
        mapping(bytes32 => CustomType[]) customTypeArrayStorage;
        /// Uint array storage
        mapping(bytes32 => uint256[]) uintArrayStorage;
        /// EnumerableSet storage
        mapping(bytes32 => EnumerableSet.UintSet) enumerableUintSetStorage;
    }

    bytes32 constant DIAMOND_STORAGE_POSITION =
        keccak256("diamond.standard.diamond.storage");

    struct DiamondStorage {
        /// Maps function selectors to the facets that execute the functions
        /// and maps the selectors to their position in the selectorSlots array
        /// func selector => (address facet, selector position)
        mapping(bytes4 => bytes32) facets;
        /// Array of slots holding function selectors, with each slot containing 8 selectors
        mapping(uint256 => bytes32) selectorSlots;
        /// The total number of function selectors in selectorSlots
        uint16 selectorCount;
        /// A mapping used to query if a contract implements an interface
        /// This is utilized for ERC-165 implementation
        mapping(bytes4 => bool) supportedInterfaces;
        /// The owner of the contract
        address contractOwner;
        /// marketplace storage
        MarketplaceStorage marketplaceStorage;
        /// erc721 storage
        ERC721Storage erc721Storage;
        /// erc20 storage
        ERC20Storage erc20Storage;
        /// erc1155 storage
        ERC1155Storage erc1155Storage;
        /// generic storage that can be used by any contract
        GenericStorage genericStorage;
    }

    /**
     * @notice Retrieves the DiamondStorage struct instance that holds the
     *         storage data for the diamond contract.
     *
     * @dev This function utilizes assembly to access the storage slot where
     *      the DiamondStorage struct data is stored.
     *
     * @return ds The DiamondStorage struct instance containing the contract's storage data
     */
    function diamondStorage()
        internal
        pure
        returns (DiamondStorage storage ds)
    {
        bytes32 position = DIAMOND_STORAGE_POSITION;
        assembly {
            ds.slot := position
        }
    }

    event OwnershipTransferred(
        address indexed previousOwner,
        address indexed newOwner
    );

    /**
     * @notice Transfers ownership of the diamond contract to a new owner.
     *
     * @dev This internal function updates the contract owner in the DiamondStorage struct
     *      and emits an OwnershipTransferred event.
     *
     * @param newOwner_ The address of the new owner to whom ownership is being transferred
     *
     * Emits an {OwnershipTransferred} event.
     */
    function setContractOwner(address newOwner_) internal {
        DiamondStorage storage ds = diamondStorage();
        address previousOwner = ds.contractOwner;
        ds.contractOwner = newOwner_;
        emit OwnershipTransferred(previousOwner, newOwner_);
    }

    /**
     * @notice Gets the current owner of the diamond contract.
     *
     * @dev This internal view function retrieves the contract owner from the DiamondStorage struct.
     *
     * @return contractOwner_ The address of the current contract owner.
     */
    function contractOwner() internal view returns (address contractOwner_) {
        contractOwner_ = diamondStorage().contractOwner;
    }

    /**
     * @notice Verifies that the caller of the function is the contract owner.
     *
     * @dev This internal view function checks if the sender is the contract owner stored
     *      in the DiamondStorage struct, and reverts if the condition is not met.
     *
     * Reverts with "LibDiamond: Must be contract owner" if the sender is not the contract owner.
     */
    function enforceIsContractOwner() internal view {
        require(
            msg.sender == diamondStorage().contractOwner,
            "LibDiamond: Must be contract owner"
        );
    }

    event DiamondCut(
        IDiamondCut.FacetCut[] _diamondCut,
        address init_,
        bytes _calldata
    );

    /// A constant mask used to clear the address part of a bytes32 value
    bytes32 constant CLEAR_ADDRESS_MASK =
        bytes32(uint256(0xffffffffffffffffffffffff));
    /// A constant mask used to clear the function selector part of a bytes32 value
    bytes32 constant CLEAR_SELECTOR_MASK = bytes32(uint256(0xffffffff << 224));

    /**
     * @notice Executes an internal diamond cut, modifying the contract's facets by adding,
     *         replacing, or removing functions, and optionally initializing a facet or contract.
     * @dev This internal version of diamondCut is almost identical to the external version,
     *      but it uses a memory array instead of a calldata array. This approach avoids copying
     *      calldata to memory, which would result in errors for two-dimensional arrays. The
     *      function iterates through the _diamondCut array, performing actions as specified
     *      and updating the contract's selector slots accordingly.
     *
     *      Note: This code is almost the same as the external diamondCut,
     *      except it is using 'Facet[] memory _diamondCut' instead of
     *      'Facet[] calldata _diamondCut'.
     *      The code is duplicated to prevent copying calldata to memory which
     *      causes an error for a two-dimensional array.
     *
     * @param diamondCut_ An array of FacetCut structs containing facet addresses, actions, and
     *                    function selectors to be added, replaced, or removed
     * @param init_ The address of the contract or facet to execute calldata_ using delegatecall
     * @param calldata_ Encoded function call, including function selector and arguments, to be
     *                  executed using delegatecall on init_
     *
     * Emits a {DiamondCut} event.
     *
     * Requirements:
     * - The `diamondCut_` array must not be empty.
     * - The `init_` address must contain contract code if it is non-zero.
     * - If an add action is performed, the function selector must not already exist.
     * - If a replace action is performed, the function selector must exist and cannot be replaced with the same function.
     * - If a remove action is performed, the function selector must exist and the `init_` address must be a zero address.
     * - The contract must have enough storage to store the new function selectors.
     */
    function diamondCut(
        IDiamondCut.FacetCut[] memory diamondCut_,
        address init_,
        bytes memory calldata_
    ) internal {
        DiamondStorage storage ds = diamondStorage();
        uint256 originalSelectorCount = ds.selectorCount;
        uint256 selectorCount = originalSelectorCount;
        bytes32 selectorSlot;
        /// Determine if the last selector slot is not fully occupied
        /// Efficient modulo by eight using bitwise AND
        if (selectorCount & 7 > 0) {
            /// Retrieve the last selectorSlot using bitwise shift for efficient division by 8
            selectorSlot = ds.selectorSlots[selectorCount >> 3];
        }
        /// Iterate through the diamond cut array
        for (uint256 facetIndex; facetIndex < diamondCut_.length; ) {
            (selectorCount, selectorSlot) = addReplaceRemoveFacetSelectors(
                selectorCount,
                selectorSlot,
                diamondCut_[facetIndex].facetAddress,
                diamondCut_[facetIndex].action,
                diamondCut_[facetIndex].functionSelectors
            );

            unchecked {
                facetIndex++;
            }
        }
        if (selectorCount != originalSelectorCount) {
            ds.selectorCount = uint16(selectorCount);
        }
        /// Check if the last selector slot is not fully occupied
        /// Efficient modulo by eight using bitwise AND
        if (selectorCount & 7 > 0) {
            /// Update the selector slot using bitwise shift for efficient division by 8
            ds.selectorSlots[selectorCount >> 3] = selectorSlot;
        }
        emit DiamondCut(diamondCut_, init_, calldata_);
        initializeDiamondCut(init_, calldata_);
    }

    /**
     * @dev This internal function adds, replaces, or removes function selectors for facets based on the action provided.
     *      This function ensures the selectors are properly stored in the contract's storage and maintain the gas efficient design.
     *      It also checks for valid inputs, ensuring that facets and selectors conform to the requirements of each action.
     *
     * @param selectorCount_ The current count of total selectors. This value is adjusted based on the action taken.
     * @param selectorSlot_ The current selector slot. This value is adjusted based on the action taken.
     * @param newFacetAddress_ The address of the new facet to be added or replaced. It must be address(0) when removing facets.
     * @param action_ The action to execute, which can be adding, replacing, or removing a facet.
     * @param selectors_ Array of function selectors to be added, replaced, or removed in the facet.
     *
     * @return selectorCount_ The updated count of total selectors after the function execution.
     * @return selectorSlot_ The updated selector slot after the function execution.
     *
     * Requirements:
     * - The `selectors_` array must not be empty.
     * - In the case of adding a new facet, the `newFacetAddress_` must not be a zero address, and the facet must contain code.
     * - In the case of replacing a facet, the `newFacetAddress_` must not be a zero address, and the facet must contain code. The function to be replaced must exist, and cannot be the same as the replacement function.
     * - In the case of removing a facet, the `newFacetAddress_` must be a zero address. The function to be removed must exist.
     */

    function addReplaceRemoveFacetSelectors(
        uint256 selectorCount_,
        bytes32 selectorSlot_,
        address newFacetAddress_,
        IDiamondCut.FacetCutAction action_,
        bytes4[] memory selectors_
    ) internal returns (uint256, bytes32) {
        DiamondStorage storage ds = diamondStorage();
        require(
            selectors_.length > 0,
            "LibDiamondCut: No selectors in facet to cut"
        );
        if (action_ == IDiamondCut.FacetCutAction.Add) {
            enforceHasContractCode(
                newFacetAddress_,
                "LibDiamondCut: Add facet has no code"
            );
            for (uint256 selectorIndex; selectorIndex < selectors_.length; ) {
                bytes4 selector = selectors_[selectorIndex];
                bytes32 oldFacet = ds.facets[selector];
                require(
                    address(bytes20(oldFacet)) == address(0),
                    "LibDiamondCut: Can't add function that already exists"
                );

                /// Adding the facet address and the selector count to the facet
                ds.facets[selector] =
                    bytes20(newFacetAddress_) |
                    bytes32(selectorCount_);

                /// Utilizing bitwise operations for efficient modulo by 8 and multiplication by 32
                uint256 selectorInSlotPosition = (selectorCount_ & 7) << 5;

                /// Clearing the selector's position in the slot and adding the selector
                selectorSlot_ =
                    (selectorSlot_ &
                        ~(CLEAR_SELECTOR_MASK >> selectorInSlotPosition)) |
                    (bytes32(selector) >> selectorInSlotPosition);

                /// If the slot is filled, then it is written to storage
                if (selectorInSlotPosition == 224) {
                    /// Utilizing bitwise operation for efficient division by 8
                    ds.selectorSlots[selectorCount_ >> 3] = selectorSlot_;
                    selectorSlot_ = 0;
                }

                selectorCount_++;

                unchecked {
                    selectorIndex++;
                }
            }
        } else if (action_ == IDiamondCut.FacetCutAction.Replace) {
            enforceHasContractCode(
                newFacetAddress_,
                "LibDiamondCut: Replace facet has no code"
            );
            for (uint256 selectorIndex; selectorIndex < selectors_.length; ) {
                bytes4 selector = selectors_[selectorIndex];
                bytes32 oldFacet = ds.facets[selector];
                address oldFacetAddress = address(bytes20(oldFacet));

                /// This check is relevant if immutable functions are present
                require(
                    oldFacetAddress != address(this),
                    "LibDiamondCut: Immutable functions cannot be replaced"
                );

                /// Prevents replacement of a function with an identical one
                require(
                    oldFacetAddress != newFacetAddress_,
                    "LibDiamondCut: A function cannot be replaced with the same function"
                );

                /// Ensures the function to be replaced exists
                require(
                    oldFacetAddress != address(0),
                    "LibDiamondCut: Non-existent functions cannot be replaced"
                );

                /// Substituting the old facet address with the new one
                ds.facets[selector] =
                    (oldFacet & CLEAR_ADDRESS_MASK) |
                    bytes20(newFacetAddress_);

                unchecked {
                    selectorIndex++;
                }
            }
        } else if (action_ == IDiamondCut.FacetCutAction.Remove) {
            /// The address for the removed facet should be null
            require(
                newFacetAddress_ == address(0),
                "LibDiamondCut: Address for removed facet must be null address"
            );

            /// "selectorCount_ >> 3" is a computational optimization for division by 8
            uint256 selectorSlotCount = selectorCount_ >> 3;

            /// "selectorCount_ & 7" is a computational optimization for modulo by eight
            uint256 selectorInSlotIndex = selectorCount_ & 7;
            for (uint256 selectorIndex; selectorIndex < selectors_.length; ) {
                if (selectorSlot_ == 0) {
                    /// Retrieve the last selectorSlot
                    selectorSlotCount--;
                    selectorSlot_ = ds.selectorSlots[selectorSlotCount];
                    selectorInSlotIndex = 7;
                } else {
                    selectorInSlotIndex--;
                }
                bytes4 lastSelector;
                uint256 oldSelectorsSlotCount;
                uint256 oldSelectorInSlotPosition;

                /// Adding this block helps to avoid 'Stack too deep' error
                {
                    bytes4 selector = selectors_[selectorIndex];
                    bytes32 oldFacet = ds.facets[selector];

                    /// Check if function to remove exists
                    require(
                        address(bytes20(oldFacet)) != address(0),
                        "LibDiamondCut: Function to remove does not exist"
                    );

                    /// Immutable functions cannot be removed
                    require(
                        address(bytes20(oldFacet)) != address(this),
                        "LibDiamondCut: Immutable function cannot be removed"
                    );

                    /// Retrieve the last selector
                    /// " << 5" is a computational optimization for multiplication by 32
                    lastSelector = bytes4(
                        selectorSlot_ << (selectorInSlotIndex << 5)
                    );

                    if (lastSelector != selector) {
                        /// Update the last selector's slot position info
                        ds.facets[lastSelector] =
                            (oldFacet & CLEAR_ADDRESS_MASK) |
                            bytes20(ds.facets[lastSelector]);
                    }

                    /// Remove the selector from the facets
                    delete ds.facets[selector];

                    uint256 oldSelectorCount = uint16(uint256(oldFacet));

                    /// "oldSelectorCount >> 3" is a computational optimization for division by 8
                    oldSelectorsSlotCount = oldSelectorCount >> 3;

                    /// "oldSelectorCount & 7" is a computational optimization for modulo by eight
                    /// " << 5" is a computational optimization for multiplication by 32
                    oldSelectorInSlotPosition = (oldSelectorCount & 7) << 5;
                }
                if (oldSelectorsSlotCount != selectorSlotCount) {
                    bytes32 oldSelectorSlot = ds.selectorSlots[
                        oldSelectorsSlotCount
                    ];

                    /// Clear the selector being deleted and replace it with the last selector
                    oldSelectorSlot =
                        (oldSelectorSlot &
                            ~(CLEAR_SELECTOR_MASK >>
                                oldSelectorInSlotPosition)) |
                        (bytes32(lastSelector) >> oldSelectorInSlotPosition);

                    /// Update the storage with the modified slot
                    ds.selectorSlots[oldSelectorsSlotCount] = oldSelectorSlot;
                } else {
                    /// Clear the selector being deleted and replace it with the last selector
                    selectorSlot_ =
                        (selectorSlot_ &
                            ~(CLEAR_SELECTOR_MASK >>
                                oldSelectorInSlotPosition)) |
                        (bytes32(lastSelector) >> oldSelectorInSlotPosition);
                }
                if (selectorInSlotIndex == 0) {
                    delete ds.selectorSlots[selectorSlotCount];
                    selectorSlot_ = 0;
                }

                unchecked {
                    selectorIndex++;
                }
            }
            selectorCount_ = selectorSlotCount * 8 + selectorInSlotIndex;
        } else {
            revert("LibDiamondCut: Incorrect FacetCutAction");
        }
        return (selectorCount_, selectorSlot_);
    }

    /**
     * @dev This internal function is used to initialize a diamond cut. It performs a delegate call to
     *      the provided address with the given calldata. This is typically used to call a function on
     *      a facet that sets initial state in the diamond storage.
     *
     * @param init_ The address of the contract to delegate call. This address should contain the logic
     *              that needs to be executed for the initialization. If it is address(0), the function
     *              returns without doing anything.
     * @param calldata_ The calldata to be passed to the delegate call. This should include the
     *                  function selector for the initialization function and any necessary parameters.
     *
     * @notice If the delegate call is not successful, the function will revert. If the call returns
     *         an error message, it will be bubbled up and reverted with. Otherwise, it will revert
     *         with the `InitializationFunctionReverted` error, which includes the `init_` address
     *         and the `calldata_`.
     *
     * Requirements:
     * - The `init_` address must contain contract code. If it is a zero address or an address without
     *   contract code, the function will revert with the "LibDiamondCut: init_ address has no code"
     *   error.
     */
    function initializeDiamondCut(
        address init_,
        bytes memory calldata_
    ) internal {
        if (init_ == address(0)) {
            return;
        }
        enforceHasContractCode(
            init_,
            "LibDiamondCut: init_ address has no code"
        );
        (bool success, bytes memory error) = init_.delegatecall(calldata_);
        if (!success) {
            if (error.length > 0) {
                /// bubble up error
                /// @solidity memory-safe-assembly
                /// Use inline assembly to load the size of the error message and revert with it.
                assembly {
                    let returndata_size := mload(error)
                    revert(add(32, error), returndata_size)
                }
            } else {
                revert InitializationFunctionReverted(init_, calldata_);
            }
        }
    }

    /**
     * @dev This internal function checks if the provided address (contract_) contains contract code.
     *      It uses low-level EVM instructions to access the contract size directly.
     *      If the contract size is 0 (meaning there's no contract code at the address), it reverts with the provided error message.
     *
     * @param contract_ The address to be checked for the presence of contract code.
     * @param errorMessage_ The error message to be reverted with if there's no contract code at the provided address.
     *
     * Requirements:
     * - The `contract_` must contain contract code. If not, it reverts with the provided `errorMessage_`.
     */
    function enforceHasContractCode(
        address contract_,
        string memory errorMessage_
    ) internal view {
        uint256 contractSize;
        /// Using EVM assembly to get the size of the code at address `contract_`
        assembly {
            contractSize := extcodesize(contract_)
        }

        /// Reverting if the contract size is zero (i.e., the address does not contain contract code)
        require(contractSize > 0, errorMessage_);
    }
}